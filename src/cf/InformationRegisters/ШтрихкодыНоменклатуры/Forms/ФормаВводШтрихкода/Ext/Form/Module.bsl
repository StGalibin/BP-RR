#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПодборМаркируемойПродукции = Параметры.ИспользоватьПодборМаркируемойПродукции;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШтрихкодАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = ДанныеВыбораНаСервере(Текст, ИспользоватьПодборМаркируемойПродукции);
	
КонецПроцедуры

&НаКлиенте
Процедура ШтрихкодПриИзменении(Элемент)
	Результат = ДанныеВыбораПоШтрихкодуНаСервере(Штрихкод, ИспользоватьПодборМаркируемойПродукции);
	Если ЗначениеЗаполнено(Результат) Тогда
		Закрыть(Результат);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьЗначение(Команда)
	Результат = ДанныеВыбораПоШтрихкодуНаСервере(Штрихкод, ИспользоватьПодборМаркируемойПродукции);
	
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыбор(Команда)
	Закрыть(Неопределено);
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДанныеВыбораНаСервере(Текст, ИспользоватьПодборМаркируемойПродукции)
	ДанныеВыбора = Новый СписокЗначений;
	
	НоменклатураПоШтрихкоду = РегистрыСведений.ШтрихкодыНоменклатуры.НоменклатураПоШтрихкоду(Текст);
	Для каждого СтрокаНоменклатуры Из НоменклатураПоШтрихкоду Цикл
		ДанныеВыбора.Добавить(СтрокаНоменклатуры.Штрихкод, СтрокаНоменклатуры.Представление);
	КонецЦикла;
	
	Если ИспользоватьПодборМаркируемойПродукции Тогда
		НомераКиз = Справочники.КонтрольныеЗнакиГИСМ.ПодобратьКИЗ_ГИСМПоШтрихкоду(Текст);
		Для каждого НомерКиз Из НомераКиз Цикл
			ДанныеВыбора.Добавить(НомерКиз.Штрихкод, НомерКиз.Представление);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДанныеВыбора;

КонецФункции 

&НаСервереБезКонтекста
Функция ДанныеВыбораПоШтрихкодуНаСервере(Штрихкод, ИспользоватьПодборМаркируемойПродукции)

	Если ИспользоватьПодборМаркируемойПродукции
		И ИнтеграцияГИСМКлиентСервер.ЭтоНомерКиЗ(Штрихкод) Тогда
		ДанныеКиЗ = Справочники.КонтрольныеЗнакиГИСМ.ДанныеКИЗ_ГИСМПоНомеру(Штрихкод);
		//Если соответствия нет -вводи вручную, выбрав номенклатуру
		Если ДанныеКиЗ <> Неопределено Тогда
			Возврат Новый Структура("Номенклатура, КИЗ_ГИСМ", ДанныеКиЗ.Владелец, ДанныеКиЗ.КИЗ_ГИСМ);
		КонецЕсли;
	Иначе
		ТаблицаНоменклатуры = РегистрыСведений.ШтрихкодыНоменклатуры.НоменклатураПоШтрихкоду(Штрихкод);
		Если ТаблицаНоменклатуры.Количество() = 1 Тогда
			Возврат Новый Структура("Номенклатура, КИЗ_ГИСМ", ТаблицаНоменклатуры[0].Номенклатура, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти 



