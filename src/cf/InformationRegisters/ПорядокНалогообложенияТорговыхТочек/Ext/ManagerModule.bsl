#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
Функция ОсобыйПорядокНалогообложенияНаСкладе(Склад, Дата) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПорядокНалогообложенияТорговыхТочекСрезПоследних.ОсобыйПорядокНалогообложения
	|ИЗ
	|	РегистрСведений.ПорядокНалогообложенияТорговыхТочек.СрезПоследних(&Дата, Склад = &Склад) КАК ПорядокНалогообложенияТорговыхТочекСрезПоследних";
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() = 0 Тогда 
		Возврат Ложь;
	Иначе
		Возврат Результат[0].ОсобыйПорядокНалогообложения;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьПорядокНалогообложенияТорговыхТочек() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПорядокНалогообложенияТорговыхТочек.Период,
	|	ПорядокНалогообложенияТорговыхТочек.Склад,
	|	ПорядокНалогообложенияТорговыхТочек.ОсобыйПорядокНалогообложения
	|ИЗ
	|	РегистрСведений.ПорядокНалогообложенияТорговыхТочек КАК ПорядокНалогообложенияТорговыхТочек";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КонецТекущегоГода", КонецГода(ТекущаяДата()));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ ВТ_Склады
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.ТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.НеавтоматизированнаяТорговаяТочка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПриходныйКассовыйОрдер.Ссылка КАК Регистратор,
	|	ПриходныйКассовыйОрдер.Контрагент КАК Склад
	|ПОМЕСТИТЬ ВТ_Документы
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК ПриходныйКассовыйОрдер
	|ГДЕ
	|	ПриходныйКассовыйОрдер.Проведен
	|	И ПриходныйКассовыйОрдер.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПКО.РозничнаяВыручка)
	|	И ПриходныйКассовыйОрдер.Контрагент В
	|			(ВЫБРАТЬ
	|				ВТ_Склады.Склад
	|			ИЗ
	|				ВТ_Склады)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОплатаПлатежнойКартой.Ссылка,
	|	ОплатаПлатежнойКартой.Контрагент
	|ИЗ
	|	Документ.ОплатаПлатежнойКартой КАК ОплатаПлатежнойКартой
	|ГДЕ
	|	ОплатаПлатежнойКартой.Проведен
	|	И ОплатаПлатежнойКартой.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОплатаПлатежнойКартой.РозничнаяВыручка)
	|	И ОплатаПлатежнойКартой.Контрагент В
	|			(ВЫБРАТЬ
	|				ВТ_Склады.Склад
	|			ИЗ
	|				ВТ_Склады)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Склады.Склад КАК Склад
	|ИЗ
	|	ВТ_Склады КАК ВТ_Склады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(УдалитьСчетаУчетаВНТТ.Период, МЕСЯЦ) КАК Период,
	|	УдалитьСчетаУчетаВНТТ.ТорговаяТочка КАК Склад,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА УдалитьСчетаУчетаВНТТ.СчетДоходовОтРеализации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
	|					ИЛИ УдалитьСчетаУчетаВНТТ.СчетРасходовОтРеализации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажЕНВД)
	|				ТОГДА ИСТИНА
	|			КОГДА УдалитьСчетаУчетаВНТТ.СчетДоходовОтРеализации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД)
	|					ИЛИ УдалитьСчетаУчетаВНТТ.СчетРасходовОтРеализации = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СебестоимостьПродажНеЕНВД)
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОсобыйПорядокНалогообложения,
	|	КОНЕЦПЕРИОДА(ВЫБОР
	|			КОГДА ВТ_СоответствиеПериодов.ПериодСледующий ЕСТЬ NULL 
	|					И УдалитьСчетаУчетаВНТТ.Период <= &КонецТекущегоГода
	|				ТОГДА &КонецТекущегоГода
	|			КОГДА ВТ_СоответствиеПериодов.ПериодСледующий ЕСТЬ NULL 
	|					И УдалитьСчетаУчетаВНТТ.Период > &КонецТекущегоГода
	|				ТОГДА КОНЕЦПЕРИОДА(УдалитьСчетаУчетаВНТТ.Период, ГОД)
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВТ_СоответствиеПериодов.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|		КОНЕЦ, МЕСЯЦ) КАК ПериодПо
	|ИЗ
	|	РегистрСведений.УдалитьСчетаУчетаВНТТ КАК УдалитьСчетаУчетаВНТТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			УдалитьСчетаУчетаВНТТ1.Период КАК ПериодТекущий,
	|			УдалитьСчетаУчетаВНТТ1.ТорговаяТочка КАК ТорговаяТочка,
	|			МИНИМУМ(УдалитьСчетаУчетаВНТТ2.Период) КАК ПериодСледующий
	|		ИЗ
	|			РегистрСведений.УдалитьСчетаУчетаВНТТ КАК УдалитьСчетаУчетаВНТТ1
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УдалитьСчетаУчетаВНТТ КАК УдалитьСчетаУчетаВНТТ2
	|				ПО УдалитьСчетаУчетаВНТТ1.ТорговаяТочка = УдалитьСчетаУчетаВНТТ2.ТорговаяТочка
	|					И УдалитьСчетаУчетаВНТТ1.Период < УдалитьСчетаУчетаВНТТ2.Период
	|		
	|		СГРУППИРОВАТЬ ПО
	|			УдалитьСчетаУчетаВНТТ1.Период,
	|			УдалитьСчетаУчетаВНТТ1.ТорговаяТочка) КАК ВТ_СоответствиеПериодов
	|		ПО УдалитьСчетаУчетаВНТТ.ТорговаяТочка = ВТ_СоответствиеПериодов.ТорговаяТочка
	|			И УдалитьСчетаУчетаВНТТ.Период = ВТ_СоответствиеПериодов.ПериодТекущий
	|
	|СГРУППИРОВАТЬ ПО
	|	УдалитьСчетаУчетаВНТТ.ТорговаяТочка,
	|	НАЧАЛОПЕРИОДА(УдалитьСчетаУчетаВНТТ.Период, МЕСЯЦ),
	|	КОНЕЦПЕРИОДА(ВЫБОР
	|			КОГДА ВТ_СоответствиеПериодов.ПериодСледующий ЕСТЬ NULL 
	|					И УдалитьСчетаУчетаВНТТ.Период <= &КонецТекущегоГода
	|				ТОГДА &КонецТекущегоГода
	|			КОГДА ВТ_СоответствиеПериодов.ПериодСледующий ЕСТЬ NULL 
	|					И УдалитьСчетаУчетаВНТТ.Период > &КонецТекущегоГода
	|				ТОГДА КОНЕЦПЕРИОДА(УдалитьСчетаУчетаВНТТ.Период, ГОД)
	|			ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ВТ_СоответствиеПериодов.ПериодСледующий, ДЕНЬ), СЕКУНДА, -1)
	|		КОНЕЦ, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ) КАК Период,
	|	Хозрасчетный.Организация КАК Организация,
	|	ВТ_Документы.Склад КАК Склад,
	|	ИСТИНА КАК ОсобыйПорядокНалогообложения,
	|	КОНЕЦПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ) КАК ПериодПо
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Документы КАК ВТ_Документы
	|		ПО Хозрасчетный.Регистратор = ВТ_Документы.Регистратор
	|ГДЕ
	|	Хозрасчетный.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыручкаЕНВД)
	|	И Хозрасчетный.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТ_Документы.Регистратор
	|			ИЗ
	|				ВТ_Документы)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Документы.Склад,
	|	Хозрасчетный.Организация,
	|	НАЧАЛОПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ),
	|	КОНЕЦПЕРИОДА(Хозрасчетный.Период, МЕСЯЦ)";
	
	Результат = Запрос.ВыполнитьПакет();
	НТТ_Склады          = Результат[2].Выгрузить();
	НТТ_СчетаУчета      = Результат[3].Выгрузить(); // исходная таблица
	НТТ_Движения        = Результат[4].Выгрузить();
	
	МассивСтрокКУдалению_НТТ_Движения = Новый Массив();
	
	Для Каждого СтрокаТаблицы ИЗ НТТ_Склады Цикл
		
		ОтборНТТ = Новый Структура("Склад", СтрокаТаблицы.Склад);
		МассивСтрокНТТ = НТТ_Движения.НайтиСтроки(ОтборНТТ);
		Для Каждого СтрокаМассиваНТТ ИЗ МассивСтрокНТТ Цикл
			ОтборПериод = Новый Структура("Склад", СтрокаМассиваНТТ.Склад);
			МассивСтрокПериод = НТТ_СчетаУчета.НайтиСтроки(ОтборПериод);
			Если МассивСтрокПериод.Количество() <> 0 Тогда
				Для Каждого СтрокаМассиваНТТПериод ИЗ МассивСтрокПериод Цикл
					Если СтрокаМассиваНТТ.Период >= СтрокаМассиваНТТПериод.Период
						И СтрокаМассиваНТТ.ПериодПо <= СтрокаМассиваНТТПериод.ПериодПо Тогда
						
						МассивСтрокКУдалению_НТТ_Движения.Добавить(СтрокаМассиваНТТ);
						
						Если СтрокаМассиваНТТ.ОсобыйПорядокНалогообложения = СтрокаМассиваНТТПериод.ОсобыйПорядокНалогообложения Тогда
							Продолжить;
						КонецЕсли;
						
						Если СтрокаМассиваНТТ.Период = СтрокаМассиваНТТПериод.Период
							И СтрокаМассиваНТТ.ПериодПо = СтрокаМассиваНТТПериод.ПериодПо Тогда
							СтрокаМассиваНТТПериод.ОсобыйПорядокНалогообложения = СтрокаМассиваНТТ.ОсобыйПорядокНалогообложения;
						Иначе
							ПериодПо = СтрокаМассиваНТТПериод.ПериодПо;
							
							Если СтрокаМассиваНТТ.Период = СтрокаМассиваНТТПериод.Период Тогда
								НоваяСтрока = СтрокаМассиваНТТПериод;
							Иначе
								НоваяСтрока = НТТ_СчетаУчета.Добавить();
								СтрокаМассиваНТТПериод.ПериодПо = КонецМесяца(ДобавитьМесяц(СтрокаМассиваНТТ.Период, -1));
							КонецЕсли;
							ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассиваНТТ);
							
							Если СтрокаМассиваНТТ.ПериодПо <> ПериодПо Тогда
								НоваяСтрока                              = НТТ_СчетаУчета.Добавить();
								НоваяСтрока.Склад                        = СтрокаМассиваНТТ.Склад;
								НоваяСтрока.Период                       = НачалоМесяца(ДобавитьМесяц(СтрокаМассиваНТТ.Период, 1));
								НоваяСтрока.ПериодПо                     = ПериодПо;
								НоваяСтрока.ОсобыйПорядокНалогообложения = НЕ СтрокаМассиваНТТ.ОсобыйПорядокНалогообложения;
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			Иначе
				
				МассивСтрокКУдалению_НТТ_Движения.Добавить(СтрокаМассиваНТТ);
				
				НоваяСтрока = НТТ_СчетаУчета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМассиваНТТ);
				
				НоваяСтрока                              = НТТ_СчетаУчета.Добавить();
				НоваяСтрока.Склад                        = СтрокаМассиваНТТ.Склад;
				НоваяСтрока.Период                       = НачалоМесяца(ДобавитьМесяц(СтрокаМассиваНТТ.Период, 1));
				НоваяСтрока.ПериодПо                     = КонецМесяца(ДобавитьМесяц(СтрокаМассиваНТТ.Период, 1));
				НоваяСтрока.ОсобыйПорядокНалогообложения = НЕ СтрокаМассиваНТТ.ОсобыйПорядокНалогообложения;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СтрокаКУдалению ИЗ МассивСтрокКУдалению_НТТ_Движения Цикл
		НТТ_Движения.Удалить(СтрокаКУдалению);
	КонецЦикла;
	
	Если НТТ_Движения.Количество() <> 0 Тогда
		Для Каждого СтрокаТаблицы ИЗ НТТ_Движения Цикл
			
			ОтборПериод = Новый Структура("Период, Склад", СтрокаТаблицы.Период, СтрокаТаблицы.Склад);
			МассивСтрокПериод = НТТ_СчетаУчета.НайтиСтроки(ОтборПериод);
			Если МассивСтрокПериод.Количество() = 0 Тогда
				НоваяСтрока = НТТ_СчетаУчета.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЕсли;
			
			Период = НачалоМесяца(ДобавитьМесяц(СтрокаТаблицы.Период, 1));
			ОтборПериод = Новый Структура("Период, Склад", Период, СтрокаТаблицы.Склад);
			МассивСтрокПериод = НТТ_СчетаУчета.НайтиСтроки(ОтборПериод);
			Если МассивСтрокПериод.Количество() = 0 Тогда
				НоваяСтрока                              = НТТ_СчетаУчета.Добавить();
				НоваяСтрока.Склад                        = СтрокаТаблицы.Склад;
				НоваяСтрока.Период                       = Период;
				НоваяСтрока.ОсобыйПорядокНалогообложения = НЕ СтрокаТаблицы.ОсобыйПорядокНалогообложения;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	НТТ_СчетаУчета.Свернуть("Период, Склад, ОсобыйПорядокНалогообложения");
	
	Набор = РегистрыСведений.ПорядокНалогообложенияТорговыхТочек.СоздатьНаборЗаписей();
	Набор.Загрузить(НТТ_СчетаУчета);
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли