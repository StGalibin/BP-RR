#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр банковских документов'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "БанковскиеВыписки";
	КомандаПечати.Порядок        = 100;
	
	// Печать кассовых ордеров
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "КассовыйОрдер";
	КомандаПечати.Представление = НСтр("ru = 'Кассовый ордер'");
	КомандаПечати.Обработчик    = "УчетДенежныхСредствКлиент.ВыполнитьКомандуПечатиКассовыйОрдер";
	КомандаПечати.СписокФорм    = "КассовыеДокументы";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru = 'Реестр документов'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Реестр кассовых документов'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "КассовыеДокументы";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

Функция ПолучитьДополнительныеРеквизитыДляРеестра(КлючНазначения = "") Экспорт
	
	Результат = Новый Структура;
	
	Если КлючНазначения = "БанковскиеВыписки" Тогда
		
		ПолеЗапросаСумма =
		"	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПоступлениеНаРасчетныйСчет)
		|			ТОГДА Таб.Доход
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.СписаниеСРасчетногоСчета)
		|			ТОГДА Таб.Расход
		|		ИНАЧЕ 0
		|	КОНЕЦ";
		
		ПолеЗапросаИнформация =
		"ВЫБОР
		|		КОГДА Таб.НазначениеПлатежа ПОДОБНО """"
		|			ТОГДА ВЫРАЗИТЬ(Таб.Контрагент.Наименование КАК СТРОКА(100))
		|		ИНАЧЕ """" + (ВЫРАЗИТЬ(Таб.Контрагент.Наименование КАК СТРОКА(100))) + "" / "" + Таб.НазначениеПлатежа
		|	КОНЕЦ";
		
		Результат.Вставить("НомерВходящегоДокумента", "НомерВходящегоДокумента");
		Результат.Вставить("ДатаВходящегоДокумента",  "ДатаВходящегоДокумента");
		
	ИначеЕсли КлючНазначения = "КассовыеДокументы" Тогда
		
		ПолеЗапросаСумма =
		"	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПриходныйКассовыйОрдер)
		|			ТОГДА Таб.Доход
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.РасходныйКассовыйОрдер)
		|			ТОГДА Таб.Расход
		|		ИНАЧЕ 0
		|	КОНЕЦ";
		
		ПолеЗапросаИнформация = "Контрагент";
		
	Иначе
		
		ПолеЗапросаСумма =
		"	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПоступлениеНаРасчетныйСчет)
		|			ТОГДА Таб.Доход
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.СписаниеСРасчетногоСчета)
		|			ТОГДА Таб.Расход
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПриходныйКассовыйОрдер)
		|			ТОГДА Таб.Доход
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.РасходныйКассовыйОрдер)
		|			ТОГДА Таб.Расход
		|		ИНАЧЕ 0
		|	КОНЕЦ";
		
		ПолеЗапросаИнформация =
		"ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.ПриходныйКассовыйОрдер)
		|			ИЛИ ТИПЗНАЧЕНИЯ(Таб.Ссылка) = ТИП(Документ.РасходныйКассовыйОрдер)
		|			ТОГДА Таб.Контрагент
		|		КОГДА Таб.НазначениеПлатежа ПОДОБНО """"
		|			ТОГДА ВЫРАЗИТЬ(Таб.Контрагент.Наименование КАК СТРОКА(100))
		|		ИНАЧЕ """" + (ВЫРАЗИТЬ(Таб.Контрагент.Наименование КАК СТРОКА(100))) + "" / "" + Таб.НазначениеПлатежа
		|	КОНЕЦ";
		
		Результат.Вставить("НомерВходящегоДокумента", "НомерВходящегоДокумента");
		Результат.Вставить("ДатаВходящегоДокумента",  "ДатаВходящегоДокумента");
		
	КонецЕсли;
	
	Результат.Вставить("Информация",      ПолеЗапросаИнформация);
	Результат.Вставить("СуммаДокумента",  ПолеЗапросаСумма);
	Результат.Вставить("ВалютаДокумента", "Валюта");
	
	Возврат Результат;
	
КонецФункции

Функция ОтборПоКлючуНазначения(КлючНазначения) Экспорт
	
	ТекстОтбора = "";
	
	Если КлючНазначения = "БанковскиеВыписки" Тогда
		
		ТекстОтбора = "
		|
		|ГДЕ
		|	(Таб.Ссылка ССЫЛКА Документ.ПоступлениеНаРасчетныйСчет
		|			ИЛИ Таб.Ссылка ССЫЛКА Документ.СписаниеСРасчетногоСчета)";
		
	ИначеЕсли КлючНазначения = "КассовыеДокументы" Тогда
		
		ТекстОтбора = "
		|
		|ГДЕ
		|	(Таб.Ссылка ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ИЛИ Таб.Ссылка ССЫЛКА Документ.РасходныйКассовыйОрдер)";
		
	КонецЕсли;
	
	Возврат ТекстОтбора;
		
КонецФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Устанавливаем признак доступности печати покомплектно.
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Не УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Реестр");
	
	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Реестр") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм
		ВызватьИсключение "Попытка распечатать реестр в комплекте.";
	КонецЕсли;
	
	// Проверяем, нужно ли для макета ПКО формировать табличный документ.
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПКО") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм
		Документы.ПриходныйКассовыйОрдер.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "РКО") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм
		Документы.РасходныйКассовыйОрдер.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли