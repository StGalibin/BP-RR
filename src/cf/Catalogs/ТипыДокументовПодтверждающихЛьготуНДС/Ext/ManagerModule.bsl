#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает тип документа, подтверждающего налогувую льготу по НДС.
//
// Параметры:
//  ВидЭД - ПеречислениеСсылка.ВидыЭД - вид электронного документа, указанный в документе реализации
//
// Возвращаемое значение:
// ТипПодтверждающегоДокумента - СправочникСсылка.ТипыДокументовПодтверждающихЛьготуНДС или Неопределено
//

Функция ТипПодтверждающегоДокументаПоВидуЭД(ВидЭД) Экспорт
	
	Если ВидЭД = Перечисления.ВидыЭД.АктВыполненныхРабот
		ИЛИ ВидЭД = Перечисления.ВидыЭД.АктЗаказчик
		ИЛИ ВидЭД = Перечисления.ВидыЭД.АктИсполнитель
		ИЛИ ВидЭД = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда 
		Возврат Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Акт;
	ИначеЕсли ВидЭД = Перечисления.ВидыЭД.ТОРГ12
		ИЛИ ВидЭД = Перечисления.ВидыЭД.ТОРГ12Покупатель
		ИЛИ ВидЭД = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда 
		Возврат Справочники.ТипыДокументовПодтверждающихЛьготуНДС.Накладная;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик обновления 3.0.51.5
// Устанавливает полное наименование для типов документов
Процедура УстановитьПолноеНаименование() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТипыДокументовПодтверждающихЛьготуНДС.Ссылка,
	|	ВЫБОР
	|		КОГДА ТипыДокументовПодтверждающихЛьготуНДС.Ссылка = ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Акт)
	|			ТОГДА ""Акт""
	|		КОГДА ТипыДокументовПодтверждающихЛьготуНДС.Ссылка = ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Договор)
	|			ТОГДА ""Договор""
	|		КОГДА ТипыДокументовПодтверждающихЛьготуНДС.Ссылка = ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.Накладная)
	|			ТОГДА ""Товарная накладная""
	|		КОГДА ТипыДокументовПодтверждающихЛьготуНДС.Ссылка = ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.СправкаРасчет)
	|			ТОГДА ""Справка-расчет""
	|		КОГДА ТипыДокументовПодтверждающихЛьготуНДС.Ссылка = ЗНАЧЕНИЕ(Справочник.ТипыДокументовПодтверждающихЛьготуНДС.УПД)
	|			ТОГДА ""Универсальный передаточный документ""
	|	КОНЕЦ КАК ПолноеНаименование
	|ИЗ
	|	Справочник.ТипыДокументовПодтверждающихЛьготуНДС КАК ТипыДокументовПодтверждающихЛьготуНДС
	|ГДЕ
	|	ТипыДокументовПодтверждающихЛьготуНДС.ПолноеНаименование = """"
	|	И ТипыДокументовПодтверждающихЛьготуНДС.Предопределенный";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТипДокументаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ТипДокументаОбъект.ПолноеНаименование = Выборка.ПолноеНаименование;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(ТипДокументаОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли