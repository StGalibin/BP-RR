
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ ЗначениеЗаполнено(Объект.УполномоченныйБанк) Тогда
		ТекстСообщения = ОбщегоНазначенияБПКлиентСервер.ПолучитьТекстСообщения(,, "Уполномоченный банк");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ПолеВыбораУполномоченногоБанка",, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ПолеВыбораУполномоченногоБанкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "999999999" Тогда
		СтандартнаяОбработка = Ложь;
		ВыбранноеЗначение = "";
		ПолеВыбораУполномоченногоБанка = "";
		Объект.УполномоченныйБанк = Неопределено;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораБанкаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Банки.ФормаВыбора",,,,,, ОписаниеОповещения);
	Иначе
		Объект.УполномоченныйБанк = УполномоченныйБанкОбработкаВыбораНаСервере(ВыбранноеЗначение, УполномоченныеБанки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеВыбораУполномоченногоБанкаОчистка(Элемент, СтандартнаяОбработка)
	
	Объект.УполномоченныйБанк = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораБанкаЗавершение(ВыбранноеЗначение, ДополнительныеПараметра) Экспорт
	
	Объект.УполномоченныйБанк = ВыбранноеЗначение;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		НастроитьСписокВыбораУполномоченныхБанков(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	НастроитьСписокВыбораУполномоченныхБанков(Объект.УполномоченныйБанк);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьСписокВыбораУполномоченныхБанков(ВыбранныйБанк)
	
	УполномоченныеБанки = Новый Структура;
	
	СписокВыбора = Элементы.ПолеВыбораУполномоченногоБанка.СписокВыбора;
	СписокВыбора.Очистить();
	
	БИКи = Новый Массив;
	БИКи.Добавить("044030861"); // ОАО "АБ "РОССИЯ"
	БИКи.Добавить("044525060"); // ВНЕШЭКОНОМБАНК
	БИКи.Добавить("044525111"); // АО "РОССЕЛЬХОЗБАНК"
	БИКи.Добавить("044525187"); // БАНК ВТБ (ПАО)
	БИКи.Добавить("044525219"); // ОАО "БАНК МОСКВЫ"
	БИКи.Добавить("044525225"); // ПАО "СБЕРБАНК РОССИИ"
	БИКи.Добавить("044525823"); // БАНК ГПБ (АО)
	БИКи.Добавить("044583162"); // АО АКБ "НОВИКОМБАНК"
	БИКи.Добавить("044583266"); // АКБ "РОССИЙСКИЙ КАПИТАЛ" (ПАО)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("БИКи", БИКи);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторБанковРФ.Код КАК БИК,
	|	ЕСТЬNULL(Банки.Наименование, КлассификаторБанковРФ.Наименование) КАК Наименование,
	|	ЕСТЬNULL(Банки.Ссылка, НЕОПРЕДЕЛЕНО) КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторБанковРФ КАК КлассификаторБанковРФ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Банки КАК Банки
	|		ПО КлассификаторБанковРФ.Код = Банки.Код
	|			И КлассификаторБанковРФ.КоррСчет = Банки.КоррСчет
	|			И (Банки.Код В (&БИКи))
	|ГДЕ
	|	КлассификаторБанковРФ.Код В(&БИКи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	БИК";
	
	ВыбранныйБанкНеЯвляетсяУполномоченным = ЗначениеЗаполнено(ВыбранныйБанк);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокВыбора.Добавить(Выборка.БИК, Выборка.Наименование);
		
		Если Выборка.Ссылка <> Неопределено Тогда
			УполномоченныеБанки.Вставить("_" + Выборка.БИК, Выборка.Ссылка);
		КонецЕсли;
		
		Если ВыбранныйБанкНеЯвляетсяУполномоченным И ВыбранныйБанк.Код = Выборка.БИК Тогда
			ВыбранныйБанкНеЯвляетсяУполномоченным = Ложь;
			ПолеВыбораУполномоченногоБанка = Выборка.БИК;
		КонецЕсли;
	КонецЦикла;
	
	Если ВыбранныйБанкНеЯвляетсяУполномоченным Тогда
		СписокВыбора.Добавить(ВыбранныйБанк.Код, ВыбранныйБанк.Наименование);
		УполномоченныеБанки.Вставить("_" + ВыбранныйБанк.Код, ВыбранныйБанк);
		ПолеВыбораУполномоченногоБанка = ВыбранныйБанк.Код;
	КонецЕсли;
	
	СписокВыбора.Добавить("999999999", "<Другой уполномоченный банк...>");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УполномоченныйБанкОбработкаВыбораНаСервере(Знач ВыбранноеЗначение, УполномоченныеБанки)
	Перем Ссылка;
	
	Если УполномоченныеБанки.Свойство("_" + ВыбранноеЗначение, Ссылка) Тогда
		Возврат Ссылка;
	КонецЕсли;
	
	Ссылка = Справочники.Банки.СсылкаНаБанк(ВыбранноеЗначение);
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		ТаблицаБанков = Справочники.Банки.ПолучитьТаблицуБанковПоРеквизитам("Код", ВыбранноеЗначение);
		Для каждого ЭлементТаблицы Из ТаблицаБанков Цикл
			Если НЕ ЭлементТаблицы.Ссылка.ЭтоГруппа Тогда
				Ссылка = ЭлементТаблицы.Ссылка;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	УполномоченныеБанки.Вставить("_" + Ссылка.Код, Ссылка);
	
	Возврат Ссылка;
	
КонецФункции

#КонецОбласти
