////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем Номенклатура, Организация;
	
	ПоказыватьТолькоОстатки = Истина;
	Период = Неопределено;
	
	Если Параметры.Свойство("Дата", Период) Тогда
		Период = Новый Граница(КонецДня(Период), ВидГраницы.Включая);
	КонецЕсли;
	
	Параметры.Отбор.Свойство("Номенклатура", Номенклатура);
	Параметры.Отбор.Свойство("Организация",  Организация);
		
	Список.Параметры.УстановитьЗначениеПараметра("Период", Период);
	Список.Параметры.УстановитьЗначениеПараметра("Номенклатура", Номенклатура);
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Организация);
	
	УстановитьЗначениеПараметраПоказыватьТолькоОстатки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьИнформационнуюНадписьПараметрыВыбора(ЭтаФорма);
	
КонецПроцедуры  


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = Новый Структура;
	ДанныеВыбора.Вставить("НомерГТД", Значение);
	ДанныеВыбора.Вставить("СтранаПроисхождения", Элемент.ТекущиеДанные.СтранаПроисхождения);
	
	ОповеститьОВыборе(ДанныеВыбора);
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОстаткиПриИзменении(Элемент)
	
	УстановитьЗначениеПараметраПоказыватьТолькоОстатки(ЭтаФорма);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИнформационнуюНадписьПараметрыВыбора(Форма)

	ЭлементыОтбора = Форма.Список.Отбор.Элементы;

	ТекстНадписи = "";
	Для Каждого Элемент Из ЭлементыОтбора Цикл
		Если ЗначениеЗаполнено(Элемент.ПравоеЗначение) и Элемент.Использование Тогда
			ТекстНадписи = ТекстНадписи + Элемент.ПравоеЗначение + "; ";
		КонецЕсли;
	КонецЦикла;

	ТекстНадписи = Лев(ТекстНадписи, СтрДлина(ТекстНадписи) - 2);

	Форма.ИнформационнаяНадписьПараметровВыбора = ТекстНадписи;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеПараметраПоказыватьТолькоОстатки(Форма)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Форма.Список, 
		"КоличествоОстаток", 
		0, 
		Форма.ПоказыватьТолькоОстатки, 
		ВидСравненияКомпоновкиДанных.Больше
		);

КонецПроцедуры

