#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)	
	
	СписокСОтборомПоВладельцу = Параметры.Свойство("СписокСОтборомПоВладельцу") И Параметры.СписокСОтборомПоВладельцу;
	
	ОтображатьПродукцию = ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Или Не СписокСОтборомПоВладельцу;
	
	НастроитьЗаголовокИВидимостьЭлементовФормы(Параметры.ОткрытоИзКарточкиНоменклатуры, ОтображатьПродукцию);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьПараметрыЗаписиДляОповещения(ПараметрыЗаписи);	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)	
	
	Оповестить("ЗаписанаСпецификацияНоменклатуры", ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодобратьМатериалы(Команда)
	
	ОткрытьФорму(
		"Обработка.ПодборНоменклатуры.Форма.Форма", 
		Новый Структура, 
		ЭтотОбъект,
		УникальныйИдентификатор);
		
	КонецПроцедуры
	
&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)
	
	ПодобранныеМатериалы = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	
	Для Каждого СтрокаПодбора Из ПодобранныеМатериалы Цикл
		НоваяСтрока = Объект.ИсходныеКомплектующие.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПодбора);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЗаголовокИВидимостьЭлементовФормы(ОткрытоИзКарточкиНоменклатуры, ОтображатьПродукцию)
	
	Если ОткрытоИзКарточкиНоменклатуры Тогда
		ЭтотОбъект.Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"),НСтр("ru = 'Материалы'"), Объект.Наименование);
	Иначе
		ЭтотОбъект.Заголовок = Метаданные.Справочники.СпецификацииНоменклатуры.РасширенноеПредставлениеОбъекта;
	КонецЕсли;
	
	Элементы.Наименование.Видимость = Не ОткрытоИзКарточкиНоменклатуры;
	
	Если ОткрытоИзКарточкиНоменклатуры Или Не ОтображатьПродукцию Тогда
		
		// Продукция видна "на заднем фоне". Форма блокирующая, продукция в форме не выводится
		Элементы.Владелец.Видимость  = Ложь;
		ЭтотОбъект.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		
		// У блокирующих форм кнопки внизу
		Элементы.ГруппаДействияФормы.Видимость   = Истина;
		Элементы.ГруппаКоманднаяПанель.Видимость = Ложь;
		
	Иначе
		
		Элементы.Владелец.Видимость  = Истина;
		ЭтотОбъект.РежимОткрытияОкна = РежимОткрытияОкнаФормы.Независимый;
		
		Элементы.ГруппаДействияФормы.Видимость   = Ложь;
		Элементы.ГруппаКоманднаяПанель.Видимость = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыЗаписиДляОповещения(ПараметрыЗаписи)
	
	СпецификацияПредставление = УправлениеПроизводством.ПредставлениеОсновнойСпецификации(Объект.Ссылка);
	ПараметрыЗаписи.Вставить("СпецификацияПредставление", СпецификацияПредставление);
	ПараметрыЗаписи.Вставить("НоменклатураВладелец",      Объект.Владелец);
	ПараметрыЗаписи.Вставить("ИзмененнаяСпецификация",    Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти
