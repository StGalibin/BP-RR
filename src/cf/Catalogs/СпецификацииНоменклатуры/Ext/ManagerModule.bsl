#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// В программе наименование основной спецификации, если она всего одна, совпадает с наименованием продукции
//
Процедура ИсправитьНаименованиеСпецификации(ОсновнаяСпецификацияНоменклатуры, Номенклатура, Наименование) Экспорт
	
	Если Не ЗначениеЗаполнено(ОсновнаяСпецификацияНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	Если НесколькоСпецификацийНоменклатуры(Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	НаименованиеСпецификации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнаяСпецификацияНоменклатуры, "Наименование");
	
	Если НаименованиеСпецификации = Наименование Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектОсновнаяСпецификация = ОсновнаяСпецификацияНоменклатуры.ПолучитьОбъект();
	ОбъектОсновнаяСпецификация.Наименование = Наименование;
	ОбъектОсновнаяСпецификация.Записать();
	
КонецПроцедуры

Функция НесколькоСпецификацийНоменклатуры(Номенклатура)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(СправочникСпецификаций.Ссылка) КАК КоличествоСпецификаций
	|ИЗ
	|	Справочник.СпецификацииНоменклатуры КАК СправочникСпецификаций
	|ГДЕ
	|	СправочникСпецификаций.Владелец = &Номенклатура
	|	И СправочникСпецификаций.ПометкаУдаления = ЛОЖЬ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоСпецификаций > 1;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#КонецЕсли
