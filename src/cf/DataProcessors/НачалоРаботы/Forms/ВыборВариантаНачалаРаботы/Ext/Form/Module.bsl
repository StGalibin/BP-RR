#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачатьРаботу(Команда)
	
	Если ОрганизацияУжеСоздана() Тогда
		ПодготовитьКонстантыДляНачалаРаботы();
		ОткрытьФорму("Обработка.НачалоРаботы.Форма.НачалоРаботыПредпринимательВСервисе");
		ОбновитьИнтерфейс();
	Иначе
		СохранитьРежимРаботыБыстрыйСтартНаСервере();
		ОбщегоНазначенияБПКлиент.ОткрытьБыстрыйСтарт();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Зарегистрировать(Команда)
	
	НомерШагаПомощника = 0;
	ПодготовитьДанныеДляРегистрацииНаСервере(РегистрацияОрганизацииКлиентСервер.ИмяПомощникаРегистрации(), НомерШагаПомощника);
	РегистрацияОрганизацииКлиент.ОткрытьЭтап(НомерШагаПомощника);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнестиИзменения(Команда)
	
	НомерШагаПомощника = 0;
	ПодготовитьДанныеДляРегистрацииНаСервере(РегистрацияОрганизацииКлиентСервер.ИмяПомощникаВнесенияИзменений(), НомерШагаПомощника);
	РегистрацияОрганизацииКлиент.ОткрытьЭтап(НомерШагаПомощника);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ОрганизацияУжеСоздана()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации";
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПодготовитьКонстантыДляНачалаРаботы()
	
	Константы.НачалоРаботы.Установить(Ложь);
	РежимыРаботыПользователя     = Обработки.НачалоРаботы.НовыйРежимРаботыПользователя();
	РежимРаботыВыборПользователя = Обработки.НачалоРаботы.РежимРаботыВыборПользователя();
	Если РежимРаботыВыборПользователя = РежимыРаботыПользователя.Регистрация Тогда
		// Оставим помощник регистрации на случай, чтобы пользователь не потерял ранее введенные данные.
		Константы.РегистрацияНеЗавершена.Установить(Истина);
	КонецЕсли;
	
	ОбщегоНазначенияБПВызовСервера.УстановитьСтандартныйИнтерфейс();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьРежимРаботыБыстрыйСтартНаСервере()
	
	РежимыРаботыПользователя = Обработки.НачалоРаботы.НовыйРежимРаботыПользователя();
	Обработки.НачалоРаботы.СохранитьРежимРаботыПользователя(РежимыРаботыПользователя.БыстрыйСтарт);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьДанныеДляРегистрацииНаСервере(ИмяПомощника, НомерШагаПомощника)
	
	// Если структура навигации ранее не была готова, то сформируем её.
	СтруктураНавигации = Обработки.РегистрацияОрганизации.СтруктураНавигацииПомощника();
	
	ИмяПомощникаСтруктурыНавигации = "";
	Если СтруктураНавигации <> Неопределено Тогда
		ИмяШага = Обработки.РегистрацияОрганизации.ИмяШага(?(НомерШагаПомощника = 0, 1, НомерШагаПомощника));
		СтруктураШага = СтруктураНавигации[ИмяШага];
		Если СтруктураШага.Свойство("ИмяПомощника") Тогда
			ИмяПомощникаСтруктурыНавигации = СтруктураШага.ИмяПомощника;
		КонецЕсли;
	КонецЕсли;
	
	Если ИмяПомощника <> ИмяПомощникаСтруктурыНавигации Тогда
		// Структура навигации не соответствует помощнику,
		// инициализируем новую структуру.
		Обработки.РегистрацияОрганизации.ПодготовитьСтруктуруНавигацииПомощника(, ИмяПомощника);
		НомерШагаПомощника = 1;
	Иначе
		// Получим номер шага помощника, который будем открывать.
		НомерШагаПомощника = Обработки.РегистрацияОрганизации.НомерШагаПомощника();
		Если Не ЗначениеЗаполнено(НомерШагаПомощника) Тогда
			НомерШагаПомощника = 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти