
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Дерево = РеквизитФормыВЗначение("Реквизиты");
	СтрокаДерева = Дерево.Строки.Добавить();
	СтрокаДерева.Имя = НСтр("ru = 'Наименование'");
	СтрокаДерева = Дерево.Строки.Добавить();
	СтрокаДерева.Имя = НСтр("ru = 'Уникальный идентификатор номенклатуры'");
	СтрокаДерева = Дерево.Строки.Добавить();
	СтрокаДерева.Имя = НСтр("ru = 'Артикул'");
	
	СтрокаДереваЦены = Дерево.Строки.Добавить();
	СтрокаДереваЦены.Имя = НСтр("ru = 'Цена'");
	
	ДоступныеТипыЦен = ПолучитьИзВременногоХранилища(Параметры.АдресСпискаТипыЦен);
	
	Для Каждого ОписаниеТипЦены Из ДоступныеТипыЦен Цикл
		
		ЗаполнитьЗначенияСвойств(СтрокаДереваЦены.Строки.Добавить(), ОписаниеТипЦены);
		
	КонецЦикла;
	
	ИспользоватьТипыЦен = ДоступныеТипыЦен.Количество() > 0;
	
	СтрокаДерева = Дерево.Строки.Добавить();
	СтрокаДерева.Имя = НСтр("ru = 'Единица измерения'");
	СтрокаДерева = Дерево.Строки.Добавить();
	СтрокаДерева.Имя = НСтр("ru = 'Ставка НДС'");
	ЗначениеВРеквизитФормы(Дерево,"Реквизиты");
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработкаВыбора(Элементы.Реквизиты.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОбработкаВыбора(Элементы.Реквизиты.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ДанныеТекущейСтроки)
	
	Если ИспользоватьТипыЦен И ДанныеТекущейСтроки.Имя = "Цена" Тогда
		Возврат;
	ИначеЕсли ИспользоватьТипыЦен И ДанныеТекущейСтроки.ИмяРодителя = "Цена" Тогда
		ИмяРеквизита = "Цена, " + ДанныеТекущейСтроки.Имя;
		СтруктураВозврат = Новый Структура("ИмяРеквизита, ЗначениеТипаЦен", ИмяРеквизита, ДанныеТекущейСтроки.Значение);
	Иначе
		СтруктураВозврат = Новый Структура("ИмяРеквизита", ДанныеТекущейСтроки.Имя);
	КонецЕсли;
	
	Закрыть(СтруктураВозврат);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ИспользоватьТипыЦен Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Элемент Из Реквизиты.ПолучитьЭлементы() Цикл
		Элементы.Реквизиты.Развернуть(Элемент.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры


