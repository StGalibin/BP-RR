#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьРазделениеПоОбластямДанных = ТехнологияСервисаИнтеграцияСБСП.РазделениеВключено()
		И ТехнологияСервисаИнтеграцияСБСП.ДоступноИспользованиеРазделенныхДанных();
		
	Элементы.ГруппаЕслиОсталисьВопросы.Видимость = ИспользоватьРазделениеПоОбластямДанных
		И ИнформационныйЦентрСервер.УстановленаИнтеграцияСоСлужбойПоддержки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СообщитьОНенайденном(Команда)
	
	Если ПустаяСтрока(ТекстСообщения) Тогда
		ТекстПредупреждения = НСтр("ru='Напишите, что не удалось найти.'");
		ПоказатьПредупреждение(, ТекстПредупреждения);
		ТекущийЭлемент = Элементы.ТекстСообщения;
		Возврат;
	КонецЕсли;
	
	ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
		СобытиеЖурналаРегистрацииСообщение(),
		,
		ТекстСообщения,
		,
		Истина); // Сообщение сразу записываем.
	ПоказатьОповещениеПользователя(НСтр("ru='Сообщение отправлено'"), , ТекстСообщения);
	
	ТекстСообщения = "";	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбращенияВСлужбуПоддержкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьОбращенияВСлужбуПоддержки" Тогда
		ИнформационныйЦентрКлиент.ОткрытьОбращенияВСлужбуПоддержки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидеороликНажатие(Элемент)
	
	ПерейтиПоНавигационнойСсылке("http://its.1c.ru/bmk/youtube/bs/review");
	
КонецПроцедуры

&НаКлиенте
Процедура НовостиИОбновленияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Новости" Тогда
		ОткрытьФорму("Справочник.Новости.Форма.ФормаПросмотраНовостей",,, "");
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ЧтоНовогоВПрограмме" Тогда
		URLОписанияИзменений = "http://its.1c.ru/updinfo/accounting/"
			+ ВерсияКонфигурации()
			+ "#"
			+ ОбщегоНазначенияБПВызовСервера.ПолучитьФильтрПоФункциональнымОпциям();
		ПерейтиПоНавигационнойСсылке(URLОписанияИзменений);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

&НаСервереБезКонтекста
Функция ВерсияКонфигурации()
	
	Возврат Метаданные.Версия;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СобытиеЖурналаРегистрацииСообщение()

	КодЯзыка = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	ШаблонТекста = НСтр("ru='%1.Сообщение'", КодЯзыка);
	Возврат СтрШаблон(ШаблонТекста, ГруппаСобытийЖурналаРегистрации());

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ГруппаСобытийЖурналаРегистрации()
	
	КодЯзыка  = ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка();
	Возврат НСтр("ru='Помощь БизнесСтарт'", КодЯзыка);

КонецФункции

#КонецОбласти
