#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ПЕЧАТНЫХ ФОРМ

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЕстьСчетаФактуры = Ложь;
	Для каждого Объект Из МассивОбъектов Цикл
		Если ТипЗнч(Объект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ЕстьСчетаФактуры = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьСчетаФактуры И УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УКДСтатус1") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УКДСтатус1", "УКД (статус 1)",
			УчетНДС.ПечатьУниверсальныхКорректировочныхДокументов(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактураВыданный.ТекстЗапросаПечатьКорректировочныхСчетовФактур( , , Истина)),,
			"Обработка.ПечатьУКД.ПФ_MXL_УниверсальныйКорректировочныйДокумент");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УКДСтатус2") Тогда
		ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов = "";
		Для каждого Объект Из МассивОбъектов Цикл
			Если ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов = 
					Документы[Объект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ПустаяСтрока(ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов) Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УКДСтатус2", "УКД (статус 2)",
				УчетНДС.ПечатьУниверсальныхКорректировочныхДокументов(МассивОбъектов, ОбъектыПечати, 
				ТекстЗапросаПечатьУниверсальныхКорректировочныхДокументов, Истина),,
				"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйКорректировочныйДокумент");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

Процедура ОчиститьПользовательскиеМакетыУКД() Экспорт
	
	МенеджерЗаписи = РегистрыСведений.ПользовательскиеМакетыПечати.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИмяМакета = "ПФ_MXL_УниверсальныйКорректировочныйДокумент";
	МенеджерЗаписи.Объект    = "Обработка.ПечатьУКД";
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.Удалить();
	КонецЕсли;
	
КонецПроцедуры

// Обработчик обновления 3.0.51.22
// устанавливает для пользовательского макета УКД без идентификатора госконтракта Использование = Ложь
Процедура СброситьИспользованиеПользовательскогоМакетаУКД() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПользовательскиеМакетыПечати.ИмяМакета,
	|	ПользовательскиеМакетыПечати.Объект
	|ИЗ
	|	РегистрСведений.ПользовательскиеМакетыПечати КАК ПользовательскиеМакетыПечати
	|ГДЕ
	|	ПользовательскиеМакетыПечати.Объект = ""Обработка.ПечатьУКД""
	|	И ПользовательскиеМакетыПечати.ИмяМакета = ""ПФ_MXL_УниверсальныйКорректировочныйДокумент""
	|	И ПользовательскиеМакетыПечати.Использование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Если в макете нет области ИдентификаторГосКонтракта, то устанавливаем использование в Ложь
		МенеджерЗаписи = РегистрыСведений.ПользовательскиеМакетыПечати.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка, "ИмяМакета, Объект");
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			Макет = МенеджерЗаписи.Макет.Получить();
			Если ТипЗнч(Макет) <> Тип("ТабличныйДокумент")
				ИЛИ ЗначениеЗаполнено(Макет.Области.Найти("ИдентификаторГосКонтракта")) Тогда 
				Продолжить;
			КонецЕсли;
			МенеджерЗаписи.Использование = Ложь;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли