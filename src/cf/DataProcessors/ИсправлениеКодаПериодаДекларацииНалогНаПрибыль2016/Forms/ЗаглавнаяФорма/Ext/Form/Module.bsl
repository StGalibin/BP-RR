#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СведенияОбОшибке = Обработки.ИсправлениеКодаПериодаДекларацииНалогНаПрибыль2016.СведенияОбОшибке();
	
	Если Не ЗначениеЗаполнено(СведенияОбОшибке) Тогда
		Обработки.ИсправлениеКодаПериодаДекларацииНалогНаПрибыль2016.ОтменитьРегулярноеОтображениеСообщения();
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// СписокОрганизаций
	ТекстСписокОрганизаций = СтрСоединить(СведенияОбОшибке.ВыгрузитьКолонку("НаименованиеОрганизации"), ", ");
	КоличествоОрганизаций = СведенияОбОшибке.Количество();
	Если КоличествоОрганизаций > 1 И СтрДлина(ТекстСписокОрганизаций) > 500 Тогда
		ТекстСписокОрганизаций = Обработки.ИсправлениеКодаПериодаДекларацииНалогНаПрибыль2016.ЧислительноеВТекстеРодительныйПадеж(
			КоличествоОрганизаций, 
			"организации", "организаций", "организаций");
	КонецЕсли;
	
	Элементы.ОписаниеОшибки.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'На титульном листе декларации за 2016 год %1 указан код налогового периода ""46 (год)"".'"),
		ТекстСписокОрганизаций);
	
	Элементы.ОписаниеПодачи.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Скорее всего, %1 такое уведомление:
		|- в 2016 году декларации представлялись ежеквартально
		|- в настройках указано, что налог уплачивается ежеквартально'"),
		?(КоличествоОрганизаций = 1, НСтр("ru = 'организация не подавала'"), НСтр("ru = 'организации не подавали'")));
	
	СписокДеклараций = НСтр("ru = 'Список деклараций'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если НЕ ЗавершениеРаботы Тогда
		Если БольшеНеПоказывать Тогда
			Отказ = Истина;
			ПодключитьОбработчикОжидания("Подключаемый_ОтменитьРегулярноеОтображениеСообщения", 0.1, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодготовитьСейчас(Команда)
	
	ПараметрыФормы = ПараметрыФормы(УникальныйИдентификатор);
	
	ОткрытьФорму(
		"Обработка.ИсправлениеКодаПериодаДекларацииНалогНаПрибыль2016.Форма.ИсправлениеОшибки",
		ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДекларацийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Раздел", ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты"));
	ПараметрыФормы.Вставить("ВидОтчета", "Декларация по налогу на прибыль");
	ОткрытьФорму("ОбщаяФорма.РегламентированнаяОтчетность", ПараметрыФормы, , "1С-Отчетность");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыФормы(УникальныйИдентификатор)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСведенияОбОшибке", "");
	
	ПараметрыФормы.АдресСведенияОбОшибке = ПоместитьВоВременноеХранилище(Обработки.ИсправлениеКодаПериодаДекларацииНалогНаПрибыль2016.СведенияОбОшибке(), УникальныйИдентификатор);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОтменитьРегулярноеОтображениеСообщения() Экспорт
	
	Если БольшеНеПоказывать Тогда
		ОтменитьРегулярноеОтображениеСообщения();
		БольшеНеПоказывать = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьРегулярноеОтображениеСообщения()
	
	Обработки.ИсправлениеКодаПериодаДекларацииНалогНаПрибыль2016.ОтменитьРегулярноеОтображениеСообщения();
	
КонецПроцедуры

#КонецОбласти