
#Область ПроцедурыИФункцииОбщегоНазначения

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьДатуУчетнойПолитики(ДатаВводаОстатков)

	Возврат ДатаВводаОстатков + 86400;

КонецФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	Объект.ДатаВводаОстатков              = ПолучитьДатуНачалаУчета();
	Объект.ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	КоличествоДокументовВводаОстатков     = ПолучитьКоличествоДокументовВводаОстатков(Объект.Организация);
	ОбновитьФормуНаСервере();

КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстЗаголовок = НСтр("ru = 'Начальные остатки (%1)'");
		ТекстЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовок, Объект.Организация);
	Иначе
		ТекстЗаголовок = НСтр("ru = 'Начальные остатки'");
	КонецЕсли;
	ЭтаФорма.Заголовок = ТекстЗаголовок;
	
	Объект.ДатаВводаОстатков = ПолучитьДатуНачалаУчета();

	ДатаУчетнойПолитики = ПолучитьДатуУчетнойПолитики(Объект.ДатаВводаОстатков);
	
	ДоступностьУчетнойПолитики = ЗначениеЗаполнено(Объект.Организация)
		И ЗначениеЗаполнено(Объект.ДатаВводаОстатков);
	
	УчетнаяПолитикаЗадана = Ложь;
	Если ДоступностьУчетнойПолитики Тогда
		УчетнаяПолитикаЗадана = УчетнаяПолитикаСуществует(Объект.Организация, ДатаУчетнойПолитики);
	КонецЕсли;
	
	ВидимостьУчетнойПолитики = ДоступностьУчетнойПолитики И НЕ УчетнаяПолитикаЗадана;
	
	Элементы.ДекорацияУчетнаяПолитика.Видимость = ВидимостьУчетнойПолитики;
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(
		ЭтаФорма,
		Объект.Организация,
		ДатаУчетнойПолитики);
		
	КоличествоДокументовВводаОстатков = ПолучитьКоличествоДокументовВводаОстатков(Объект.Организация);

	МожноИзменятьОстатки = ЗначениеЗаполнено(Объект.Организация) 
		И ЗначениеЗаполнено(Объект.ДатаВводаОстатков)
		И УчетнаяПолитикаЗадана;

	ЭтоПредприниматель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ЮридическоеФизическоеЛицо") = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
		
	ПолучитьРазделыОстатков();

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКоличествоДокументовВводаОстатков(Знач Организация)

	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат 0;
	КонецЕсли;

	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.УстановитьПараметр("Организация", Организация);
	ЗапросПоДокументам.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                           |	КОЛИЧЕСТВО(ВводНачальныхОстатков.Ссылка) КАК Документов
	                           |ИЗ
	                           |	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
	                           |ГДЕ
	                           |	ВводНачальныхОстатков.Организация = &Организация
	                           |	И ВводНачальныхОстатков.ОтражатьВБухгалтерскомУчете = ИСТИНА
	                           |	И ВводНачальныхОстатков.ОтражатьВНалоговомУчете = ИСТИНА
	                           |	И ВводНачальныхОстатков.ОтражатьПоСпециальнымРегистрам = ИСТИНА";

	РезультатЗапроса = ЗапросПоДокументам.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат 0;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Документов;
	КонецЕсли;

КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииСервер(Отказ)

	Если НЕ Обработки.ВводНачальныхОстатков.МожноИспользоватьПростуюФормуПомощника(Объект.Организация) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновитьФормуНаСервере();
	Модифицированность = Ложь;

КонецПроцедуры

&НаСервереБезКонтекста
Функция УчетнаяПолитикаСуществует(Знач Организация, Знач Дата)
	
	Возврат УчетнаяПолитика.Существует(Организация, Дата);
	
КонецФункции

&НаСервере
Функция ПолучитьДатуНачалаУчета()
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Возврат ДатаНачалаУчетаПоУмолчанию();
	КонецЕсли;
	
	МенеджерЗаписиРегистра = РегистрыСведений.ДатыВводаНачальныхОстатков.СоздатьМенеджерЗаписи();
	МенеджерЗаписиРегистра.Организация = Объект.Организация;
	МенеджерЗаписиРегистра.Прочитать();
	Если МенеджерЗаписиРегистра.Выбран() Тогда
		Возврат МенеджерЗаписиРегистра.ДатаВводаНачальныхОстатков;
	Иначе
		УстановитьДатуНачалаУчета(ОпределитьДатуНачалаУчета(Объект.Организация));
		Возврат ПолучитьДатуНачалаУчета();
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СоздатьЗаписьУчетнойПолитики(Организация, ДатаНачалаУчета);
	
	ДатаУчетнойПолитики = ПолучитьДатуУчетнойПолитики(ПолучитьДатуНачалаУчета());
	ДействующаяУчетнаяПолитика = ПолучитьКлючЗаписиУчетнойПолитики(Организация, ДатаУчетнойПолитики);
	Если ДействующаяУчетнаяПолитика.Период <= ДатаНачалаУчета Тогда
		Возврат;
	Иначе
		НастройкиУчета.СоздатьЗаписьУчетнойПолитикиНаНовуюДату(Организация, ДействующаяУчетнаяПолитика.Период, ПолучитьДатуУчетнойПолитики(ДатаНачалаУчета));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКлючЗаписиУчетнойПолитики(Знач Организация, Знач ДатаНачалаУчета)
	
	Возврат НастройкиУчета.КлючЗаписиДействующейУчетнойПолитики("УчетнаяПолитика", Организация, ДатаНачалаУчета);
	
КонецФункции

&НаСервереБезКонтекста
Функция ОпределитьДатуНачалаУчета(Знач Организация)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеПервичныхДокументов.ДатаРегистратора КАК ДатаРегистратора
	|ИЗ
	|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|ГДЕ
	|	ДанныеПервичныхДокументов.Организация = &Организация
	|	И ДанныеПервичныхДокументов.ДатаРегистратора < &ТекущаяДата
	|	И НЕ ДанныеПервичныхДокументов.Документ ССЫЛКА Документ.ВводНачальныхОстатков
	|			И НЕ ДанныеПервичныхДокументов.Документ ССЫЛКА Документ.ДокументРасчетовСКонтрагентом
	|			И НЕ ДанныеПервичныхДокументов.Документ ССЫЛКА Документ.Партия
	|			И НЕ ДанныеПервичныхДокументов.Документ ССЫЛКА Документ.ПартияМатериаловВЭксплуатации
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРегистратора";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ДатаВводаНачальныхОстатков = ДатаНачалаУчетаПоУмолчанию();
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДатаПервогоРегистратора    = Выборка.ДатаРегистратора;
			ДатаВводаНачальныхОстатков = КонецМесяца(ДобавитьМесяц(ДатаПервогоРегистратора,-1));
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ДатаВводаНачальныхОстатков;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДатаНачалаУчетаПоУмолчанию()
	
	Возврат КонецМесяца(ДобавитьМесяц(ТекущаяДатаСеанса(),-1));
	
КонецФункции

&НаСервере
Процедура УстановитьДатуНачалаУчета(ДатаНачалаУчета)

	МенеджерЗаписиРегистра = РегистрыСведений.ДатыВводаНачальныхОстатков.СоздатьМенеджерЗаписи();
	МенеджерЗаписиРегистра.Организация = Объект.Организация;
	МенеджерЗаписиРегистра.Прочитать();
	Если НЕ МенеджерЗаписиРегистра.Выбран() Тогда
		МенеджерЗаписиРегистра.Организация = Объект.Организация;
	КонецЕсли;
	МенеджерЗаписиРегистра.ДатаВводаНачальныхОстатков = ДатаНачалаУчета;
	Если КоличествоДокументовВводаОстатков > 0 Тогда
		Если НЕ УчетнаяПолитикаСуществует(Объект.Организация, ДатаНачалаУчета) Тогда
			СоздатьЗаписьУчетнойПолитики(Объект.Организация, ДатаНачалаУчета);
		КонецЕсли;
		Отказ = Ложь;
		ПерезаписатьДокументыНаНовуюДату(Объект.Организация, ДатаНачалаУчета, Отказ);
		Если Отказ Тогда
			Объект.ДатаВводаОстатков = ПолучитьДатуНачалаУчета();
			Возврат;
		Иначе
			ЭтаФорма.Модифицированность = Ложь;
		КонецЕсли;
	КонецЕсли;
	МенеджерЗаписиРегистра.Записать();
	Объект.ДатаВводаОстатков = ПолучитьДатуНачалаУчета();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПерезаписатьДокументыНаНовуюДату(Знач Организация, Знач ДатаНачалаУчета, Отказ)
	
	ЗапросПоДокументам = Новый Запрос;
	ЗапросПоДокументам.УстановитьПараметр("Организация", Организация);
	ЗапросПоДокументам.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                           |	ВводНачальныхОстатков.Ссылка,
	                           |	ВводНачальныхОстатков.Организация,
	                           |	ВводНачальныхОстатков.Проведен
	                           |ИЗ
	                           |	Документ.ВводНачальныхОстатков КАК ВводНачальныхОстатков
	                           |ГДЕ
	                           |	ВводНачальныхОстатков.Организация = &Организация
	                           |	И ВводНачальныхОстатков.ОтражатьВБухгалтерскомУчете = ИСТИНА
	                           |	И ВводНачальныхОстатков.ОтражатьВНалоговомУчете = ИСТИНА
	                           |	И ВводНачальныхОстатков.ОтражатьПоСпециальнымРегистрам = ИСТИНА";

	СписокДокументов = ЗапросПоДокументам.Выполнить().Выгрузить();

	Если СписокДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		НачатьТранзакцию();
		
		Для Каждого ДокументВводаОстатков Из СписокДокументов Цикл
			
			ДокументОбъект = ДокументВводаОстатков.Ссылка.ПолучитьОбъект();
			ДокументОбъект.Дата = ДатаНачалаУчета;
			Если ДокументОбъект.Проведен Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			Иначе
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный);
			КонецЕсли;
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = НСтр("ru='Не удалось изменить дату ввода остатков по причине: '");
		ТекстСообщения = ТекстСообщения + Символы.ПС + ОписаниеОшибки();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		Отказ = Истина;
		
		Возврат;
		
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура ПолучитьРазделыОстатков()
	
	Объект.РазделыВводаОстатков.Очистить();
	
	МенеджерОбработки = Обработки.ВводНачальныхОстатков;
	
	ПараметрыПроверки = Новый Структура("Организация, ДатаВводаОстатков", 
		Объект.Организация, Объект.ДатаВводаОстатков);
	
	// Деньги - есть у всех
	НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
	НоваяСтрока.Раздел             = НСтр("ru='Деньги'");
	НоваяСтрока.ИмяРаздела         = "Деньги";
	НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
	НоваяСтрока.Описание           = НСтр("ru='Наличные деньги, а также деньги на банковских счетах'");
	
	// Запасы - есть у всех
	НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
	НоваяСтрока.Раздел             = НСтр("ru='Запасы'");
	НоваяСтрока.ИмяРаздела         = "Запасы";
	НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
	НоваяСтрока.Описание           = НСтр("ru='Товары, материалы, продукция и другие материальные ценности'");
	
	// Основные средства - только при включенной функциональности
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетОсновныхСредств") Тогда
		НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
		НоваяСтрока.Раздел     = НСтр("ru='Основные средства'");
		НоваяСтрока.ИмяРаздела = "ОсновныеСредства";
		НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
		НоваяСтрока.Описание   = НСтр("ru='Здания, станки, транспортные средства и прочее оборудование'");
	КонецЕсли;
	
	// Нематериальные активы - только при включенной функциональности
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетНМА") Тогда
		НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
		НоваяСтрока.Раздел     = НСтр("ru='Нематериальные активы'");
		НоваяСтрока.ИмяРаздела = "НематериальныеАктивы";
		НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
		НоваяСтрока.Описание   = НСтр("ru='Права на товарные знаки, патенты, а также сведения о прочих нематериальных активах'");
	КонецЕсли;
	
	// Поставщики - есть у всех
	НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
	НоваяСтрока.Раздел     = НСтр("ru='Поставщики'");
	НоваяСтрока.ИмяРаздела = "Поставщики";
	НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
	НоваяСтрока.Описание   = НСтр("ru='Задолженность перед поставщиками за товары и услуги, а также сведения о выданных авансах'");
	
	// Покупатели - есть у всех
	НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
	НоваяСтрока.Раздел     = НСтр("ru='Покупатели'");
	НоваяСтрока.ИмяРаздела = "Покупатели";
	НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
	НоваяСтрока.Описание   = НСтр("ru='Задолженность покупателей за товары и услуги, а также сведения о полученных авансах'");
	
	// Комиссионеры - только при включенной функциональности
	Если ПолучитьФункциональнуюОпцию("ОсуществляетсяРеализацияТоваровУслугЧерезКомиссионеров") Тогда
		НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
		НоваяСтрока.Раздел     = НСтр("ru='Комиссионеры'");
		НоваяСтрока.ИмяРаздела = "Комиссионеры";
		НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
		НоваяСтрока.Описание   = НСтр("ru='Товары, переданные на реализацию комиссионеру'");
	КонецЕсли;
	
	// Зарплата и Подотчет - только при включенной функциональности
	//            а для ИП при условии что есть наемные работники
	ИспользуетсяПодсистемаУчетаЗарплатыИКадров = УчетЗарплаты.ИспользуетсяПодсистемаУчетаЗарплатыИКадров();
	ВедутсяРасчетыПоЗаработнойПлате = Ложь;
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров Тогда
		ВедутсяРасчетыПоЗаработнойПлате = Истина;
		Если ЭтоПредприниматель
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "ИПИспользуетТрудНаемныхРаботников") <> Истина Тогда
			ВедутсяРасчетыПоЗаработнойПлате = Ложь;
		КонецЕсли;
	КонецЕсли;
		
	Если ВедутсяРасчетыПоЗаработнойПлате Тогда
		НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
		НоваяСтрока.Раздел     = НСтр("ru='Зарплата'");
		НоваяСтрока.ИмяРаздела = "Зарплата";
		НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
		НоваяСтрока.Описание   = НСтр("ru='Расчеты с сотрудниками по зарплате и выданным в счет зарплаты авансам'");
	КонецЕсли;
	
	// Подотчет - только при включенной функциональности
	Если ИспользуетсяПодсистемаУчетаЗарплатыИКадров Тогда
		НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
		НоваяСтрока.Раздел     = НСтр("ru='Подотчет'");
		НоваяСтрока.ИмяРаздела = "Подотчет";
		НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
		НоваяСтрока.Описание   = НСтр("ru='Расчеты с подотчетными лицами по выданным или подлежащим возмещению суммам'");
	КонецЕсли;
	
	// Налоги и взносы - есть у всех
	НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
	НоваяСтрока.Раздел     = НСтр("ru='Налоги и взносы'");
	НоваяСтрока.ИмяРаздела = "НалогиИВзносы";
	НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
	НоваяСтрока.Описание   = НСтр("ru='Расчеты с налоговыми органами и внебюджетными фондами по налогам и взносам'");
	
	//Кредиты и займы - могуть быть у любого
	НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
	НоваяСтрока.Раздел     = НСтр("ru='Кредиты и займы'");
	НоваяСтрока.ИмяРаздела = "КредитыИЗаймы";
	НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
	НоваяСтрока.Описание   = НСтр("ru='Действующие кредиты (займы) и начисленные проценты по ним'");
	
	// Уставный капитал - только у юридических лиц
	Если НЕ ЭтоПредприниматель Тогда
		НоваяСтрока = Объект.РазделыВводаОстатков.Добавить();
		НоваяСтрока.Раздел     = НСтр("ru='Уставный капитал'");
		НоваяСтрока.ИмяРаздела = "Капитал";
		НоваяСтрока.ЕстьОстаткиРаздела = МенеджерОбработки.ЕстьОстаткиРаздела(НоваяСтрока.ИмяРаздела, ПараметрыПроверки);
		НоваяСтрока.Описание   = НСтр("ru='Расчеты с учредителями по взносам в уставный капитал'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураПараметровВспомогательнойФормы()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",                    Объект.Организация);
	СтруктураПараметров.Вставить("ДатаВводаОстатков",              Объект.ДатаВводаОстатков);
	СтруктураПараметров.Вставить("ВалютаРегламентированногоУчета", Объект.ВалютаРегламентированногоУчета);
	Возврат СтруктураПараметров;
	
КонецФункции

&НаКлиенте
Функция ОповещениеФормыПомощника(ТекущаяСтрока)
	
	ОповещениеФормы = Новый ОписаниеОповещения("ОбработчикЗакрытияФормыРедактированияЗавершение", ЭтотОбъект);
	Возврат ОповещениеФормы;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикЗакрытияФормыРедактированияЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущаяСтрока = Элементы.РазделыВводаОстатков.ТекущиеДанные;
	ТекущаяСтрока.ЕстьОстаткиРаздела = ПроверитьОстаткиРаздела(ТекущаяСтрока.ИмяРаздела,
		Новый Структура("Организация, ДатаВводаОстатков", Объект.Организация, Объект.ДатаВводаОстатков));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьОстаткиРаздела(ИмяРаздела, ПараметрыПроверки)
	
	Возврат Обработки.ВводНачальныхОстатков.ЕстьОстаткиРаздела(ИмяРаздела, ПараметрыПроверки);
	
КонецФункции

&НаКлиенте
Процедура ВопросИзменитьДатуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьДатуНачалаУчета(ДополнительныеПараметры.ДатаВводаОстатков);
		Оповестить("ИзмененениеДатыВводаОстатков",Объект.Организация, "ВводНачальныхОстатков");
	Иначе
		Объект.ДатаВводаОстатков = ПолучитьДатуНачалаУчета();
		Модифицированность = Ложь;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ПризнакЗакрытия Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
	ПараметрыПодчиненнойФормы = ДополнительныеПараметры.ПараметрыПодчиненнойФормы;
	Если ПараметрыПодчиненнойФормы <> Неопределено Тогда
		ОткрытьПодчиненнуюФорму(ПараметрыПодчиненнойФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодчиненнуюФорму(ПараметрыПодчиненнойФормы)
	
	ОткрытьФорму(ПараметрыПодчиненнойФормы.ИмяФормы, 
		СтруктураПараметровВспомогательнойФормы(),
		ЭтаФорма, 
		ПараметрыПодчиненнойФормы.ИдентификаторОкнаФормы,
		,
		,
		ОповещениеФормыПомощника(ПараметрыПодчиненнойФормы.ТекущаяСтрока), 
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область ОбработчикиЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Отказ = Ложь;
	ОрганизацияПриИзмененииСервер(Отказ);
	Если Отказ Тогда
		ОткрытьФорму("Обработка.ВводНачальныхОстатков.Форма.Форма",
			Новый Структура("Организация", Объект.Организация),
			ЭтаФорма,,,,, РежимОткрытияОкнаФормы.Независимый);
			Модифицированность = Ложь;
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУчетнаяПолитикаНажатие(Элемент)
	
	ДатаУчетнойПолитики = ПолучитьДатуУчетнойПолитики(Объект.ДатаВводаОстатков);
	УчетнаяПолитикаСуществует = УчетнаяПолитикаСуществует(Объект.Организация, ДатаУчетнойПолитики);
	
	Ключ = ПолучитьКлючЗаписиУчетнойПолитики(Объект.Организация, ДатаУчетнойПолитики);
	
	ПараметрыФормы = Новый Структура;
	Если Ключ <> Неопределено Тогда
		ПараметрыФормы.Вставить("Ключ", Ключ);
	Иначе
		ЗначенияЗаполнения = Новый Структура("Организация, Период", Объект.Организация, ДатаУчетнойПолитики);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.УчетнаяПолитика.ФормаЗаписи", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаВводаОстатковПриИзменении(Элемент)
	
	Если Объект.ДатаВводаОстатков <> КонецМесяца(Объект.ДатаВводаОстатков) Тогда
		Объект.ДатаВводаОстатков = КонецМесяца(Объект.ДатаВводаОстатков);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТабличныхЧастей

&НаКлиенте
Процедура РазделыВводаОстатковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.РазделыВводаОстатков.ТекущиеДанные;
	ИдентификаторОкнаФормы = Строка(УникальныйИдентификатор) + ТекущаяСтрока.ИмяРаздела;
	
	ПараметрыПодчиненнойФормы = Новый Структура();
	ПараметрыПодчиненнойФормы.Вставить("ИмяФормы", "Обработка.ВводНачальныхОстатков.Форма.Форма_" + ТекущаяСтрока.ИмяРаздела);
	ПараметрыПодчиненнойФормы.Вставить("ИдентификаторОкнаФормы", ИдентификаторОкнаФормы);
	ПараметрыПодчиненнойФормы.Вставить("ТекущаяСтрока", ТекущаяСтрока);
	
	Если Модифицированность Тогда
		Если КоличествоДокументовВводаОстатков <> 0 Тогда
			ПараметрыОтвета = Новый Структура("ДатаВводаОстатков, ПризнакЗакрытия", Объект.ДатаВводаОстатков, Ложь);
			ПараметрыОтвета.Вставить("ПараметрыПодчиненнойФормы", ПараметрыПодчиненнойФормы);
			ТекстВопроса = НСтр("ru='Изменена дата остатков. 
				|Перезаписать все введенные данные на новую дату?'");
			Оповещение = Новый ОписаниеОповещения("ВопросИзменитьДатуЗавершение", ЭтотОбъект, ПараметрыОтвета);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет,,);
			Возврат;
		Иначе
			УстановитьДатуНачалаУчета(Объект.ДатаВводаОстатков);
		КонецЕсли;
	КонецЕсли;
	
	ОткрытьПодчиненнуюФорму(ПараметрыПодчиненнойФормы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Объект.Организация = Параметры.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнаяОрганизация");
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаВводаОстатков) Тогда
		Объект.ДатаВводаОстатков = ПолучитьДатуНачалаУчета();
	КонецЕсли;
	
	ПодготовитьФормуНаСервере();
	
	ИмяОтчета = "";
	Если Параметры.Свойство("ОткрытИзОтчета", ИмяОтчета) Тогда
		РазделыИОтчеты = ПолучитьСоответствиеОтчетовРазделам();
		Отбор = Новый Структура("ИмяРаздела", РазделыИОтчеты[ИмяОтчета]);
		СтрокаРаздела = Объект.РазделыВводаОстатков.НайтиСтроки(Отбор);
		Элементы.РазделыВводаОстатков.ТекущаяСтрока = СтрокаРаздела[0].ПолучитьИдентификатор();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Обработка.ВводНачальныхОстатков",
		"Форма",
		НСтр("ru='Новости: Помощник ввода остатков'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСоответствиеОтчетовРазделам()
	
	РазделыИОтчеты = Новый Соответствие;
	РазделыИОтчеты.Вставить("ОстаткиДенежныхСредств",   "Деньги");
	РазделыИОтчеты.Вставить("ОстаткиТоваров",           "Запасы");
	РазделыИОтчеты.Вставить("ЗадолженностьПокупателей", "Покупатели");
	РазделыИОтчеты.Вставить("ЗадолженностьПоставщикам", "Поставщики");
	
	Возврат РазделыИОтчеты;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ИзменениеУчетнойПолитики" Тогда
		Если Параметр = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(Объект.Организация) Тогда
			ОбновитьФормуНаСервере();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОбновитьФормуПомощникаВводаОстатков" 
		И Источник = "ВводНачальныхОстатков" 
		И Параметр = Объект.Организация Тогда
		ОбновитьФормуНаСервере();
	ИначеЕсли ИмяСобытия = "ИзменениеОсновнойОрганизации" Тогда
		Объект.Организация = Параметр;
		ОрганизацияПриИзменении(Неопределено);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Если КоличествоДокументовВводаОстатков <> 0 Тогда
			Отказ = Истина;
			ПараметрыОтвета = Новый Структура("ДатаВводаОстатков, ПризнакЗакрытия, ПараметрыПодчиненнойФормы",
				Объект.ДатаВводаОстатков, Истина, Неопределено);
			ТекстВопроса = НСтр("ru='Изменена дата остатков. 
				|Перезаписать все введенные данные на новую дату?'");
			Оповещение = Новый ОписаниеОповещения("ВопросИзменитьДатуЗавершение", ЭтотОбъект, ПараметрыОтвета);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет,,);
		Иначе
			УстановитьДатуНачалаУчета(Объект.ДатаВводаОстатков);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РеквизитыОрганизацииЗаполнены = ОбщегоНазначенияБПКлиент.ПроверитьНаличиеОрганизаций();
	
	Если РеквизитыОрганизацииЗаполнены Тогда
		
		// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
		// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
		
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеФункцииИПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

#КонецОбласти
