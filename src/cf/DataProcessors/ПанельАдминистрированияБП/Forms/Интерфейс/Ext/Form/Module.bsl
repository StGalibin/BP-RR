#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначенияБП.ДоступенПростойИнтерфейс() Тогда
		Элементы.ВариантИнтерфейса.СписокВыбора.Очистить();
		Элементы.ВариантИнтерфейса.СписокВыбора.Добавить("ИнтерфейсТаксиПростой", НСтр("ru='Простой'"));
		Элементы.ВариантИнтерфейса.СписокВыбора.Добавить("ИнтерфейсТакси", НСтр("ru='Полный'"));
	Иначе
		Элементы.ВариантИнтерфейса.СписокВыбора.Очистить();
		Элементы.ВариантИнтерфейса.СписокВыбора.Добавить("ИнтерфейсТакси", НСтр("ru='""Такси"" (рекомендуется)'"));
		Элементы.ВариантИнтерфейса.СписокВыбора.Добавить("ИнтерфейсВерсии82", НСтр("ru='Как в предыдущих версиях 1С:Бухгалтерии 8'"));
		Элементы.ВариантИнтерфейса.СписокВыбора.Добавить("ИнтерфейсВерсии77", НСтр("ru='Аналогичный 1С:Бухгалтерии 7.7'"));
	КонецЕсли;
	
	// Значения реквизитов формы
	Если Константы.ИнтерфейсТакси.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсТакси";
	ИначеЕсли Константы.ИнтерфейсТаксиПростой.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсТаксиПростой";
	ИначеЕсли Константы.ИнтерфейсВерсии82.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсВерсии82";
	ИначеЕсли Константы.ИнтерфейсВерсии77.Получить() Тогда
		ВариантИнтерфейса = "ИнтерфейсВерсии77";
	Иначе
		ВариантИнтерфейса = "";
	КонецЕсли;
	
	ВариантИнтерфейсаДоИзменения = ВариантИнтерфейса;
	Если ВариантИнтерфейса = "" Тогда
		Если ОбщегоНазначенияБП.ДоступенПростойИнтерфейс() Тогда
			ВариантИнтерфейса = "ИнтерфейсТаксиПростой";
		Иначе
			ВариантИнтерфейса = "ИнтерфейсТакси";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантИнтерфейсаПриИзменении(Элемент)

	Элементы.ГруппаПерезапускКнопка.Видимость = ВариантИнтерфейсаДоИзменения <> ВариантИнтерфейса;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьТабличныеФормыБанковскихДокументовПриИзменении(Элемент)
	Подключаемый_ПриИзмененииРеквизита(Элемент, Ложь);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перезапустить(Команда)
	
	УстановитьИнтерфейсНаСервере(ВариантИнтерфейса);
	
	ЗавершитьРаботуСистемы(Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьИнтерфейсНаСервере(ВариантИнтерфейса)

	ОбщегоНазначенияБП.УстановитьРежимКомандногоИнтерфейса(ВариантИнтерфейса);
	ХранилищеОбщихНастроек.Сохранить(ВРег("ДатаСменыИнтерфейса"),,ТекущаяДатаСеанса(),,);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина)
	
	Результат = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	Результат = Новый Структура;
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат;
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 14)) = НРег("НаборКонстант.") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 15);
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = НаборКонстант[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
