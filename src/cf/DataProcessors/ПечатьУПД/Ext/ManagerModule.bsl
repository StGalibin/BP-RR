#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// ПРОЦЕДУРЫ И ФУНКЦИИ ФОРМИРОВАНИЯ ПЕЧАТНЫХ ФОРМ

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЕстьСчетаФактуры = Ложь;
	Для каждого Объект Из МассивОбъектов Цикл
		Если ТипЗнч(Объект) = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ЕстьСчетаФактуры = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьСчетаФактуры И УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус1") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус1", "УПД (статус 1)",
			УчетНДС.ПечатьУниверсальныхПередаточныхДокументов(МассивОбъектов, ОбъектыПечати, 
			Документы.СчетФактураВыданный.ТекстЗапросаПечатьСчетовФактур(Истина, Истина)),,
			"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент");
			
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "УПДСтатус2") Тогда
		ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов = "";
		Для каждого Объект Из МассивОбъектов Цикл
			Если ТипЗнч(Объект) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов = 
					Документы[Объект.Метаданные().Имя].ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ПустаяСтрока(ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов) Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "УПДСтатус2", "УПД (статус 2)",
				УчетНДС.ПечатьУниверсальныхПередаточныхДокументов(МассивОбъектов, ОбъектыПечати, 
				ТекстЗапросаПечатьУниверсальныхПередаточныхДокументов, Истина),,
				"Обработка.ПечатьУПД.ПФ_MXL_УниверсальныйПередаточныйДокумент");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры

// Обработчик обновления 3.0.51.22
// устанавливает для пользовательского макета УПД без идентификатора госконтракта Использование = Ложь
Процедура СброситьИспользованиеПользовательскогоМакетаУПД() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПользовательскиеМакетыПечати.ИмяМакета,
	|	ПользовательскиеМакетыПечати.Объект
	|ИЗ
	|	РегистрСведений.ПользовательскиеМакетыПечати КАК ПользовательскиеМакетыПечати
	|ГДЕ
	|	ПользовательскиеМакетыПечати.Объект = ""Обработка.ПечатьУПД""
	|	И ПользовательскиеМакетыПечати.ИмяМакета = ""ПФ_MXL_УниверсальныйПередаточныйДокумент""
	|	И ПользовательскиеМакетыПечати.Использование";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// Если в макете нет области ИдентификаторГосКонтракта, то устанавливаем использование в Ложь
		МенеджерЗаписи = РегистрыСведений.ПользовательскиеМакетыПечати.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка, "ИмяМакета, Объект");
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			Макет = МенеджерЗаписи.Макет.Получить();
			Если ТипЗнч(Макет) <> Тип("ТабличныйДокумент")
				ИЛИ ЗначениеЗаполнено(Макет.Области.Найти("ИдентификаторГосКонтракта")) Тогда 
				Продолжить;
			КонецЕсли;
			МенеджерЗаписи.Использование = Ложь;
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли