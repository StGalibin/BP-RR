#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СоответствиеКонтрагентовЕГАИС(МассивКонтрагентов) Экспорт
	Запрос = Новый Запрос;
	Результат = Новый Соответствие;
	Запрос.УстановитьПараметр("МассивКонтрагентов", МассивКонтрагентов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК ОрганизацияЕГАИС,
	|	КлассификаторОрганизацийЕГАИС.Наименование,
	|	КлассификаторОрганизацийЕГАИС.НаименованиеПолное,
	|	КлассификаторОрганизацийЕГАИС.ИНН,
	|	КлассификаторОрганизацийЕГАИС.КПП,
	|	КлассификаторОрганизацийЕГАИС.КодСтраны
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО КлассификаторОрганизацийЕГАИС.ИНН = Контрагенты.ИНН
	|			И КлассификаторОрганизацийЕГАИС.КПП = Контрагенты.КПП
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.Ссылка В (&МассивКонтрагентов)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Контрагент) Тогда
			КонтрагентСсылка = Выборка.Контрагент;
		ИначеЕсли НЕ ПустаяСтрока(Выборка.ИНН) Тогда
			// Если не находим контрагента то создаем его
			ДанныеЗаполнения = Новый Структура("ЭтоЭлектронныйДокумент, Наименование, НаименованиеПолное, ИНН, КПП", Истина);
			ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, Выборка);
			
			СтранаРегистрации = Справочники.СтраныМира.НайтиПоКоду(Выборка.КодСтраны);
			Если СтранаРегистрации <> Неопределено Тогда
				ДанныеЗаполнения.Вставить("СтранаРегистрации", СтранаРегистрации);
			КонецЕсли; 
			
			СправочникКонтрагенты = Справочники.Контрагенты.СоздатьЭлемент();
			СправочникКонтрагенты.Заполнить(ДанныеЗаполнения);
			СправочникКонтрагенты.Записать();
			
			КонтрагентСсылка = СправочникКонтрагенты.Ссылка;
		Иначе
			КонтрагентСсылка = Справочники.Контрагенты.ПустаяСсылка();
		КонецЕсли;
		
		Результат.Вставить(Выборка.ОрганизацияЕГАИС, КонтрагентСсылка);
	КонецЦикла;
	
	Результат.Вставить(Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка(), Справочники.Контрагенты.ПустаяСсылка());
	
	Возврат Результат;
КонецФункции

Функция ПолучитьОрганизациюИСклад(ОрганизацияЕГАИС) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Организация", 	Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("Склад", 		Справочники.Склады.ПустаяСсылка());

	КодФСРАР = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОрганизацияЕГАИС, "Код"));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодФСРАР", КодФСРАР);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НастройкиОбменаЕГАИС.Организация КАК Организация,
	|	НастройкиОбменаЕГАИС.Склад КАК Склад
	|ИЗ
	|	РегистрСведений.НастройкиОбменаЕГАИС КАК НастройкиОбменаЕГАИС
	|ГДЕ
	|	НастройкиОбменаЕГАИС.ИдентификаторФСРАР = &КодФСРАР
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Склад";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат.Организация 	= Выборка.Организация;
		Результат.Склад 		= Выборка.Склад;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция СоответствиеВидовЛицензийЕГАИС() Экспорт
	Результат = Новый Соответствие;
	Результат.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.ПустаяСсылка(), Перечисления.ВидыЛицензийНаПродажуАлкоголя.Пиво);
	Результат.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.Пиво, Перечисления.ВидыЛицензийНаПродажуАлкоголя.Пиво);
	Результат.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.АлкогольнаяПродукция, Перечисления.ВидыЛицензийНаПродажуАлкоголя.АлкогольнаяПродукция);
	Результат.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяНеПищеваяПродукция, Перечисления.ВидыЛицензийНаПродажуАлкоголя.СпиртосодержащаяНеПищеваяПродукция);
	Результат.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.СпиртосодержащаяПищеваяПродукция, Перечисления.ВидыЛицензийНаПродажуАлкоголя.СпиртосодержащаяПищеваяПродукция);
	Результат.Вставить(Перечисления.ВидыЛицензийАлкогольнойПродукции.Спирт, Перечисления.ВидыЛицензийНаПродажуАлкоголя.АлкогольнаяПродукция);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли