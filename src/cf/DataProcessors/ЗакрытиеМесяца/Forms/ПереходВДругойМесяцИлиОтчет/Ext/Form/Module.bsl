
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ДатаМесяца)
		ИЛИ Не ЗначениеЗаполнено(Параметры.ПериодРегистрации) Тогда
			Отказ = Истина;
			ВызватьИсключение СтрШаблон(НСтр("ru = '%1, недостаточно параметров для открытия формы'"), ИмяФормы);
		Возврат;
	КонецЕсли;

	КлючСохраненияПоложенияОкна = Параметры.НазначениеФормы;
	
	КомандаПерейти = Параметры.НазначениеФормы;
	
	Если КомандаПерейти = "ПерейтиВСледующийМесяц" Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Теперь можно перейти к закрытию %1 %2 г.'"),
			МесяцВРодительномПадеже(Параметры.ДатаМесяца),
			Формат(Год(Параметры.ДатаМесяца), "ЧГ=0"));
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрыт'"),
			Формат(Параметры.ПериодРегистрации, "ДФ='ММММ гггг'"));
		ТекстКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перейти к закрытию %1 %2 г.'"),
			МесяцВРодительномПадеже(Параметры.ДатаМесяца),
			Формат(Год(Параметры.ДатаМесяца), "ЧГ=0"));
			
	ИначеЕсли КомандаПерейти = "СформироватьОтчет" Тогда
		
		ТекстСообщения = НСтр("ru = 'Теперь можно сформировать отчет'");
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрыт'"),
			Формат(Параметры.ПериодРегистрации, "ДФ='ММММ гггг'"));
		
		ТекстКнопкиПерейти = НСтр("ru = 'Сформировать отчет'");
		
	Иначе
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сначала нужно закрыть %1 г.'"),
			Формат(Параметры.ДатаМесяца, "ДФ='ММММ гггг'"));
		
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 г. закрывать еще рано'"),
			Формат(Параметры.ПериодРегистрации, "ДФ='ММММ гггг'"));
			
		ТекстКнопкиПерейти = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Перейти к закрытию %1 %2 г.'"),
			МесяцВРодительномПадеже(Параметры.ДатаМесяца),
			Формат(Год(Параметры.ДатаМесяца), "ЧГ=0"));
			
		Элементы.МесяцЗакрыт.Видимость = Ложь;
		
	КонецЕсли;
	
	Элементы.ПерейтиКЗакрытию.Заголовок = ТекстКнопкиПерейти;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчкиКоманд

&НаКлиенте
Процедура ПерейтиКзакрытию(Команда)
	
	Если КомандаПерейти = "СформироватьОтчет" Тогда
		Закрыть("СформироватьОтчет");
	Иначе
		Закрыть("Перейти");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НеСейчас(Команда)
	Закрыть("Отмена");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентенаСерверебезКонтекста
Функция МесяцВРодительномПадеже(Дата)
	
	Месяцы = Новый Соответствие();
	
	Месяцы.Вставить(1, НСтр("ru = 'января'"));
	Месяцы.Вставить(2, НСтр("ru = 'февраля'"));
	Месяцы.Вставить(3, НСтр("ru = 'марта'"));
	Месяцы.Вставить(4, НСтр("ru = 'апреля'"));
	Месяцы.Вставить(5, НСтр("ru = 'мая'"));
	Месяцы.Вставить(6, НСтр("ru = 'июня'"));
	Месяцы.Вставить(7, НСтр("ru = 'июля'"));
	Месяцы.Вставить(8, НСтр("ru = 'августа'"));
	Месяцы.Вставить(9, НСтр("ru = 'сентября'"));
	Месяцы.Вставить(10, НСтр("ru = 'октября'"));
	Месяцы.Вставить(11, НСтр("ru = 'ноября'"));
	Месяцы.Вставить(12, НСтр("ru = 'декабря'"));
	
	Возврат Месяцы.Получить(Месяц(Дата));
	
КонецФункции

#КонецОбласти