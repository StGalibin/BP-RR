#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Обработки.РегистрацияОрганизации.РазместитьНавигацию(ЭтотОбъект, Параметры);
	
	УстановитьЗначенияПоУмолчанию();
	ПодготовитьФормуНаСервере();
	
	УстановитьУсловноеОформление();
	
	ВидОрганизации = "ИндивидуальныйПредприниматель";
	СсылкаДляПереходаНаКарту = УправлениеКонтактнойИнформациейБП.СтрокаСсылкиПоказатьНаКарте();
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	ИсключаемыеВиды = Новый Массив();
	ИсключаемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	ИсключаемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	ИсключаемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.EmailОрганизации);
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(
		ЭтотОбъект, Организация, "КонтактныеДанные", , ИсключаемыеВиды, Истина);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	Если НавигацияПараметрФормы = "Документы" Тогда
		ИзменитьЭтап(3);
	Иначе
		ИзменитьЭтап(1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РегистрацияОрганизацииКлиент.ОповеститьОбОткрытии(ЭтотОбъект,
		РегистрацияОрганизацииКлиентСервер.ИмяПомощникаРегистрации(),
		НавигацияНомерШага);
	ПроверитьСоответствиеТребованиямИНН();
	// Восстановим признак модифицированности формы
	// для того, чтобы была выполнена запись объекта при переходе к следующему шагу.
	Если ДанныеМодифицированы Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	// Все данные сохраняются в настройках формы и при последующем открытии восстанавливаются.
	// Поэтому сохраним модифицированность формы, чтобы при следующем открытии помощника восстановить форму.
	// В базу данные запишутся, после того как пользователь двинется дальше по помощнику.
	ДанныеМодифицированы = Модифицированность;
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Источник = ЭтотОбъект Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ИзменитьПараметрНавигации" Тогда
		
		Если Параметр = "Заявление" Тогда
			ИзменитьЭтап(1);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ОткрытШагПомощника_РегистрацияОрганизации" Тогда
		
		Если Параметр = Неопределено
			Или Параметр.ИмяПомощника <> РегистрацияОрганизацииКлиентСервер.ИмяПомощникаРегистрации()
			Или Параметр.НомерШага <> НавигацияНомерШага Тогда
			Закрыть();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ЗавершенаРаботаПомощникаНачалаРаботы" Тогда
		
		// Работа помощника начала работы может быть завершена в другом окне (например, из окна БыстрыйСтарт).
		// В этом случае нужно закрыть окно помощника регистрации. Но пользователь мог начать изменять значение какого-нибудь
		// текстового поля и при закрытии окна сработает обработчик "ПриИзменении" и форма станет Модифицированной и не сможет
		// закрыться без вопроса. Для этого изменяем текущий элемент и снимаем модифицированность.
		ЭтотОбъект.ТекущийЭлемент = Элементы.СформироватьДокументы;
		ЭтотОбъект.Модифицированность = Ложь;
		Если ЭтотОбъект.Открыта() Тогда
			ЭтотОбъект.Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// Персональные данные
	
	ПроверитьЗаполнениеПаспортныхДанных(Отказ);
	
	Если КонтактнаяИнформацияПолеЮрАдресОрганизации = УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки() Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Адрес по прописке'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КонтактнаяИнформацияПолеЮрАдресОрганизации", , Отказ);
	КонецЕсли;
	
	// Контактные данные
	
	Если Не ЗначениеЗаполнено(КонтактнаяИнформацияПолеТелефонОрганизации) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Контактный телефон'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КонтактнаяИнформацияПолеТелефонОрганизации", , Отказ);
	ИначеЕсли Не ОрганизацииФормыКлиентСервер.ТелефонСоответствуетТребованиям(КонтактнаяИнформацияПолеТелефонОрганизации) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Контактный телефон'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КонтактнаяИнформацияПолеТелефонОрганизации", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонтактнаяИнформацияПолеEmailОрганизации)
		И Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(КонтактнаяИнформацияПолеEmailОрганизации) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Email'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КонтактнаяИнформацияПолеEmailОрганизации", , Отказ);
	КонецЕсли;
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	УправлениеКонтактнойИнформацией.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Организация, Отказ);
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	РегистрацияОрганизацииКлиент.ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФамилияИППриИзменении(Элемент)
	
	ФИОПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяИППриИзменении(Элемент)
	
	ФИОПриИзменении();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоИППриИзменении(Элемент)
	
	ФИОПриИзменении();
	ОрганизацииФормыКлиентСервер.ОпределитьПолПоОтчеству(ФизическоеЛицо.Пол, Организация.ОтчествоИП);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПолеЮрАдресОрганизацииНажатие(Элемент, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиентБП.НачалоВыбора(ЭтотОбъект, Элемент, Модифицированность, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПолеТелефонОрганизацииПриИзменении(Элемент)
	
	ОрганизацииФормыКлиентСервер.ТелефонПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПолеТелефонОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент, Модифицированность, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПолеEmailОрганизацииПриИзменении(Элемент)
	
	КонтактнаяИнформацияПолеEmailОрганизации = СокрЛП(КонтактнаяИнформацияПолеEmailОрганизации);
	Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияПолеEmailОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент, Модифицированность, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтактнаяИнформацияРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОбразецЗаявленияОРегистрацииИПЛистБ" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.ОбразецЗаявленияОРегистрацииИПЛистБ", , ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СерияНомерПаспортаПриИзменении(Элемент)
	
	МассивПодстрок = СтрРазделить(СерияНомерПаспорта, " ", Истина);
	
	Если МассивПодстрок.Количество() = 3 Тогда
		ПаспортныеДанные.Номер = МассивПодстрок[2];
		МассивПодстрок.Удалить(2);
	Иначе
		ПаспортныеДанные.Номер = "";
	КонецЕсли;
	
	ПаспортныеДанные.Серия = СтрСоединить(МассивПодстрок, " ");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыФизическихЛицДатаВыдачиПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ПаспортныеДанные.Период) И ЗначениеЗаполнено(ПаспортныеДанные.ДатаВыдачи) Тогда
		ПаспортныеДанные.Период = ПаспортныеДанные.ДатаВыдачи;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	Организация.ИНН = СокрЛП(Организация.ИНН);
	
	ФизическоеЛицо.ИНН = Организация.ИНН;
	
	ИННОбработатьИзменение();
	
КонецПроцедуры

&НаКлиенте
Процедура УзнатьИНННажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ПроверитьЗаполнениеПаспортныхДанных(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ДанныеФизическогоЛица", ДанныеИндивидуальногоПредпринимателя());
	
	ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.УзнатьИНН", ПараметрыФормы, Элементы.ИНН);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрирующийОрганАдресПереходНаКартуНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	УправлениеКонтактнойИнформациейКлиентБП.ПоказатьНаКартеНажатие(ЭтотОбъект, Элемент, РегистрирующийОрганАдрес);
	
КонецПроцедуры

&НаКлиенте
Процедура УплатаГоспошлиныОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОрганизацииФормыКлиент.ПерейтиКСервисуСкачатьКвитанциюНаОплатуГоспошлиныПриРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗаявлениеОРегистрацииИПНажатие(Элемент)
	
	ЗаявлениеРегистрацияИП();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаУведомлениеОПереходеНаУСННажатие(Элемент)
	
	УведомлениеОПереходенНаУСН();
	
КонецПроцедуры

&НаКлиенте
Процедура КартинкаЗаявлениеНаПолучениеПатентаНажатие(Элемент)
	
	ЗаявлениеПолучениеПатента();
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаИнструкцияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнструкцияРегистрацияИП();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьДокументы(Команда)
	
	Если Организация.Ссылка.Пустая() Или Модифицированность Тогда
		
		ДлительнаяОперация = ЗаписатьДанныеВФоне();
		
		Если ДлительнаяОперация = Неопределено Тогда
			// Ошибка проверки заполнения.
			Возврат;
		КонецЕсли;
		
		Если ДлительнаяОперация.Статус = "Ошибка" Тогда
			
			ПоказатьОшибкуФормированияДокументов(ДлительнаяОперация);
			
		Иначе
			
			ИзменитьЭтап(2);
			
			НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			НастройкиОжидания.ВыводитьОкноОжидания = Ложь;
			НастройкиОжидания.ПолучатьРезультат = Истина;
			
			Обработчик = Новый ОписаниеОповещения("ПослеЗавершенияФоновогоЗаданияФормированияДокументов", ЭтотОбъект);
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
			
		КонецЕсли;
		
	Иначе
		ИзменитьЭтап(3);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДанныеВФоне()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование документов для регистрации организации'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	ПараметрыФормированияДокументов = ПараметрыФормированияДокументов();
	
	Возврат ДлительныеОперации.ВыполнитьВФоне("Обработки.РегистрацияОрганизации.СформироватьДокументыИПВФоне", ПараметрыФормированияДокументов, НастройкиЗапуска);
	
КонецФункции

&НаСервере
Функция ПараметрыФормированияДокументов()
	// Все реквизиты, которые выведены на форму необходимо
	// перенести в структуру для передачи в фоновое задание.
	
	ПараметрыФормированияДокументов = Новый Структура;
	
	// Организация
	ДанныеОрганизации = Новый Структура();
	ДанныеОрганизации.Вставить("Ссылка",                        Организация.Ссылка);
	ДанныеОрганизации.Вставить("ЮридическоеФизическоеЛицо",     Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	ДанныеОрганизации.Вставить("ИндивидуальныйПредприниматель", ДанныеИндивидуальногоПредпринимателя());
	ДанныеОрганизации.Вставить("КодОКВЭД2",                     Организация.КодОКВЭД2);
	ДанныеОрганизации.Вставить("НаименованиеОКВЭД2",            Организация.НаименованиеОКВЭД2);
	ДанныеОрганизации.Вставить("ДополнительныеКодыОКВЭД2",      Организация.ДополнительныеКодыОКВЭД2);
	
	ДанныеОрганизации.Вставить("УчетнаяПолитика",           ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураУчетнойПолитики));
	
	ДанныеОрганизации.Вставить("ДанныеРегистрацииВНалоговомОргане", ДанныеРегистрацииВНалоговомОргане());
	
	ПараметрыФормированияДокументов.Вставить("ДанныеОрганизации", ДанныеОрганизации);
	ПараметрыФормированияДокументов.Вставить("СпособРегистрации", СпособРегистрации);
	
	Возврат ПараметрыФормированияДокументов;
	
КонецФункции

&НаСервере
Функция ДанныеИндивидуальногоПредпринимателя()
	
	ДанныеИндивидуальногоПредпринимателя = Обработки.РегистрацияОрганизации.НовыеДанныеФизическогоЛица();
	ДанныеИндивидуальногоПредпринимателя.Ссылка                     = ФизическоеЛицо.Ссылка;
	ДанныеИндивидуальногоПредпринимателя.Фамилия                    = Организация.ФамилияИП;
	ДанныеИндивидуальногоПредпринимателя.Имя                        = Организация.ИмяИП;
	ДанныеИндивидуальногоПредпринимателя.Отчество                   = Организация.ОтчествоИП;
	ДанныеИндивидуальногоПредпринимателя.Пол                        = ФизическоеЛицо.Пол;
	ДанныеИндивидуальногоПредпринимателя.ИНН                        = Организация.ИНН;
	ДанныеИндивидуальногоПредпринимателя.ДатаРождения               = ФизическоеЛицо.ДатаРождения;
	ДанныеИндивидуальногоПредпринимателя.МестоРожденияПредставление = ФизическоеЛицо.МестоРожденияПредставление;
	
	ДанныеИндивидуальногоПредпринимателя.АдресПропискиЗначенияПолей  =
		ЗначенияПолейКонтактнойИнформации(Элементы.КонтактнаяИнформацияПолеЮрАдресОрганизации);
	ДанныеИндивидуальногоПредпринимателя.ТелефонРабочийЗначенияПолей =
		ЗначенияПолейКонтактнойИнформации(Элементы.КонтактнаяИнформацияПолеТелефонОрганизации);
	ДанныеИндивидуальногоПредпринимателя.EMailЗначенияПолей          = 
		ЗначенияПолейКонтактнойИнформации(Элементы.КонтактнаяИнформацияПолеEmailОрганизации);
		
	СерияИНомерПаспорта = ОрганизацииФормы.СерияНомерПаспортаРаздельно(СерияНомерПаспорта);
	
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.Серия            = СерияИНомерПаспорта.Серия;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.Номер            = СерияИНомерПаспорта.Номер;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.КемВыдан         = ПаспортныеДанные.КемВыдан;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.ДатаВыдачи       = ПаспортныеДанные.ДатаВыдачи;
	ДанныеИндивидуальногоПредпринимателя.ПаспортныеДанные.КодПодразделения = ПаспортныеДанные.КодПодразделения;
	
	Возврат ДанныеИндивидуальногоПредпринимателя;
	
КонецФункции

&НаСервере
Функция ДанныеРегистрацииВНалоговомОргане()
	
	ДанныеРегистрацииВНалоговомОргане = Обработки.РегистрацияОрганизации.НовыеДанныеРегистрацииВНалоговомОргане();
	АдресПропискиЗначениеПолей = ЗначенияПолейКонтактнойИнформации(Элементы.КонтактнаяИнформацияПолеЮрАдресОрганизации);
	
	СведенияОНалоговомОргане = СведенияОНалоговомОрганеПоАдресу(АдресПропискиЗначениеПолей);
	Если СведенияОНалоговомОргане <> Неопределено И ЗначениеЗаполнено(СведенияОНалоговомОргане.КодНалоговогоОргана) Тогда
		ДанныеРегистрацииВНалоговомОргане.КодНалоговогоОргана = СведенияОНалоговомОргане.КодНалоговогоОргана;
		ДанныеРегистрацииВНалоговомОргане.КодПоОКТМО          = СведенияОНалоговомОргане.КодПоОКТМО;
		ДанныеРегистрацииВНалоговомОргане.КодПоОКАТО          = СведенияОНалоговомОргане.КодПоОКАТО;
	КонецЕсли;
	
	Возврат ДанныеРегистрацииВНалоговомОргане;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьРеквизитыОрганизации(Команда)
	
	ИзменитьЭтап(1);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьРаботу(Команда)
	
	ПодготовитьНачалоРаботы();
	ОбновитьИнтерфейс();
	ОповеститьОЗавершенииПомощникаНачалаРаботы();
	
	ОткрытьФормуНачалаРаботы();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресИРеквизитыВашейИнспекции(Команда)
	
	ОрганизацииФормыКлиент.ПерейтиКСервисуОпределитьРеквизитыИФНС();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеОРегистрацииИП(Команда)
	
	ЗаявлениеРегистрацияИП();
	
КонецПроцедуры

&НаКлиенте
Процедура УведомлениеУСН(Команда)
	
	УведомлениеОПереходенНаУСН();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеНаПолучениеПатента(Команда)
	
	ЗаявлениеПолучениеПатента();
	
КонецПроцедуры

&НаКлиенте
Процедура КвитанцияНаОплатуГоспошлиныНажатие(Элемент)
	
	АдресХранилища = КвитанцияНаОплатуГоспошлины(Организация.Ссылка);
	
	ИмяФайла = НСтр("ru='Квитанция на оплату госпошлины.pdf'");
	
	Если АдресХранилища <> Неопределено Тогда
		ПолучитьФайл(АдресХранилища, ИмяФайла, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	РегистрацияОрганизацииКлиент.ОткрытьЭтап(НавигацияНомерШага - 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ХочуКонсультацию(Команда)
	
	ОткрытьФорму("Обработка.РегистрацияОрганизации.Форма.ФормаХочуКонсультацию");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ОпределенРегистрирующийОрган = ЗначениеЗаполнено(Форма.РегистрирующийОрганНаименование);
	Элементы.РегистрирующийОрганНаименование.Видимость = ОпределенРегистрирующийОрган;
	Элементы.ГруппаРегистрирующийОрганАдрес.Видимость  = ОпределенРегистрирующийОрган;
	Элементы.РегистрирующийОрганТелефон.Видимость      = ОпределенРегистрирующийОрган;
	Элементы.РегистрирующийОрганИнформация.Видимость   = Не ПустаяСтрока(Форма.РегистрирующийОрганИнформация) И ОпределенРегистрирующийОрган;
	Элементы.АдресИРеквизитыВашейИнспекции.Видимость   = Не ОпределенРегистрирующийОрган;
	
	Элементы.ГруппаУведомлениеОПереходеНаУСН.Видимость   = ПодаетсяУведомлениеОПереходеНаУСН(Форма.СтруктураУчетнойПолитики);
	Элементы.ГруппаЗаявлениеНаПолучениеПатента.Видимость = ПодаетсяЗаявлениеНаПолучениеПатента(Форма.СтруктураУчетнойПолитики);
	
	Элементы.УслугаКонсультации.Видимость = Форма.ИспользуетсяСервисРегистрации;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеРегистрацияИП()
	
	ЗаявлениеОРегистрации = НайтиЗаявлениеОРегистрации(Организация.Ссылка);
	
	Если ЗаявлениеОРегистрации<> Неопределено И ЗначениеЗаполнено(ЗаявлениеОРегистрации.Ссылка) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ЗаявлениеОРегистрации.Ссылка);
		ПараметрыФормы.Вставить("Организация", Организация.Ссылка);
		
		ФормаОтчета = ПолучитьФорму(ЗаявлениеОРегистрации.ИмяФормы, ПараметрыФормы, ЭтотОбъект, Организация.Ссылка);
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимах(ФормаОтчета);
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Заявление о регистрации не создано.'");
		ПоказатьПредупреждение(, ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнструкцияРегистрацияИП()
	
	ДанныеРегистрирующегоОргана = Новый Структура;
	ДанныеРегистрирующегоОргана.Вставить("НаименованиеФНС", РегистрирующийОрганНаименование);
	ДанныеРегистрирующегоОргана.Вставить("АдресФНС",        РегистрирующийОрганАдрес);
	ДанныеРегистрирующегоОргана.Вставить("ТелефонФНС",      РегистрирующийОрганТелефон);
	
	Инструкция = ИнструкцияРегистрацияИПНаСервере(СпособРегистрации, ДанныеРегистрирующегоОргана, СтруктураУчетнойПолитики);
	
	ВывестиИнструкцию(Инструкция);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПоместитьИнструкциюВоВременноеХранилищеНаСервере(Инструкция)
	
	АдресХранилища = Неопределено;
	ИмяФайла       = ПолучитьИмяВременногоФайла("pdf");
	
	Попытка
		Инструкция.Записать(ИмяФайла, ТипФайлаТабличногоДокумента.PDF);
		АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла));
		УдалитьФайлы(ИмяФайла);
	Исключение
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Запись инструкции по регистрации во временный файл'"),
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат АдресХранилища;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИнструкцияРегистрацияИПНаСервере(СпособРегистрации, ДанныеРегистрирующегоОргана, СтруктураУчетнойПолитики)
	
	ВыборСпецрежимаНалогообложения = Новый Структура;
	ВыборСпецрежимаНалогообложения.Вставить("УСН",    ПодаетсяУведомлениеОПереходеНаУСН(СтруктураУчетнойПолитики));
	ВыборСпецрежимаНалогообложения.Вставить("Патент", ПодаетсяЗаявлениеНаПолучениеПатента(СтруктураУчетнойПолитики));
	
	Возврат Обработки.РегистрацияОрганизации.ИнструкцияРегистрацияИП(
		СпособРегистрации, ВыборСпецрежимаНалогообложения, ДанныеРегистрирующегоОргана);
	
КонецФункции

&НаКлиенте
Процедура УведомлениеОПереходенНаУСН()
	
	УведомлениеОСпецРежимеНалогообложения = НайтиУведомлениеОПереходеНаУСН(Организация.Ссылка, СтруктураУчетнойПолитики);
	Если УведомлениеОСпецРежимеНалогообложения = Неопределено Тогда
		// Не требуется
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УведомлениеОСпецРежимеНалогообложения.Ссылка) Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", УведомлениеОСпецРежимеНалогообложения.Ссылка);
		ПараметрыФормы.Вставить("Организация", Организация.Ссылка);
		
		ФормаОтчета = ПолучитьФорму(УведомлениеОСпецРежимеНалогообложения.ИмяФормы, ПараметрыФормы, ЭтотОбъект, Организация.Ссылка);
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимах(ФормаОтчета);
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Уведомление о переходе на УСН не создано'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Уведомление");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КвитанцияНаОплатуГоспошлины(Знач ОрганизацияСсылка)
	
	Возврат Обработки.РегистрацияОрганизации.КвитанцияНаОплатуГоспошлины(ОрганизацияСсылка);
	
КонецФункции

&НаКлиенте
Процедура ЗаявлениеПолучениеПатента()
	
	УведомлениеОСпецРежимеНалогообложения = НайтиЗаявлениеПолучениеПатента(Организация.Ссылка, СтруктураУчетнойПолитики);
	Если УведомлениеОСпецРежимеНалогообложения = Неопределено Тогда
		// Не требуется
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Организация", Организация.Ссылка);
	Если СпособРегистрации = 2 Тогда
		ПараметрыЗаполнения.Вставить("ПризнакПодписанта", "2");
	Иначе
		ПараметрыЗаполнения.Вставить("ПризнакПодписанта", "1");
		ПараметрыЗаполнения.Вставить("НомерТелефонаПодписанта", КонтактнаяИнформацияПолеТелефонОрганизации);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", УведомлениеОСпецРежимеНалогообложения.Ссылка);
	ПараметрыФормы.Вставить("Организация", Организация.Ссылка);
	ПараметрыФормы.Вставить("СформироватьФормуОтчетаАвтоматически", Истина);
	ПараметрыФормы.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	ОткрытьФорму(УведомлениеОСпецРежимеНалогообложения.ИмяФормы, ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	ДанныеПомощникаРегистрации = Обработки.РегистрацияОрганизации.ДанныеПомощникаРегистрации();
	
	// Восстановим ссылку на организацию
	ОрганизацияСсылка = ДанныеПомощникаРегистрации.Заявление.Ссылка;
	ПрочитатьДанныеОрганизации(ОрганизацияСсылка);
	// Если на предыдущих этапах, что-то было изменено, то установим модифицированность у формы.
	Если ЗначениеЗаполнено(ОрганизацияСсылка) И ДанныеПомощникаРегистрации.Модифицированность Тогда
		Модифицированность = Истина;
	КонецЕсли;
	ИспользуетсяСервисРегистрации = Не ПустаяСтрока(Константы.АдресСервисаРегистрации.Получить());
	
	УзнатьИНН = НСтр("ru = 'Узнать ИНН'");
	
	// Установим данные ОКВЭД
	ВидДеятельности = ДанныеПомощникаРегистрации.ВидДеятельности;
	Если Организация.КодОКВЭД2 <> ВидДеятельности.ОсновнойКод Тогда
		Организация.КодОКВЭД2 = ВидДеятельности.ОсновнойКод;
	КонецЕсли;
	Если Организация.НаименованиеОКВЭД2 <> ВидДеятельности.ОсновноеНаименование Тогда
		Организация.НаименованиеОКВЭД2 = ВидДеятельности.ОсновноеНаименование;
	КонецЕсли;
	Если Организация.ДополнительныеКодыОКВЭД2 <> ВидДеятельности.ДополнительныеКоды Тогда
		Организация.ДополнительныеКодыОКВЭД2 = ВидДеятельности.ДополнительныеКоды;
	КонецЕсли;
	
	// Установим учетную политику
	ОрганизацииФормы.ПрочитатьУчетнуюПолитику(ЭтотОбъект, Организация.Ссылка);
	СохраненнаяСтруктураУчетнойПолитики = ДанныеПомощникаРегистрации.Налоги.СтруктураУчетнойПолитики;
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(СтруктураУчетнойПолитики, СохраненнаяСтруктураУчетнойПолитики, Истина);
	
	ПрочитатьДанныеЗаявления();
	ПрочитатьФизическоеЛицо();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеОрганизации(ОрганизацияСсылка)
	
	Если ТипЗнч(ОрганизацияСсылка) = Тип("СправочникСсылка.Организации")
		И ОбщегоНазначения.СсылкаСуществует(ОрганизацияСсылка) Тогда
		ОрганизацияОбъект = ОрганизацияСсылка.ПолучитьОбъект();
		// СтандартныеПодсистемы.КонтактнаяИнформация
		УправлениеКонтактнойИнформацией.ПриЧтенииНаСервере(ЭтотОбъект, ОрганизацияОбъект);
		// Конец СтандартныеПодсистемы.КонтактнаяИнформация
		ЗначениеВРеквизитФормы(ОрганизацияОбъект, "Организация");
		ОпределитьРегистрирующийОрган();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьЭтап(НомерЭтапа)
	
	Если НомерЭтапа = 1 Тогда
		
		Если Не ЗначениеЗаполнено(КонтактнаяИнформацияПолеEmailОрганизации)
			И Не ЗначениеЗаполнено(КонтактнаяИнформацияПолеТелефонОрганизации) Тогда
			ПрочитатьКонтактнуюИнформациюПользователя();
		КонецЕсли;
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаВводДанных;
		Элементы.СформироватьДокументы.КнопкаПоУмолчанию = Истина;
		
	ИначеЕсли НомерЭтапа = 2 Тогда
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаОжидание;
		
	ИначеЕсли НомерЭтапа = 3 Тогда
		
		ОпределитьРегистрирующийОрган();
		
		КвитанцияОбОплатеГоспошлины = 
			Обработки.РегистрацияОрганизации.ПрисоединенныйФайлКвитанцииНаОплатуГоспошлиныОрганизации(Организация.Ссылка);
		КвитанцияОбОплатеГоспошлиныПолучена = КвитанцияОбОплатеГоспошлины <> Неопределено
							И НЕ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КвитанцияОбОплатеГоспошлины, "ПометкаУдаления");
		
		Элементы.ГруппаИнструкцииПоОплатеГоспошлиныБезКвитанции.Видимость = Не КвитанцияОбОплатеГоспошлиныПолучена;
		Элементы.ГруппаИнструкцииПоОплатеГоспошлиныСКвитанцией.Видимость  = КвитанцияОбОплатеГоспошлиныПолучена;
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.СтраницаРегистрация;
		Элементы.НачатьРаботу.КнопкаПоУмолчанию = Истина;
		
		УстановитьВидимостьБаннераНачалоРаботы();
		
		ТекущийЭлемент = Элементы.ХочуКонсультацию;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	ТекущийЭтап = НомерЭтапа;
	
	РазместитьНавигациюНаСервере();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодаетсяУведомлениеОПереходеНаУСН(СтруктураУчетнойПолитики)
	
	Возврат Обработки.РегистрацияОрганизации.ПодаетсяУведомлениеОПереходеНаУСН(СтруктураУчетнойПолитики);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПодаетсяЗаявлениеНаПолучениеПатента(СтруктураУчетнойПолитики)
	
	Если СтруктураУчетнойПолитики <> Неопределено
		И СтруктураУчетнойПолитики.Свойство("ПрименяетсяУСНПатент")
		И СтруктураУчетнойПолитики.ПрименяетсяУСНПатент Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КонтактнаяИнформацияПолеЮрАдресОрганизации");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"КонтактнаяИнформацияПолеЮрАдресОрганизации",
		ВидСравненияКомпоновкиДанных.Равно,
		УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки());
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
КонецПроцедуры

&НаКлиенте
Процедура ФИОПриИзменении()
	
	ОрганизацииФормыКлиент.ФИОПриИзменении(Организация);
	
	Если Не Организация.ИндивидуальныйПредприниматель.Пустая() Тогда
		// Изменяется существующий элемент справочника
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Организация.ФамилияИП) Или ПустаяСтрока(Организация.ИмяИП) Тогда
		// Искать физическое лицо рано
		Возврат;
	КонецЕсли;
	
	Фамилия = Организация.ФамилияИП;
	Имя = Организация.ИмяИП;
	Отчество = Организация.ОтчествоИП;
	
	ИндивидуальныйПредприниматель = НайтиФизическоеЛицо(Фамилия, Имя, Отчество, Организация.ИНН);
	
	Если ЗначениеЗаполнено(ИндивидуальныйПредприниматель) Тогда
		
		Организация.ИндивидуальныйПредприниматель = ИндивидуальныйПредприниматель;
		ПрочитатьФизическоеЛицо();
		
		Если ПустаяСтрока(Организация.ИНН) Тогда
			
			Организация.ИНН = ФизическоеЛицо.ИНН;
			ИННОбработатьИзменение();
			
			// Восстановим ФИО, если различаются
			Организация.ФамилияИП = Фамилия;
			Организация.ИмяИП = Имя;
			Организация.ОтчествоИП = Отчество;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННОбработатьИзменение()
	
	ПроверитьСоответствиеТребованиямИНН();
	
	Если Не Организация.Ссылка.Пустая() Тогда
		// Изменяется существующий элемент справочника
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Организация.ИНН) Тогда
		Возврат;
	КонецЕсли;
	
	ОрганизацияСсылка = НайтиОрганизацию(Организация.ИНН);
	Если ЗначениеЗаполнено(ОрганизацияСсылка) Тогда
		УстановитьОрганизацию(ОрганизацияСсылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСоответствиеТребованиямИНН()
	
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиков.ПроверитьСоответствиеТребованиямИНН(Организация.ИНН, Ложь);
	
	Если ЗначениеЗаполнено(РезультатПроверки.ОписаниеОшибки) Тогда
		ЦветВыделенияНекорректногоЗначения = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветВыделенияКонтрагентаСОшибкой");
		НадписьПоясненияНекорректногоИНН   = Новый ФорматированнаяСтрока(РезультатПроверки.ОписаниеОшибки, , ЦветВыделенияНекорректногоЗначения);
	Иначе
		НадписьПоясненияНекорректногоИНН  = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиФизическоеЛицо(Знач Фамилия, Знач Имя, Знач Отчество = "", Знач ИНН = "")
	
	Возврат Справочники.Организации.ФизическоеЛицо(Фамилия, Имя, Отчество, ИНН);
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиОрганизацию(Знач ИНН = "")
	
	Если ПустаяСтрока(ИНН) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.ИНН = &ИНН
	|	И НЕ Организации.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьОрганизацию(ОрганизацияСсылка)
	
	ДанныеПомощникаРегистрации = Обработки.РегистрацияОрганизации.ДанныеПомощникаРегистрации();
	ДанныеПомощникаРегистрации.Заявление.Ссылка = ОрганизацияСсылка;
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьКонтактнуюИнформациюПользователя()
	
	// Получим контактные данные пользователя
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если ЗначениеЗаполнено(ТекущийПользователь) Тогда
		
		МассивПользователей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекущийПользователь);
		МассивВидовКИ = Новый Массив;
		МассивВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации.EmailПользователя);
		МассивВидовКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонПользователя);
		ТаблицаКонтактнойИнформации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(МассивПользователей, , МассивВидовКИ);
		Для Каждого СтрокаКонтактнойИнформции Из ТаблицаКонтактнойИнформации Цикл
			
			Если СтрокаКонтактнойИнформции.Вид = Справочники.ВидыКонтактнойИнформации.EmailПользователя
				И Не ЗначениеЗаполнено(КонтактнаяИнформацияПолеEmailОрганизации) Тогда
				
				КонтактнаяИнформацияПолеEmailОрганизации = СтрокаКонтактнойИнформции.Представление;
				НайденныеСтроки = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(
					Новый Структура("ИмяРеквизита", "КонтактнаяИнформацияПолеEmailОрганизации"));
				Если НайденныеСтроки <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], СтрокаКонтактнойИнформции, "Представление, ЗначенияПолей");
				КонецЕсли;
				
			ИначеЕсли СтрокаКонтактнойИнформции.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонПользователя
				И Не ЗначениеЗаполнено(КонтактнаяИнформацияПолеТелефонОрганизации) Тогда
				
				КонтактнаяИнформацияПолеТелефонОрганизации = СтрокаКонтактнойИнформции.Представление;
				ОрганизацииФормыКлиентСервер.ТелефонПриИзменении(ЭтотОбъект, Элементы.КонтактнаяИнформацияПолеТелефонОрганизации);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьНачалоРаботы()
	
	// Установить стандартный интерфейс.
	
	Константы.НачалоРаботы.Установить(Ложь);
	
	Константы.РегистрацияНеЗавершена.Установить(Истина);
	
	УстановитьВидимостьБаннераНачалоРаботы();
	
	ОбщегоНазначенияБПВызовСервера.УстановитьСтандартныйИнтерфейс();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьБаннераНачалоРаботы()
	
	ВидимостьБаннераНачалоРаботы = ПолучитьФункциональнуюОпцию("ПредпринимательВСервисе") И Константы.НачалоРаботы.Получить();
	
	Элементы.НачалоРаботы.Видимость = ВидимостьБаннераНачалоРаботы;
	Элементы.НачатьРаботу.КнопкаПоУмолчанию = ВидимостьБаннераНачалоРаботы;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНачалаРаботы()
	
	ОткрытьФорму("Обработка.НачалоРаботы.Форма.НачалоРаботыПредпринимательВСервисе");
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОЗавершенииПомощникаНачалаРаботы()
	Оповестить("ЗавершенаРаботаПомощникаНачалаРаботы", ЭтотОбъект.ИмяФормы, ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьФизическоеЛицо()
	
	Если НЕ ЗначениеЗаполнено(Организация.ИндивидуальныйПредприниматель) Тогда
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицоОбъект = Организация.ИндивидуальныйПредприниматель.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ФизическоеЛицоОбъект, "ФизическоеЛицо");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизЛицо",      Организация.ИндивидуальныйПредприниматель);
	Запрос.УстановитьПараметр("ВидДокумента", Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументыФизическихЛиц.Период КАК Период,
	|	ДокументыФизическихЛиц.ФизЛицо КАК ФизЛицо,
	|	ДокументыФизическихЛиц.ВидДокумента КАК ВидДокумента,
	|	ДокументыФизическихЛиц.Серия КАК Серия,
	|	ДокументыФизическихЛиц.Номер КАК Номер,
	|	ДокументыФизическихЛиц.ДатаВыдачи КАК ДатаВыдачи,
	|	ДокументыФизическихЛиц.СрокДействия КАК СрокДействия,
	|	ДокументыФизическихЛиц.КемВыдан КАК КемВыдан,
	|	ДокументыФизическихЛиц.КодПодразделения,
	|	ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность КАК ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ДокументыФизическихЛиц.Представление КАК Представление
	|ИЗ
	|	РегистрСведений.ДокументыФизическихЛиц.СрезПоследних(
	|			,
	|			Физлицо = &ФизЛицо
	|				И ВидДокумента = &ВидДокумента) КАК ДокументыФизическихЛиц";
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ПаспортныеДанные, Выборка);
		СерияНомерПаспорта = Выборка.Серия + " " + Выборка.Номер;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеЗаявления()
	
	ЗаявлениеОРегистрации = НайтиЗаявлениеОРегистрации(Организация.Ссылка);
	
	Если ЗаявлениеОРегистрации <> Неопределено И ЗначениеЗаполнено(ЗаявлениеОРегистрации.Ссылка) Тогда
		ДанныеУведомления = Отчеты.РегламентированноеУведомлениеФормаР21001.ДанныеУведомления(ЗаявлениеОРегистрации.Ссылка);
		ДанныеУведомления.Свойство("СпособРегистрации", СпособРегистрации);
		ДатаСозданияЗаявления = ЗаявлениеОРегистрации.Дата;
	Иначе
		СпособРегистрации = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьРегистрирующийОрган()
	
	РеквизитыРегистрирующегоОргана = Обработки.РегистрацияОрганизации.РеквизитыРегистрирующегоОргана(
		Организация.КодНалоговогоОргана, Ложь);
	Если РеквизитыРегистрирующегоОргана <> Неопределено Тогда
		РегистрирующийОрганНаименование = РеквизитыРегистрирующегоОргана.Наименование;
		РегистрирующийОрганТелефон      = РеквизитыРегистрирующегоОргана.Телефон;
		РегистрирующийОрганАдрес        = РеквизитыРегистрирующегоОргана.Адрес;
		РегистрирующийОрганИнформация   = РеквизитыРегистрирующегоОргана.Информация;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СведенияОНалоговомОрганеПоАдресу(Адрес)
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СведенияОНалоговомОрганеПоАдресу = АдресныйКлассификаторБП.КодыАдреса(Адрес, "Сервис1С");
	
	КодНалоговогоОрганаПоАдресу = Формат(СведенияОНалоговомОрганеПоАдресу.КодИФНСФЛ, "ЧЦ=4; ЧДЦ=; ЧВН=; ЧГ=0");
	
	Если ЗначениеЗаполнено(КодНалоговогоОрганаПоАдресу) Тогда
		
		Сведения = Новый Структура();
		Сведения.Вставить("КодНалоговогоОргана", КодНалоговогоОрганаПоАдресу);
		Сведения.Вставить("КодПоОКТМО", Формат(СведенияОНалоговомОрганеПоАдресу.ОКТМО, "ЧДЦ=; ЧГ=0"));
		Сведения.Вставить("КодПоОКАТО", Формат(СведенияОНалоговомОрганеПоАдресу.OKATO, "ЧДЦ=; ЧГ=0"));
		
		Возврат Сведения;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияФоновогоЗаданияФормированияДокументов(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОбработатьРезультатВыполненияФоновогоЗаданияФормированияДокументов(ДлительнаяОперация.АдресРезультата);
		
		ОповеститьОбИзменении(Организация.Ссылка);
		ОповеститьОбИзменении(Организация.ИндивидуальныйПредприниматель);
		
	Иначе
		ПоказатьОшибкуФормированияДокументов(ДлительнаяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеЗаявления()
	
	ДанныеЗаявления = Обработки.РегистрацияОрганизации.НовыйДанныеЗаявления();
	ДанныеЗаявления.Фамилия           = Организация.ФамилияИП;
	ДанныеЗаявления.Имя               = Организация.ИмяИП;
	ДанныеЗаявления.Отчество          = Организация.ОтчествоИП;
	ДанныеЗаявления.Пол               = Строка(ФизическоеЛицо.Пол);
	ДанныеЗаявления.ИНН               = Организация.ИНН;
	ДанныеЗаявления.Телефон           = КонтактнаяИнформацияПолеТелефонОрганизации;
	ДанныеЗаявления.Email             = КонтактнаяИнформацияПолеEmailОрганизации;
	ДанныеЗаявления.СпособРегистрации = Обработки.РегистрацияОрганизации.НазваниеСпособаРегистрации(СпособРегистрации);
	ДанныеЗаявления.АдресXML = УправлениеКонтактнойИнформациейБП.ЗначенияПолейВидаКонтактнойИнформации(
		КонтактнаяИнформацияОписаниеДополнительныхРеквизитов, Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
		
	Возврат ДанныеЗаявления;
	
КонецФункции

&НаСервере
Процедура ОбработатьРезультатВыполненияФоновогоЗаданияФормированияДокументов(АдресРезультата)
	
	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	ДанныеЗаявления = ДанныеЗаявления();
	ДанныеЗаявления.Ссылка = РезультатВыполнения.Организация;
	Обработки.РегистрацияОрганизации.СохранитьДанныеЗаявления(ДанныеЗаявления);
	
	ПодготовитьФормуНаСервере();
	
	Модифицированность = Ложь;
	
	ИзменитьЭтап(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуФормированияДокументов(ДлительнаяОперация)
	
	// Если при создании документов прозошла ошибка,
	// то нужно вернуться на этап заполнения данных и вывести сообщение об ошибке.
	ИзменитьЭтап(1);
	
	Текст = НСтр("ru = 'Ошибка при формировании документов:'");
	Предупреждение = Новый Структура;
	Предупреждение.Вставить("Текст",    Текст + Символы.ПС + ДлительнаяОперация.КраткоеПредставлениеОшибки);
	Предупреждение.Вставить("Подробно", ДлительнаяОперация.ПодробноеПредставлениеОшибки);
	СтандартныеПодсистемыКлиент.ВывестиПредупреждение(ЭтотОбъект, Предупреждение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиЗаявлениеОРегистрации(Знач ОрганизацияСсылка)
	
	Возврат Обработки.РегистрацияОрганизации.НайтиЗаявлениеОРегистрации(ОрганизацияСсылка, "Р21001");
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиУведомлениеОПереходеНаУСН(Знач ОрганизацияСсылка, Знач СтруктураУчетнойПолитики)
	
	Если Не ПодаетсяУведомлениеОПереходеНаУСН(СтруктураУчетнойПолитики) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОПереходеНаУСН;
	ИмяФормыПоУмолчанию = Отчеты.РегламентированноеУведомлениеПереходНаУСН.ПолучитьФормуПоУмолчанию();
	
	УведомлениеОСпецРежимеНалогообложения = Обработки.РегистрацияОрганизации.НайтиУведомлениеОСпецРежимеНалогообложения(
																	ОрганизацияСсылка, ВидУведомления, ИмяФормыПоУмолчанию);
	
	Возврат УведомлениеОСпецРежимеНалогообложения;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиЗаявлениеПолучениеПатента(Знач ОрганизацияСсылка, Знач СтруктураУчетнойПолитики)
	
	Если Не ПодаетсяЗаявлениеНаПолучениеПатента(СтруктураУчетнойПолитики) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ЗаявлениеНаПолучениеПатентаРекомендованнаяФорма;
	ИмяФормыПоУмолчанию = УчетПСНВызовСервера.ИмяФормыЗаявления(ВидУведомления);
	
	УведомлениеОСпецРежимеНалогообложения = Обработки.РегистрацияОрганизации.НайтиУведомлениеОСпецРежимеНалогообложения(
																	ОрганизацияСсылка, ВидУведомления, ИмяФормыПоУмолчанию);
	
	Возврат УведомлениеОСпецРежимеНалогообложения;
	
КонецФункции

&НаКлиенте
Процедура ВывестиИнструкцию(Инструкция)
	
	АдресХранилища = ПоместитьИнструкциюВоВременноеХранилищеНаСервере(Инструкция);
	
	ИмяФайла = "Инструкция по регистрации ИП.pdf";
	
	Попытка
		ПолучитьФайл(АдресХранилища, ИмяФайла, Истина);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не удалось записать файл.'"));
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура РазместитьНавигациюНаСервере()
	
	Если ТекущийЭтап = 1 Тогда
		СтруктураПараметров = Новый Структура("НавигацияПараметрФормы", "Заявление");
		Обработки.РегистрацияОрганизации.РазместитьНавигацию(ЭтотОбъект, СтруктураПараметров);
		Элементы.ГруппаНавигация.Доступность = Истина;
	Иначе
		СтруктураПараметров = Новый Структура("НавигацияПараметрФормы", "Документы");
		Обработки.РегистрацияОрганизации.РазместитьНавигацию(ЭтотОбъект, СтруктураПараметров);
		Элементы.ГруппаНавигация.Доступность = (ТекущийЭтап <> 2);
	КонецЕсли;
	
КонецПроцедуры

#Область КонтактнаяИнформация

&НаСервере
Функция ЗначенияПолейКонтактнойИнформации(Элемент)
	
	Отбор = Новый Структура("ИмяРеквизита");
	Отбор.ИмяРеквизита = Элемент.Имя;
	ДанныеИсточника = КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	Если ДанныеИсточника.Количество() > 0 Тогда
		Возврат ДанныеИсточника[0].ЗначенияПолей;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура ПослеИзмененияКонтактнойИнформации(Результат) Экспорт
	
	Если Результат.ИмяРеквизита = "КонтактнаяИнформацияПолеЮрАдресОрганизации" Тогда
		
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриИзменении(Элемент)
	
	УправлениеКонтактнойИнформациейКлиент.ПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыОткрытия = Новый Структура("Страна", Организация.СтранаРегистрации);
	УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент, , СтандартнаяОбработка, ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияПриНажатии(Элемент, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.НачалоВыбора(ЭтотОбъект, Элемент,, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияОчистка(Элемент, СтандартнаяОбработка)
	
	УправлениеКонтактнойИнформациейКлиент.Очистка(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияВыполнитьКоманду(Команда)
	
	УправлениеКонтактнойИнформациейКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда.Имя);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ОбновитьКонтактнуюИнформацию(Результат)
	
	УправлениеКонтактнойИнформацией.ОбновитьКонтактнуюИнформацию(ЭтотОбъект, Организация, Результат);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеПаспортныхДанных(Отказ)
	
	ДопустимоВФИОКромеКириллицы = "-. 0123456789";
	
	Если НЕ ЗначениеЗаполнено(Организация.ФамилияИП) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Фамилия'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация.ФамилияИП", , Отказ);
	ИначеЕсли Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Организация.ФамилияИП, Истина, ДопустимоВФИОКромеКириллицы) Тогда
		ТекстСообщения = НСтр("ru = 'Фамилия заполняется на русском языке'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация.ФамилияИП", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Организация.ИмяИП) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Имя'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация.ИмяИП", , Отказ);
	ИначеЕсли Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Организация.ИмяИП, Истина, ДопустимоВФИОКромеКириллицы) Тогда
		ТекстСообщения = НСтр("ru = 'Имя заполняется на русском языке'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация.ИмяИП", , Отказ);
	КонецЕсли;
	
	Если Не СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(Организация.ОтчествоИП, Истина, ДопустимоВФИОКромеКириллицы) Тогда
		ТекстСообщения = НСтр("ru = 'Отчество заполняется на русском языке'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Организация.ОтчествоИП", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо.Пол) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Пол'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ФизическоеЛицо.Пол", , Отказ);
	КонецЕсли;
	
	// Паспортные данные
	
	Если Не ЗначениеЗаполнено(СерияНомерПаспорта) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Серия и номер'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СерияНомерПаспорта", , Отказ);
	Иначе
		ЦифрыСерииНомераПаспорта = ОрганизацииФормыКлиентСервер.ТолькоЦифры(СокрЛП(СерияНомерПаспорта));
		Если СтрДлина(ЦифрыСерииНомераПаспорта) <> 10 Тогда
			ТекстСообщения = НСтр("ru = 'Серия и номер паспорта должны состоять из 10 цифр'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "СерияНомерПаспорта", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПаспортныеДанные.КемВыдан) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Кем выдан'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПаспортныеДанные.КемВыдан", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПаспортныеДанные.ДатаВыдачи) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Дата выдачи'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПаспортныеДанные.ДатаВыдачи", , Отказ);
	ИначеЕсли ПаспортныеДанные.ДатаВыдачи > ТекущаяДатаСеанса() Тогда
		ТекстСообщения = НСтр("ru = 'Дата выдачи паспорта должна быть раньше текущей даты'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПаспортныеДанные.ДатаВыдачи", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПаспортныеДанные.КодПодразделения) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Код подразделения'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПаспортныеДанные.КодПодразделения", , Отказ);
	Иначе
		ЦифрыКодаПодразделения = ОрганизацииФормыКлиентСервер.ТолькоЦифры(СокрЛП(ПаспортныеДанные.КодПодразделения));
		Если СтрДлина(ЦифрыКодаПодразделения) <> 6 Тогда
			ТекстСообщения = НСтр("ru = 'Код подразделения должен состоять из 6 цифр'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ПаспортныеДанные.КодПодразделения", , Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо.ДатаРождения) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Дата рождения'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ФизическоеЛицо.ДатаРождения", , Отказ);
	ИначеЕсли ФизическоеЛицо.ДатаРождения > ТекущаяДатаСеанса() Тогда
		ТекстСообщения = НСтр("ru = 'Дата рождения должна быть раньше текущей даты'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ФизическоеЛицо.ДатаРождения", , Отказ);
	ИначеЕсли ЗначениеЗаполнено(ПаспортныеДанные.ДатаВыдачи) И ФизическоеЛицо.ДатаРождения > ПаспортныеДанные.ДатаВыдачи Тогда
		ТекстСообщения = НСтр("ru = 'Дата рождения должна быть раньше даты выдачи паспорта'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ФизическоеЛицо.ДатаРождения", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицо.МестоРожденияПредставление) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, , НСтр("ru = 'Место рождения'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ФизическоеЛицо.МестоРожденияПредставление", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
