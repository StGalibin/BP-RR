Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
    СтандартнаяОбработка    = Ложь;
     
    Представление = "Спецификация "+ Данные.НомерСпецификации +" от " + Данные.Дата;
	
КонецПроцедуры
 
Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
    СтандартнаяОбработка    = Ложь;
     
    Поля.Добавить("НомерСпецификации");
	Поля.Добавить("Дата");
    	
КонецПроцедуры

Функция ПечатьСпецификации(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОриентацияСтраницы	= ОриентацияСтраницы.Портрет;
	ТабличныйДокумент.ИмяПараметровПечати	= "ПАРАМЕТРЫ_ПЕЧАТИ_Спецификация";
			
	Макет = Документы.СпецификацияКДоговору.ПолучитьМакет("Спецификация");
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпецификацияКДоговору.Товары.(
	|		Номенклатура,
	|		Количество,
	|		Цена,
	|		Сумма,
	|		СуммаНДС
	|	),
	|	СпецификацияКДоговору.НомерСпецификации,
	|	СпецификацияКДоговору.СрокОтгрузки,
	|	СпецификацияКДоговору.Дата КАК ДатаСпецификации,
	|	СпецификацияКДоговору.ДоговорКонтрагента.Номер КАК НомерДоговора,
	|	СпецификацияКДоговору.ДоговорКонтрагента.Дата КАК ДатаДоговора,
	|	СпецификацияКДоговору.Организация,
	|	СпецификацияКДоговору.Руководитель,
	|	СпецификацияКДоговору.ДолжностьРуководителя,
	|	СпецификацияКДоговору.РуководительКонтрагента,
	|	СпецификацияКДоговору.ДолжностьРуководителяКонтрагента,
	|	СпецификацияКДоговору.ЗаРуководителяКонтрагентаПоПриказу,
	|	СпецификацияКДоговору.ЗаРуководителяПоПриказу,
	|	СпецификацияКДоговору.ПолРуководителяКонтрагента,
	|	СпецификацияКДоговору.Ссылка,
	|	СпецификацияКДоговору.Контрагент,
	|	СпецификацияКДоговору.Грузополучатель
	|ИЗ
	|	Документ.СпецификацияКДоговору КАК СпецификацияКДоговору
	|ГДЕ
	|	СпецификацияКДоговору.Ссылка В(&МассивОбъектов)";
	Запрос.Параметры.Вставить("МассивОбъектов", МассивОбъектов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
    ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьТоварыШапка = Макет.ПолучитьОбласть("ТоварыШапка");
	ОбластьТовары = Макет.ПолучитьОбласть("Товары");
	ОбластьДанные = Макет.ПолучитьОбласть("Данные");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ТабличныйДокумент.Очистить();

	ПервыйДокумент = Истина;
	
	Пока Выборка.Следующий() Цикл 
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;

	ДатаДоговора = Формат (Выборка.ДатаДоговора, "ДФ='dd.MM.yyyy ""г.""'");
	СрокОтгрузки = Формат (Выборка.СрокОтгрузки, "ДФ='dd.MM.yyyy ""г.""'");
	СуммаДокумента = 0;
	СуммаНДСДокумента = 0;
	ОбластьШапка.Параметры.Заполнить(Выборка);
	ОбластьШапка.Параметры.ДатаДоговора = ДатаДоговора;
	
	СведенияОбОрганизации    = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Выборка.Организация, Выборка.ДатаСпецификации);
ПредставлениеОрганизации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "НаименованиеДляПечатныхФорм,");
ОбластьШапка.Параметры.Организация = ПредставлениеОрганизации;
ОбластьШапка.Параметры.ПодписантОрганизация = СклонениеПредставленийОбъектов.ПросклонятьПредставление(Выборка.ДолжностьРуководителя.Наименование, 2,Выборка.Ссылка);
ОбластьШапка.Параметры.ФИОПодписантаОрганизация = СклонениеПредставленийОбъектов.ПросклонятьФИО(Выборка.Руководитель.ФИО, 2, Выборка.Ссылка, Выборка.Руководитель.Пол);
ОбластьШапка.Параметры.ОснованиеПолномочийОрганизация = ? (ЗначениеЗаполнено(Выборка.ЗаРуководителяПоПриказу),
СклонениеПредставленийОбъектов.ПросклонятьПредставление(Выборка.ЗаРуководителяПоПриказу, 2,Выборка.Ссылка), "Устава"); 
//данные контрагента
СведенияОКонтрагенте = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Выборка.Контрагент, Выборка.ДатаСпецификации);
ПредставлениеКонтрагента = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОКонтрагенте, "НаименованиеДляПечатныхФорм,");
ОбластьШапка.Параметры.Контрагент = ПредставлениеКонтрагента;
ОбластьШапка.Параметры.ПодписантКонтрагент = СклонениеПредставленийОбъектов.ПросклонятьПредставление(Выборка.ДолжностьРуководителяКонтрагента, 2,Выборка.Ссылка);
ОбластьШапка.Параметры.ФИОПодписантаКонтрагент = СклонениеПредставленийОбъектов.ПросклонятьФИО(Выборка.РуководительКонтрагента, 2, Выборка.Ссылка, Выборка.ПолРуководителяКонтрагента);
ОбластьШапка.Параметры.ОснованиеПолномочийКонтрагент = ? (ЗначениеЗаполнено(Выборка.ЗаРуководителяКонтрагентаПоПриказу),
СклонениеПредставленийОбъектов.ПросклонятьПредставление(Выборка.ЗаРуководителяКонтрагентаПоПриказу, 2,Выборка.Ссылка), "Устава"); 
Грузополучатель = ? (ЗначениеЗаполнено (Выборка.Грузополучатель), Выборка.Грузополучатель, Выборка.Контрагент);

СведенияОГрузополучателе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Грузополучатель, Выборка.ДатаСпецификации);
	
		
	ТабличныйДокумент.Вывести(ОбластьШапка);

		ТабличныйДокумент.Вывести(ОбластьТоварыШапка);
		ВыборкаТовары = Выборка.Товары.Выбрать();
		Пока ВыборкаТовары.Следующий() Цикл
			СуммаДокумента = СуммаДокумента + ВыборкаТовары.Сумма;
			СуммаНДСДокумента = СуммаНДСДокумента + ВыборкаТовары.СуммаНДС;
			ОбластьТовары.Параметры.Заполнить(ВыборкаТовары);
			ТабличныйДокумент.Вывести(ОбластьТовары, ВыборкаТовары.Уровень());
		КонецЦикла;
		
		ОбластьДанные.Параметры.Заполнить(Выборка);
		ОбластьДанные.Параметры.ДатаДоговора = ДатаДоговора;
		ОбластьДанные.Параметры.СрокОтгрузки = СрокОтгрузки;
		ОбластьДанные.Параметры.СуммаДокумента = Формат (СуммаДокумента,"ЧДЦ=2") ;
		ОбластьДанные.Параметры.СуммаДокументаПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СуммаДокумента, ВалютаРегламентированногоУчета);
		ОбластьДанные.Параметры.СуммаНДС = Формат (СуммаНДСДокумента, "ЧДЦ=2");
		ОбластьДанные.Параметры.СуммаНДСПрописью = ОбщегоНазначенияБПВызовСервера.СформироватьСуммуПрописью(СуммаНДСДокумента, ВалютаРегламентированногоУчета);
		ОбластьДанные.Параметры.Заполнить(СведенияОГрузополучателе);
		
		
		ТабличныйДокумент.Вывести(ОбластьДанные);
		
		ОбластьПодвал.Параметры.Организация = ПредставлениеОрганизации;
		ОбластьПодвал.Параметры.ПодписантОрганизация = Выборка.ДолжностьРуководителя.Наименование;
		ОбластьПодвал.Параметры.ФИОПОдписантаОрганизация = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.Руководитель.ФИО);

		ОбластьПодвал.Параметры.Контрагент = ПредставлениеКонтрагента;
		ОбластьПодвал.Параметры.ПодписантКонтрагент = Выборка.ДолжностьРуководителяКонтрагента;
		ОбластьПодвал.Параметры.ФИОПОдписантаКонтрагент = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(Выборка.РуководительКонтрагента);

		ТабличныйДокумент.Вывести(ОбластьПодвал);

	КонецЦикла;
		
Возврат ТабличныйДокумент;

КонецФункции

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Спецификация";
	КомандаПечати.Представление = НСтр("ru = 'Спецификация'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Порядок       = 10;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм,ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Спецификация") Тогда
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
			"Спецификация",
			"Спецификация к договору поставки",
			ПечатьСпецификации(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.СпецификацияКДоговору.Спецификация");
	КонецЕсли;
			
	ОбщегоНазначенияБП.ЗаполнитьДополнительныеПараметрыПечати(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);	
	
КонецПроцедуры
