
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойств = "АдресХранилищаСчетаФактурыВыданныеПокупателям, Организация, ДатаВходящегоДокумента, ДокументОснование,"
		+ "ВидСчетаФактуры, Комитент, ДоговорКомитента";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойств);
	
	Если Параметры.Свойство("ИсправлениеСобственнойОшибки") Тогда 
		ИсправлениеСобственнойОшибки = Параметры.ИсправлениеСобственнойОшибки;
		ИсправляемыйСчетФактура = Параметры.ИсправляемыйСчетФактура;
		Дата = Параметры.Дата;
	КонецЕсли;
	
	Если Параметры.Свойство("Исправление") Тогда 
		Исправление = Параметры.Исправление;
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(АдресХранилищаСчетаФактурыВыданныеПокупателям) Тогда
		ЗагрузитьТаблицуСчетаФактурыВыданныеПокупателямИзВременногоХранилища(АдресХранилищаСчетаФактурыВыданныеПокупателям);
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность И НЕ ПеренестиВДокумент Тогда
		
		Отказ = Истина;
		
		Оповещение = Новый ОписаниеОповещения("ВопросСохраненияДанныхЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
		
	ИначеЕсли ПеренестиВДокумент Тогда
		
		ОбработкаПроверкиЗаполненияНаКлиенте(Отказ);
		
		Если Отказ Тогда
			Модифицированность = Истина;
			ПеренестиВДокумент = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	
	Если ПеренестиВДокумент Тогда
		АдресХранилищаСчетаФактурыВыданныеПокупателям = ПоместитьТаблицуСчетаФактурыВыданныеПокупателямВоВременноеХранилище();
		СтруктураВозврата.Вставить("АдресХранилищаСчетаФактурыВыданныеПокупателям", АдресХранилищаСчетаФактурыВыданныеПокупателям);
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоОтчетуКомитенту(Команда)
	
	ЕстьДанные = СчетаФактурыВыданныеПокупателям.Количество() > 0;
	
	Если ЕстьДанные Тогда
		Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьСчетаФактурыВыданныеЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Табличная часть будет очищена. Заполнить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьСчетаФактурыВыданные();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаПроверкиЗаполненияНаКлиенте(Отказ)
	
	Для Индекс = 0 По СчетаФактурыВыданныеПокупателям.Количество() - 1 Цикл
		
		СтрокаСчетаФактуры = СчетаФактурыВыданныеПокупателям[Индекс];
		
		Префикс = "СчетаФактурыВыданныеПокупателям[%1]";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(Индекс, "ЧН=0; ЧГ="));
				
		ИмяСписка = "Счета-фактуры выданные покупателям";
				
		Если НЕ ЗначениеЗаполнено(СтрокаСчетаФактуры.СчетФактура) Тогда
			Поле = Префикс + ".СчетФактура";
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Счет-фактура'"),
					Индекс + 1, ИмяСписка);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСчетаФактурыВыданные()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаВходящегоДокумента", ДатаВходящегоДокумента);
	Запрос.УстановитьПараметр("ДокументОснование"     , ДокументОснование);
	Запрос.УстановитьПараметр("Организация"           , Организация);
	Запрос.УстановитьПараметр("Комитент"              , Комитент);
	Запрос.УстановитьПараметр("ДоговорКомитента"      , ДоговорКомитента);
	Запрос.УстановитьПараметр("Исправление"           , Исправление);
	
	Если ИсправлениеСобственнойОшибки Тогда
		
		Запрос.УстановитьПараметр("Ссылка", ИсправляемыйСчетФактура);
		
		Запрос.Текст = ТекстЗапросаИсправлениеСобственнойОшибки();
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаПоступление И НЕ Исправление Тогда
		
		Запрос.Текст = ТекстЗапросаСчетФактураНаПоступление();
		
	ИначеЕсли ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.Корректировочный
		ИЛИ (Исправление И НЕ ВидСчетаФактуры = Перечисления.ВидСчетаФактурыПолученного.НаАванс) Тогда
		
		Запрос.Текст = ТекстЗапросаИсправлениеКорректировка();
		
	Иначе
		
		Запрос.Текст = ТекстЗапросаПрочиеСчетаФактуры();
		
	КонецЕсли;
	
	СчетаФактурыВыданныеПокупателям.Очистить();
	СчетаФактурыВыданныеПокупателям.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаИсправлениеСобственнойОшибки()
	
	Возврат Документы.СчетФактураПолученный.ТекстЗапросаСчетаФактурыВыданныеПоИсправлениюСобственнойОшибки();
	
КонецФункции

&НаСервере
Функция ТекстЗапросаСчетФактураНаПоступление()
	
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацииПоОтчету.Покупатель КАК Покупатель
	|ПОМЕСТИТЬ ПокупателиПоОтчету
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Товары КАК РеализацииПоОтчету
	|ГДЕ
	|	РеализацииПоОтчету.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Покупатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализованныеТоварыКомитентов.Покупатель,
	|	РеализованныеТоварыКомитентов.Субкомиссионер,
	|	РеализованныеТоварыКомитентов.СчетФактураСубкомиссионера КАК СчетФактура,
	|	СУММА(РеализованныеТоварыКомитентов.Выручка) КАК Сумма,
	|	СУММА(РеализованныеТоварыКомитентов.СуммаНДС) КАК НДС
	|ПОМЕСТИТЬ ПродажиПоСубкомиссии
	|ИЗ
	|	РегистрНакопления.РеализованныеТоварыКомитентов КАК РеализованныеТоварыКомитентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПокупателиПоОтчету КАК ПокупателиПоОтчету
	|		ПО РеализованныеТоварыКомитентов.Покупатель = ПокупателиПоОтчету.Покупатель
	|ГДЕ
	|	РеализованныеТоварыКомитентов.ДатаРеализации = &ДатаВходящегоДокумента
	|	И РеализованныеТоварыКомитентов.Активность
	|	И РеализованныеТоварыКомитентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РеализованныеТоварыКомитентов.ДоговорКонтрагента = &ДоговорКомитента
	|	И РеализованныеТоварыКомитентов.Контрагент = &Комитент
	|	И РеализованныеТоварыКомитентов.Организация = &Организация
	|	И РеализованныеТоварыКомитентов.Регистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализованныеТоварыКомитентов.Покупатель,
	|	РеализованныеТоварыКомитентов.Субкомиссионер,
	|	РеализованныеТоварыКомитентов.СчетФактураСубкомиссионера
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализованныеТоварыКомитентов.Покупатель,
	|	РеализованныеТоварыКомитентов.Субкомиссионер,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	СУММА(РеализованныеТоварыКомитентов.Выручка) КАК Сумма,
	|	СУММА(РеализованныеТоварыКомитентов.СуммаНДС) КАК НДС
	|ПОМЕСТИТЬ ПродажиПоКомиссии
	|ИЗ
	|	РегистрНакопления.РеализованныеТоварыКомитентов КАК РеализованныеТоварыКомитентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО РеализованныеТоварыКомитентов.Регистратор = СчетФактураВыданныйДокументыОснования.ДокументОснование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПокупателиПоОтчету КАК ПокупателиПоОтчету
	|		ПО РеализованныеТоварыКомитентов.Покупатель = ПокупателиПоОтчету.Покупатель
	|ГДЕ
	|	РеализованныеТоварыКомитентов.ДатаРеализации = &ДатаВходящегоДокумента
	|	И РеализованныеТоварыКомитентов.Активность
	|	И РеализованныеТоварыКомитентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РеализованныеТоварыКомитентов.Контрагент = &Комитент
	|	И РеализованныеТоварыКомитентов.ДоговорКонтрагента = &ДоговорКомитента
	|	И РеализованныеТоварыКомитентов.Организация = &Организация
	|	И НЕ РеализованныеТоварыКомитентов.Регистратор ССЫЛКА Документ.ОтчетКомиссионераОПродажах
	|	И НЕ РеализованныеТоварыКомитентов.Регистратор ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|	И НЕ РеализованныеТоварыКомитентов.Регистратор ССЫЛКА Документ.ОтчетКомитентуОПродажах
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализованныеТоварыКомитентов.Покупатель,
	|	РеализованныеТоварыКомитентов.Субкомиссионер,
	|	СчетФактураВыданныйДокументыОснования.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализованныеТоварыКомитентов.Покупатель,
	|	РеализованныеТоварыКомитентов.Субкомиссионер,
	|	СчетФактураВыданныйДокументыОснования.Ссылка КАК СчетФактура,
	|	СУММА(РеализованныеТоварыКомитентов.Выручка) КАК Сумма,
	|	СУММА(РеализованныеТоварыКомитентов.СуммаНДС) КАК НДС
	|ПОМЕСТИТЬ ПродажиОтгруженныхТоваров
	|ИЗ
	|	РегистрНакопления.РеализованныеТоварыКомитентов КАК РеализованныеТоварыКомитентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПокупателиПоОтчету КАК ПокупателиПоОтчету
	|		ПО РеализованныеТоварыКомитентов.Покупатель = ПокупателиПоОтчету.Покупатель
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураВыданныйДокументыОснования
	|		ПО (ВЫРАЗИТЬ(РеализованныеТоварыКомитентов.Регистратор КАК Документ.РеализацияОтгруженныхТоваров).ДокументОтгрузки = СчетФактураВыданныйДокументыОснования.ДокументОснование)
	|ГДЕ
	|	РеализованныеТоварыКомитентов.ДатаРеализации = &ДатаВходящегоДокумента
	|	И РеализованныеТоварыКомитентов.Активность
	|	И РеализованныеТоварыКомитентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РеализованныеТоварыКомитентов.Контрагент = &Комитент
	|	И РеализованныеТоварыКомитентов.ДоговорКонтрагента = &ДоговорКомитента
	|	И РеализованныеТоварыКомитентов.Организация = &Организация
	|	И РеализованныеТоварыКомитентов.Регистратор ССЫЛКА Документ.РеализацияОтгруженныхТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализованныеТоварыКомитентов.Покупатель,
	|	РеализованныеТоварыКомитентов.Субкомиссионер,
	|	СчетФактураВыданныйДокументыОснования.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродажиПоСубкомиссии.Покупатель,
	|	ПродажиПоСубкомиссии.Субкомиссионер,
	|	ПродажиПоСубкомиссии.СчетФактура,
	|	ВЫБОР
	|		КОГДА ПродажиПоСубкомиссии.НДС > 0
	|			ТОГДА ПродажиПоСубкомиссии.Сумма
	|		ИНАЧЕ ПродажиПоСубкомиссии.СчетФактура.СуммаДокумента
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПродажиПоСубкомиссии.НДС > 0
	|			ТОГДА ПродажиПоСубкомиссии.НДС
	|		ИНАЧЕ ПродажиПоСубкомиссии.СчетФактура.СуммаНДСДокументаКомиссия
	|	КОНЕЦ КАК НДС
	|ИЗ
	|	ПродажиПоСубкомиссии КАК ПродажиПоСубкомиссии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПродажиПоКомиссии.Покупатель,
	|	ПродажиПоКомиссии.Субкомиссионер,
	|	ПродажиПоКомиссии.СчетФактура,
	|	ВЫБОР
	|		КОГДА ПродажиПоКомиссии.НДС > 0
	|			ТОГДА ПродажиПоКомиссии.Сумма
	|		ИНАЧЕ ПродажиПоКомиссии.СчетФактура.СуммаДокумента
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПродажиПоКомиссии.НДС > 0
	|			ТОГДА ПродажиПоКомиссии.НДС
	|		ИНАЧЕ ПродажиПоКомиссии.СчетФактура.СуммаНДСДокументаКомиссия
	|	КОНЕЦ КАК НДС
	|ИЗ
	|	ПродажиПоКомиссии КАК ПродажиПоКомиссии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПродажиОтгруженныхТоваров.Покупатель,
	|	ПродажиОтгруженныхТоваров.Субкомиссионер,
	|	ПродажиОтгруженныхТоваров.СчетФактура,
	|	ВЫБОР
	|		КОГДА ПродажиОтгруженныхТоваров.НДС > 0
	|			ТОГДА ПродажиОтгруженныхТоваров.Сумма
	|		ИНАЧЕ ПродажиОтгруженныхТоваров.СчетФактура.СуммаДокумента
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПродажиОтгруженныхТоваров.НДС > 0
	|			ТОГДА ПродажиОтгруженныхТоваров.НДС
	|		ИНАЧЕ ПродажиОтгруженныхТоваров.СчетФактура.СуммаНДСДокументаКомиссия
	|	КОНЕЦ КАК НДС
	|ИЗ
	|	ПродажиОтгруженныхТоваров КАК ПродажиОтгруженныхТоваров";
	
КонецФункции

&НаСервере
Функция ТекстЗапросаИсправлениеКорректировка()
	
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РеализацииПоОтчету.Покупатель КАК Покупатель
	|ПОМЕСТИТЬ ПокупателиПоОтчету
	|ИЗ
	|	Документ.ОтчетКомитентуОПродажах.Товары КАК РеализацииПоОтчету
	|ГДЕ
	|	РеализацииПоОтчету.Ссылка = &ДокументОснование
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Покупатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументКорректировкиРеализации.Ссылка КАК ДокументРеализации
	|ПОМЕСТИТЬ ДокументыРеализации
	|ИЗ
	|	Документ.КорректировкаРеализации КАК ДокументКорректировкиРеализации
	|ГДЕ
	|	ДокументКорректировкиРеализации.Организация = &Организация
	|	И НАЧАЛОПЕРИОДА(ДокументКорректировкиРеализации.Дата, ДЕНЬ) = &ДатаВходящегоДокумента
	|	И ДокументКорректировкиРеализации.Контрагент В
	|			(ВЫБРАТЬ
	|				ПокупателиПоОтчету.Покупатель
	|			ИЗ
	|				ПокупателиПоОтчету)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураШапка.Ссылка КАК СчетФактура,
	|	СчетФактураШапка.СуммаДокументаКомиссия КАК Сумма,
	|	СчетФактураШапка.СуммаНДСДокументаКомиссия КАК НДС,
	|	НЕОПРЕДЕЛЕНО КАК Субкомиссионер,
	|	СчетФактураШапка.Контрагент КАК Покупатель,
	|	СчетФактураОснования.СуммаУвеличение,
	|	СчетФактураОснования.СуммаУменьшение,
	|	СчетФактураОснования.СуммаНДСУвеличение,
	|	СчетФактураОснования.СуммаНДСУменьшение
	|ИЗ
	|	ДокументыРеализации КАК ДокументыРеализации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РеализованныеТоварыКомитентов КАК РеализованныеТоварыКомитентов
	|		ПО ДокументыРеализации.ДокументРеализации = РеализованныеТоварыКомитентов.Регистратор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.ДокументыОснования КАК СчетФактураОснования
	|		ПО ДокументыРеализации.ДокументРеализации = СчетФактураОснования.ДокументОснование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК СчетФактураШапка
	|		ПО (СчетФактураОснования.Ссылка = СчетФактураШапка.Ссылка)
	|ГДЕ
	|	НЕ СчетФактураШапка.ПометкаУдаления";
	
КонецФункции

&НаСервере
Функция ТекстЗапросаПрочиеСчетаФактуры()
	
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураВыданный.Контрагент КАК Покупатель,
	|	СчетФактураВыданный.Ссылка КАК СчетФактура,
	|	СчетФактураВыданный.СуммаДокумента КАК Сумма,
	|	СчетФактураВыданный.СуммаНДСДокумента КАК НДС,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(СчетФактураВыданный.ДокументОснование) = ТИП(Документ.ОтчетКомиссионераОПродажах)
	|			ТОГДА ЕСТЬNULL(ОтчетКомиссионераОПродажах.Контрагент, НЕОПРЕДЕЛЕНО)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Субкомиссионер
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|		ПО СчетФактураВыданный.ДокументОснование = ОтчетКомиссионераОПродажах.Ссылка
	|ГДЕ
	|	СчетФактураВыданный.Организация = &Организация
	|	И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|	И НАЧАЛОПЕРИОДА(СчетФактураВыданный.Дата, ДЕНЬ) = &ДатаВходящегоДокумента
	|	И СчетФактураВыданный.Комитент = &Комитент
	|	И НЕ ЕСТЬNULL(ОтчетКомиссионераОПродажах.ВыписыватьСчетаФактурыСводно, ЛОЖЬ)
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Исправление = &Исправление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураАвансы.Контрагент,
	|	СчетФактураВыданный.Ссылка,
	|	СчетФактураАвансы.Сумма,
	|	СчетФактураАвансы.СуммаНДС,
	|	ОтчетКомиссионераОПродажах.Контрагент
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактураВыданный
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах КАК ОтчетКомиссионераОПродажах
	|		ПО СчетФактураВыданный.ДокументОснование = ОтчетКомиссионераОПродажах.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный.Авансы КАК СчетФактураАвансы
	|		ПО (СчетФактураАвансы.Ссылка = СчетФактураВыданный.Ссылка)
	|ГДЕ
	|	СчетФактураВыданный.Организация = &Организация
	|	И СчетФактураВыданный.ВидСчетаФактуры = ЗНАЧЕНИЕ(Перечисление.ВидСчетаФактурыВыставленного.НаАвансКомитента)
	|	И НАЧАЛОПЕРИОДА(СчетФактураВыданный.Дата, ДЕНЬ) = &ДатаВходящегоДокумента
	|	И СчетФактураВыданный.Комитент = &Комитент
	|	И ОтчетКомиссионераОПродажах.ВыписыватьСчетаФактурыСводно
	|	И НЕ СчетФактураВыданный.ПометкаУдаления
	|	И СчетФактураВыданный.Исправление = &Исправление";
	
КонецФункции

&НаСервере
Процедура ЗагрузитьТаблицуСчетаФактурыВыданныеПокупателямИзВременногоХранилища(АдресХранилища)

	ТаблицаСчетаФактурыВыданныеПокупателям = ПолучитьИзВременногоХранилища(АдресХранилища);
	СчетаФактурыВыданныеПокупателям.Загрузить(ТаблицаСчетаФактурыВыданныеПокупателям);

КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуСчетаФактурыВыданныеПокупателямВоВременноеХранилище()
	
	ТаблицаСчетаФактурыВыданныеПокупателям = СчетаФактурыВыданныеПокупателям.Выгрузить();
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаСчетаФактурыВыданныеПокупателям, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ВопросСохраненияДанныхЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьСчетаФактурыВыданныеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаполнитьСчетаФактурыВыданные();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НастройкиУсловногоОформления = Новый Структура();
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетаФактурыВыданныеПокупателямСуммаУвеличение");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетаФактурыВыданныеПокупателямСуммаНДСУвеличение");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетаФактурыВыданныеПокупателямСуммаУменьшение");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СчетаФактурыВыданныеПокупателямСуммаНДСУменьшение");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"ВидСчетаФактуры",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.ВидСчетаФактурыПолученного.Корректировочный);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

#КонецОбласти
