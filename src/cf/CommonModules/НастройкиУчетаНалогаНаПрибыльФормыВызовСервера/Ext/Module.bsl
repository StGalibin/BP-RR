
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Серверные вызовы методов формы записи регистра сведений НастройкиУчетаНалогаНаПрибыль
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция НастройкиСистемыНалогообложения(Организация, Период) Экспорт
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("ПлательщикЕНВД", УчетнаяПолитика.ПлательщикЕНВДЗаПериод(Организация, НачалоГода(Период), КонецГода(Период)));
	
	Возврат Настройки;
	
КонецФункции

Функция НаличиеСписковПрямыхРасходов(Организация, Период) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПериодДействия", НачалоГода(Период));
	Запрос.УстановитьПараметр("ПрошлыйПериодДействия", НачалоГода(НачалоГода(Период) - 1));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ПериодДействия,
	|	МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Организация
	|ИЗ
	|	РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ КАК МетодыОпределенияПрямыхРасходовПроизводстваВНУ
	|ГДЕ
	|	МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ПериодДействия = &ПериодДействия
	|	И МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Организация = &Организация
	|
	|;
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ПериодДействия,
	|	МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Организация
	|ИЗ
	|	РегистрСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ КАК МетодыОпределенияПрямыхРасходовПроизводстваВНУ
	|ГДЕ
	|	МетодыОпределенияПрямыхРасходовПроизводстваВНУ.ПериодДействия = &ПрошлыйПериодДействия
	|	И МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Организация = &Организация
	|
	|";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();

	НаличиеСписков = Новый Структура();
	НаличиеСписков.Вставить("ЕстьСписокТекущегоПериода", НЕ РезультатыЗапроса[0].Пустой());
	НаличиеСписков.Вставить("ЕстьСписокПрошлогоПериода", НЕ РезультатыЗапроса[1].Пустой());

	Возврат НаличиеСписков;

КонецФункции

Процедура ЗаполнитьСписокПрямыхРасходов(Организация, Период, Скопировать = Неопределено) Экспорт
	
	РегистрыСведений.МетодыОпределенияПрямыхРасходовПроизводстваВНУ.Настроить(
		Организация,
		НачалоГода(Период),
		Скопировать);
	
КонецПроцедуры

#КонецОбласти