
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиент-серверные методы формы записи регистра сведений НастройкиСистемыНалогообложения
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиСистемыНалогообложения;
	
	Элементы.ГруппаПереходНаУСН.Видимость =
		Форма.СистемаНалогообложенияПредставление = 1
		ИЛИ Форма.СистемаНалогообложенияПредставление = 2;
		
	Элементы.ДатаПереходаНаУСН.Доступность = Форма.ПоложенияПереходногоПериодаУСН;
	Элементы.ДатаПереходаНаУСН.АвтоОтметкаНезаполненного = Форма.ПоложенияПереходногоПериодаУСН;
	
	Элементы.ГруппаЕНВД.Видимость =
		Форма.СистемаНалогообложенияПредставление <> 3
		И Форма.СистемаНалогообложенияПредставление <> 4;
		
	Элементы.ГруппаТорговыйСбор.Видимость =
		Форма.СистемаНалогообложенияПредставление <> 3
		И Форма.СистемаНалогообложенияПредставление <> 4;
		
	Элементы.ГруппаСпецрежим.Видимость =
		Элементы.ГруппаЕНВД.Видимость 
		ИЛИ Элементы.ГруппаПатент.Видимость
		ИЛИ Элементы.ГруппаТорговыйСбор.Видимость;
	
	Если Форма.ЭтоЮрЛицо Тогда
		Элементы.ГруппаПатент.Видимость = Ложь;
	Иначе
		Элементы.ГруппаПатент.Видимость =
			Форма.СистемаНалогообложенияПредставление <> 3
			И Форма.СистемаНалогообложенияПредставление <> 4;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСписокВыбораСистемНалогообложения(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.НастройкиСистемыНалогообложения;
	
	СписокВыбора = Элементы.СистемаНалогообложенияПредставление.СписокВыбора;
	
	СписокВыбора.Очистить();
	
	СписокВыбора.Добавить(
				ИндексСистемыНалогообложения(
					ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая")),
				НСтр("ru = 'Общая'"));
				
		СписокВыбора.Добавить(
				ИндексСистемыНалогообложения(
					ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"),
					,
					,
					ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы")),
				НСтр("ru = 'Упрощенная (доходы)'"));
				
	СписокВыбора.Добавить(
				ИндексСистемыНалогообложения(
					ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная"),
					,
					,
					ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы")),
				НСтр("ru = 'Упрощенная (доходы минус расходы)'"));
				
	Если НЕ Форма.ЭтоЮрЛицо 
       И ТарификацияБПВызовСервераПовтИсп.РазрешенУчетРегулярнойДеятельности() Тогда
		
		СписокВыбора.Добавить(
				ИндексСистемыНалогообложения(
					ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок"),
					Ложь,
					Истина),
				НСтр("ru = 'Только ЕНВД'"));
				
		Если Запись.Период >= '20130101' Тогда //ДатаНачалаДействияПатентнойСистемы
			
			СписокВыбора.Добавить(
				ИндексСистемыНалогообложения(
					ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок"),
					Истина,
					Ложь),
				НСтр("ru = 'Только патент'"));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИндексСистемыНалогообложения(СистемаНалогообложения, ПрименяетсяУСНПатент = Ложь, ПлательщикЕНВД = Ложь, ОбъектНалогообложенияУСН = Неопределено) Экспорт
	
	Если СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая") Тогда
		Индекс = 0;
	ИначеЕсли СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок") Тогда
		Если ПрименяетсяУСНПатент Тогда
			Индекс = 4;
		ИначеЕсли ПлательщикЕНВД Тогда
			Индекс = 3;
		КонецЕсли;
	Иначе
		Если ОбъектНалогообложенияУСН = ПредопределенноеЗначение("Перечисление.ОбъектыНалогообложенияПоУСН.Доходы") Тогда
			Индекс = 1;
		Иначе
			Индекс = 2;
		КонецЕсли;
	КонецЕсли;
		
	Возврат Индекс;
	
КонецФункции

Функция СистемаНалогообложенияПоИндексу(Индекс) Экспорт
	
	Если Индекс = 0 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Общая");
	ИначеЕсли Индекс = 1 ИЛИ Индекс = 2 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.Упрощенная");
	ИначеЕсли Индекс = 4 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок");
	ИначеЕсли Индекс = 3 Тогда
		СистемаНалогообложения = ПредопределенноеЗначение("Перечисление.СистемыНалогообложения.ОсобыйПорядок");
	Иначе
		СистемаНалогообложения = Неопределено;
	КонецЕсли;
	
	Возврат СистемаНалогообложения;
	
КонецФункции

#КонецОбласти