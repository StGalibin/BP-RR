#Область КонтактнаяИнформация

Процедура НастроитьЭлементыКонтактнойИнформации(Форма) Экспорт
	
	// Инициализируем только адрес и телефон, представленные на форме реквизитами, созданными для них.
	// Остальные поля контактной информации не отображаются на форме.
	// Это обеспечивается последним параметром (ОтложеннаяИнициализация)
	
	ИсключаемыеВиды = Новый Массив();
	ИсключаемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	ИсключаемыеВиды.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	
	УправлениеКонтактнойИнформацией.ПриСозданииНаСервере(Форма, Форма.Объект, "КонтактныеДанные", , ИсключаемыеВиды, Истина);
	
КонецПроцедуры

// Заполняет свойства объекта, хранящие контактную информацию и зависящие от адреса
//
// Параметры:
//  Форма			 - УправляемаяФорма - форма организации с реквизитами из подсистемы контактной информации
//                     (в т.ч. КонтактнаяИнформацияОписаниеДополнительныхРеквизитов)
//  ТекущийОбъект	 - СправочникОбъект.Организации - записываемый объект
//  Отказ			 - Булево - возвращаемый параметр, Истина, если так скажет подсистема контактной информации
//
Процедура ПередЗаписьюКонтактнойИнформации(Форма, ТекущийОбъект, Отказ) Экспорт
	
	КонтейнерКонтактнойИнформации = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов; // Навязано подсистемой контактной информации
	ВидИнформацииАдрес = Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации;
	
	ВидыДополнительныхАдресов = Новый Массив;
	ВидыДополнительныхАдресов.Добавить(Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
	ВидыДополнительныхАдресов.Добавить(Справочники.ВидыКонтактнойИнформации.ПочтовыйАдресОрганизации);
	
	АдресДоИзменения  = "";
	АдресаДоИзменения = Неопределено;
	ИзмененАдрес      = Истина;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда
		
		ВидыАдресов = Новый Массив;
		ВидыАдресов.Добавить(ВидИнформацииАдрес);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыАдресов, ВидыДополнительныхАдресов);
		
		АдресаДоИзменения = УправлениеКонтактнойИнформациейБП.КонтактнаяИнформацияОбъектовНаДату(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТекущийОбъект.Ссылка), , ВидыАдресов);
		
		АдресДоИзменения = УправлениеКонтактнойИнформациейБП.ЗначенияПолейВидаКонтактнойИнформации(
			АдресаДоИзменения, ВидИнформацииАдрес);
		АдресПослеИзменения = ОрганизацииФормыДляОтчетностиКлиентСервер.АдресОрганизацииЗначенияПолей(
			КонтейнерКонтактнойИнформации);
		ИзмененАдрес = (АдресДоИзменения <> АдресПослеИзменения);
		
	КонецЕсли;
	
	УстановитьКодРегиона(ТекущийОбъект, КонтейнерКонтактнойИнформации); // Код региона устанавливается в доп. свойствах объекта
	
	УправлениеКонтактнойИнформацией.ПередЗаписьюНаСервере(Форма, ТекущийОбъект, Отказ);
	
	Если Не ИзмененАдрес Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийОбъект.КодОрганаФСГС = ""; // будет перезаполнен позже - см. УстановитьКодОрганаРосстата()
	
	Для Каждого ВидДополнительногоАдреса Из ВидыДополнительныхАдресов Цикл
		
		// Проверим, не задан ли особый адрес
		Если ЗначениеЗаполнено(АдресаДоИзменения) Тогда
			
			ДополнительныйАдресДоИзменения = УправлениеКонтактнойИнформациейБП.ЗначенияПолейВидаКонтактнойИнформации(
				АдресаДоИзменения, ВидДополнительногоАдреса);
			Если ЗначениеЗаполнено(ДополнительныйАдресДоИзменения) И ДополнительныйАдресДоИзменения <> АдресДоИзменения Тогда
				// был задан особый дополнительный адрес, он и останется
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		УправлениеКонтактнойИнформациейБП.СкопироватьКонтактнуюИнформацию(ТекущийОбъект.КонтактнаяИнформация, ВидИнформацииАдрес, ВидДополнительногоАдреса);
		
	КонецЦикла;
	
КонецПроцедуры

#Область ЮридическийАдрес

// Определяет код региона по адресу организации и помещает его в дополнительные свойства
//  для использования в ходе записи.
//  Адрес хранится в форме с использованием подсистемы КонтактнаяИнформация и иметь путь КонтактнаяИнформацияПолеЮрАдресОрганизации.
//
// Параметры:
//  Объект												 - СправочникОбъект.Организации	 - модифицируемый объект
//  КонтактнаяИнформацияОписаниеДополнительныхРеквизитов - ДанныеФормыКоллекция - коллекция, содержащая сведения об адресах (см. подсистему КонтактнаяИнформация)
//
Процедура УстановитьКодРегиона(Объект, КонтактнаяИнформацияОписаниеДополнительныхРеквизитов) Экспорт
	
	
	ЗначенияПолейАдреса = ОрганизацииФормыДляОтчетностиКлиентСервер.АдресОрганизацииЗначенияПолей(
		КонтактнаяИнформацияОписаниеДополнительныхРеквизитов);
	
	КодРегиона = УправлениеКонтактнойИнформациейБП.КодРегионаПоАдресу(ЗначенияПолейАдреса);
	
	Объект.ДополнительныеСвойства.Вставить("КодРегиона", КодРегиона);
	
КонецПроцедуры

// Заполняет реквизиты организации по ее юридическому адресу
//
// Параметры:
//  Объект				- ДанныеФормыСтруктура - объект, представляющий в форме данные организации
//  КодПоОКТМО			- Строка - заполняемый код по ОКТМО
//  ЗначенияПолейАдреса - см. АдресОрганизацииЗначенияПолей - данные об адресе
//
Процедура ЗаполнитьПоАдресу(Объект, КодПоОКТМО, ЗначенияПолейАдреса) Экспорт
	
	СведенияОНалоговомОргане = СведенияОНалоговомОрганеПоАдресу(ЗначенияПолейАдреса, Объект.ЮридическоеФизическоеЛицо);
	
	Если ЗначениеЗаполнено(СведенияОНалоговомОргане.КодПоОКТМО) Тогда
		КодПоОКТМО = СведенияОНалоговомОргане.КодПоОКТМО;
	КонецЕсли;
	Если ЗначениеЗаполнено(СведенияОНалоговомОргане.КодНалоговогоОргана) Тогда
		Объект.КодНалоговогоОргана = СведенияОНалоговомОргане.КодНалоговогоОргана;
	КонецЕсли;
	Если ЗначениеЗаполнено(СведенияОНалоговомОргане.НаименованиеНалоговогоОргана) Тогда
		Объект.НаименованиеНалоговогоОргана = СведенияОНалоговомОргане.НаименованиеНалоговогоОргана;
	КонецЕсли;
	
	Объект.КодОрганаФСГС = "";// Заполняется перед записью
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
	
#Область СистемаНалогообложения

// Выполняет первоначальную настройку элементов формы, отображающих выбранную организацией систему налогообложения
//
// Параметры:
//  УсловноеОформление - УсловноеОформление - условное оформление формы, содержащей реквизит СистемаНалогообложенияПредставление
//
Процедура НастроитьЭлементыСистемыНалогообложения(УсловноеОформление) Экспорт
	
	ИмяДанных = "СистемаНалогообложенияПредставление";
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, ИмяДанных);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		ИмяДанных,
		ВидСравненияКомпоновкиДанных.Равно,
		ОрганизацииФормыКлиентСервер.ТекстВыбораЗначенияПоГиперссылке());
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
КонецПроцедуры

// Заполняет реквизиты формы, отображающие выбранные организацией систему налогообложения
//
// Параметры:
//  СистемаНалогообложенияПредставление	 - Строка - заполняемое представление системы налогообложения
//  Организация							 - СправочникСсылка.Организации - организация, реквизиты которой отображаются в форме
//
Процедура ПрочитатьСистемуНалогообложения(СистемаНалогообложенияПредставление, Организация) Экспорт
	
	СистемаНалогообложенияПредставление = ОрганизацииФормыКлиентСервер.ПредставлениеСистемыНалогообложения(Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ГосударственныеОрганы

// Конструирует коллекцию, описывающую коды государственных органов, на территории которых находится организация.
//
// Параметры:
//  Объект	 - СправочникОбъект.Организации, ДанныеФормыКоллекция - элемент справочника Организации
//             или данные формы, отображающие данные элемента
// 
// Возвращаемое значение:
//  Соответствие - коды гос. органов:
//    * Ключ - ПеречислениеСсылка.ВидыГосударственныхОрганов - вид гос. органа
//    * Значение - Строка - код гос. органа
//
Функция КодыГосударственныхОрганов(Объект) Экспорт
	
	КодыГосударственныхОрганов = Новый Соответствие;
	КодыГосударственныхОрганов.Вставить(
		Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган,
		Объект.КодНалоговогоОргана);
	КодыГосударственныхОрганов.Вставить(
		Перечисления.ВидыГосударственныхОрганов.ОрганПФР,
		Объект.КодОрганаПФР);
	КодыГосударственныхОрганов.Вставить(
		Перечисления.ВидыГосударственныхОрганов.ОрганФСС,
		Объект.КодПодчиненностиФСС);
		
	Возврат КодыГосударственныхОрганов;
	
КонецФункции

// Получает из реквизитов формы информацию о налоговом органе (т.е. ту, что хранится в справочнике РегистрацииВНалоговомОргане)
//
// Параметры:
//  Форма							 - УправляемаяФорма - форма, в реквизитах которой хранятся данные
//  ПоляРегистрацииВНалоговомОргане	 - Структура - пути к данным о налоговом органе
//                                     * Ключ - имя реквизита справочника РегистрацииВНалоговомОргане
//                                     * Значение - путь к данным в форме
// 
// Возвращаемое значение:
//  Структура - данные о налоговом органе
//      * Ключ     - имя реквизита справочника РегистрацииВНалоговомОргане
//      * Значение - значение реквизита
//
Функция ЗначенияПолейРегистрацииВНалоговомОргане(Форма, ПоляРегистрацииВНалоговомОргане) Экспорт
	
	ЗначенияПолей = Новый Структура;
	
	Для Каждого ОписаниеПоля Из ПоляРегистрацииВНалоговомОргане Цикл
		ЗначениеПоля = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ОписаниеПоля.Значение);
		ЗначенияПолей.Вставить(ОписаниеПоля.Ключ, ЗначениеПоля);
	КонецЦикла;
	
	Возврат ЗначенияПолей;
	
КонецФункции

// Выполняет действия, необходимые перед записью реквизитов ФНС и Росстата, редактируемых в форме организации.
// См. также ЗаписатьРегистрациюВНалоговомОргане()
//
// Платежные реквизиты гос. органов, хранящиеся в справочнике Контрагенты, записываются до записи Организации,
// с помощью РеквизитыГосударственногоОрганаПоКоду()
//
// Параметры:
//  Объект									 - СправочникОбъект.Организации - записываемый элемент справочника
//  ЗначенияКлючевыхПолейРегистрацииНаФорме	 - см. ЗначенияПолейРегистрацииВНалоговомОргане() - значения полей, заданных на форме и 
//                                             позволяющих идентифицировать запись справочника РегистрацииВНалоговомОргане
//
Процедура НачатьЗаписьРеквизитовГосударственныхОрганов(Объект, ЗначенияКлючевыхПолейРегистрацииНаФорме) Экспорт
	
	УстановитьКодОрганаРосстата(Объект);
	
	УстановитьРегистрациюВНалоговомОргане(Объект, ЗначенияКлючевыхПолейРегистрацииНаФорме);
	
КонецПроцедуры
	
// Определяет и устанавливает организации код органа Росстата.
// Перед применением метода в объекте должен быть определен код региона с помощью УстановитьКодРегиона()
//
// Параметры:
//  Объект - СправочникОбъект.Организации - модифицируемый объект
//
Процедура УстановитьКодОрганаРосстата(Объект) Экспорт
	
	Если ЗначениеЗаполнено(Объект.КодОрганаФСГС) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Объект.ДополнительныеСвойства.Свойство("КодРегиона") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДополнительныеСвойства.КодРегиона) Тогда
		Возврат;
	КонецЕсли;
	
	КодРегионаСтрокой = Формат(Объект.ДополнительныеСвойства.КодРегиона, "ЧЦ=2; ЧН=; ЧВН=");
	
	ОтделенияРосстата = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ОтделенияРосстатаРегиона(КодРегионаСтрокой);
	Если ОтделенияРосстата.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	Объект.КодОрганаФСГС = ОтделенияРосстата[0].КодТОГС;
		
КонецПроцедуры

Функция СведенияОНалоговомОрганеПоАдресу(Знач ЗначенияПолейАдреса, Знач ЮридическоеФизическоеЛицо)

	Сведения = Новый Структура();
	Сведения.Вставить("КодНалоговогоОргана",                "");
	Сведения.Вставить("КодПоОКТМО",                         "");
	Сведения.Вставить("КодПоОКАТО",                         "");
	Сведения.Вставить("НаименованиеНалоговогоОргана",       "");
	
	Если Не ЗначениеЗаполнено(ЗначенияПолейАдреса) Тогда
		Возврат Сведения;
	КонецЕсли;
	
	СведенияОНалоговомОрганеПоАдресу = АдресныйКлассификаторБП.КодыАдреса(ЗначенияПолейАдреса, "Сервис1С");
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		ЧисловойКодИФНС = СведенияОНалоговомОрганеПоАдресу.КодИФНСФЛ;
	Иначе
		ЧисловойКодИФНС = СведенияОНалоговомОрганеПоАдресу.КодИФНСЮЛ;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЧисловойКодИФНС) И Не ЗначениеЗаполнено(СведенияОНалоговомОрганеПоАдресу.ОКТМО) Тогда
		Возврат Сведения;
	КонецЕсли;
	
	СтроковойКодИФНС = Формат(ЧисловойКодИФНС, "ЧЦ=4; ЧДЦ=; ЧВН=; ЧГ=0");
	НаименованиеНалоговогоОргана = ОрганизацииФормыДляОтчетностиВызовСервера.РеквизитыГосударственногоОрганаПоКоду(
		Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган,
		СтроковойКодИФНС);
	
	Сведения.КодНалоговогоОргана          = СтроковойКодИФНС;
	Сведения.КодПоОКТМО                   = Формат(СведенияОНалоговомОрганеПоАдресу.ОКТМО, "ЧДЦ=; ЧГ=0");
	Сведения.КодПоОКАТО                   = Формат(СведенияОНалоговомОрганеПоАдресу.OKATO, "ЧДЦ=; ЧГ=0");
	Сведения.НаименованиеНалоговогоОргана = НаименованиеНалоговогоОргана;
	
	Возврат Сведения;
		
КонецФункции

Процедура УстановитьРегистрациюВНалоговомОргане(Объект, ЗначенияКлючевыхПолейРегистрации)
	
	// Не используем управляемые блокировки,
	// потому что речь идет об изменении НСИ и вероятность взаимных изменений небольшая.
	// Не используем автоматические пессимистические объектные блокировки,
	// потому что структура данных не позволяет установить эксклюзивную блокировку
	// на сочетание ключевых полей регистрации в налоговом органе.
	
	КлючевыеПоляЗаполнены = Истина;
	Для Каждого КлючИЗначение Из ЗначенияКлючевыхПолейРегистрации Цикл
		Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			Продолжить;
		КонецЕсли;
		КлючевыеПоляЗаполнены = Ложь;
		Прервать;
	КонецЦикла;
	
	Если Не КлючевыеПоляЗаполнены Тогда
		// Недостаточно сведений о регистрации.
		Объект.РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.ПустаяСсылка();
		Возврат;
	КонецЕсли;
	
	РегистрацияВНалоговомОргане = Неопределено;
	Если ЗначениеЗаполнено(Объект.РегистрацияВНалоговомОргане) Тогда
		РегистрацияВНалоговомОргане = Объект.РегистрацияВНалоговомОргане.ПолучитьОбъект();
		// в Объект.РегистрацияВНалоговомОргане может содержаться ссылка на новую регистрацию,
		// если предыдущая попытка записи организации не удалась.
		// В этом случае РегистрацияВНалоговомОргане == Неопределено
	КонецЕсли;
		
	Если РегистрацияВНалоговомОргане = Неопределено Тогда
		// Возможно, в информационной базе уже есть подходящая регистрация
		ИскатьРегистрациюПоКлючевымПолям = Истина;
	Иначе
		// Проверим, подходит ли эта регистрация. Если не подходит - поищем более подходящую, не найдем - очистим.
		КлючевыеПоляСовпадают = Истина;
		Для Каждого КлючИЗначение Из ЗначенияКлючевыхПолейРегистрации Цикл
			Если РегистрацияВНалоговомОргане[КлючИЗначение.Ключ] = КлючИЗначение.Значение Тогда
				Продолжить;
			КонецЕсли;
			КлючевыеПоляСовпадают = Ложь;
			Прервать;
		КонецЦикла;
		ИскатьРегистрациюПоКлючевымПолям = Не КлючевыеПоляСовпадают;
	КонецЕсли;
	
	Если ИскатьРегистрациюПоКлючевымПолям Тогда
		
		// Не учитывается, что организация может быть обособленным подразделением,
		// так как процедура предназначена для форм, где не может быть обособленных подразделений
		
		Объект.РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане(
			Объект.Ссылка,
			Объект.КПП,
			Объект.КодНалоговогоОргана);
			
		Если ЗначениеЗаполнено(Объект.РегистрацияВНалоговомОргане) Тогда
			РегистрацияВНалоговомОргане = Объект.РегистрацияВНалоговомОргане.ПолучитьОбъект();
		Иначе
			РегистрацияВНалоговомОргане = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.РегистрацияВНалоговомОргане) Тогда
		Объект.РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.ПолучитьСсылку();
	КонецЕсли;
	
КонецПроцедуры

#Область РегистрацииВНалоговомОргане

// Методы работы со справочником РегистрацииВНалоговомОргане

// Помещает в реквизиты формы значения реквизитов налогового органа
//
// Параметры:
//  РегистрацияВНалоговомОргане	 - СправочникСсылка.РегистрацииВНалоговомОргане - налоговый орган, данные которого нужно прочитать
//  Форма						 - УправляемаяФорма - форма, в реквизиты которой следует поместить прочитанные реквизиты
//  ПоляРегистрацииНаФорме		 - Структура - описание реквизитов формы, предназначенных для работы с реквизитами налогового органа
//                                  * Ключ - имя реквизита справочника РегистрацииВНалоговомОргане
//                                  * Значение - путь в форме, отображающий значение этого реквизита
//
Процедура ПрочитатьДанныеНалоговогоОргана(РегистрацияВНалоговомОргане, Форма, ПоляРегистрацииНаФорме) Экспорт
	
	Если Не ЗначениеЗаполнено(РегистрацияВНалоговомОргане) Тогда
		Возврат;
	КонецЕсли;
	
	ИменаПолей = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(ПоляРегистрацииНаФорме);
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РегистрацияВНалоговомОргане, ИменаПолей);
	
	Для Каждого КлючИЗначение Из ПоляРегистрацииНаФорме Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
			Форма,
			КлючИЗначение.Значение,
			ЗначенияРеквизитов[КлючИЗначение.Ключ]);
	КонецЦикла;
	
КонецПроцедуры

// Записывает (создает, обновляет) элемент справочника РегистрацииВНалоговомОргане,
// соответствующий основной регистрации организации.
// Может вызываться из обработчика ПриЗаписи.
// До вызова следует в обработчике ПередЗаписью вызывать УстановитьРегистрациюВНалоговомОргане()
//
// Параметры:
//  Объект                       - СправочникОбъект.Организации - записываемый объект
//  ЗначенияПолейРегистрации	 - см. ЗначенияПолейРегистрацииВНалоговомОргане() - установленные при редактировании значения реквизитов налогового органа
//  РеквизитыНалоговогоОргана	 - см. ДанныеГосударственныхОрганов.НовыеРеквизитыГосударственногоОргана() - заранее полученные из открытых данных значения реквизитов налогового органа
//
Процедура ЗаписатьРегистрациюВНалоговомОргане(Объект, ЗначенияПолейРегистрации, РеквизитыНалоговогоОргана = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект.РегистрацияВНалоговомОргане) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрацияВНалоговомОргане = Объект.РегистрацияВНалоговомОргане.ПолучитьОбъект();
	
	Если РегистрацияВНалоговомОргане = Неопределено Тогда
		
		РегистрацияВНалоговомОргане = Справочники.РегистрацииВНалоговомОргане.СоздатьЭлемент();
		РегистрацияВНалоговомОргане.УстановитьСсылкуНового(Объект.РегистрацияВНалоговомОргане);
		
	ИначеЕсли Не РегистрацияВНалоговомОргане.ЭтоНовый() Тогда
		
		// Проверим, надо ли записывать - поменялось ли фактически что-то
		ВсеСвойстваСовпадают = Истина;
		Для Каждого КлючИЗначение Из ЗначенияПолейРегистрации Цикл
			Если РегистрацияВНалоговомОргане[КлючИЗначение.Ключ] = КлючИЗначение.Значение Тогда
				Продолжить;
			КонецЕсли;
			ВсеСвойстваСовпадают = Ложь;
			Прервать;
		КонецЦикла;
		
		Если ВсеСвойстваСовпадают Тогда
			Объект.ДополнительныеСвойства.Удалить("РегистрацияВНалоговомОргане");
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	РегистрацияВНалоговомОргане.Владелец = Объект.Ссылка;
	
	Для Каждого ОписаниеПоляРегистрации Из ЗначенияПолейРегистрации Цикл
		РегистрацияВНалоговомОргане[ОписаниеПоляРегистрации.Ключ] = ОписаниеПоляРегистрации.Значение;
	КонецЦикла;
	
	НаименованиеИзСправочника = ОрганизацииФормыДляОтчетностиВызовСервера.РеквизитыГосударственногоОрганаПоКоду(
		Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган,
		РегистрацияВНалоговомОргане.Код);
		
	Если Не ПустаяСтрока(НаименованиеИзСправочника) Тогда
		РегистрацияВНалоговомОргане.НаименованиеИФНС = НаименованиеИзСправочника;
	КонецЕсли;
	
	Если Объект.ДополнительныеСвойства.Свойство("КодРегиона") Тогда
		РегистрацияВНалоговомОргане.КодРегиона = Объект.ДополнительныеСвойства.КодРегиона;
	КонецЕсли;
	
	РегистрацияВНалоговомОргане.Записать();
	
	Объект.ДополнительныеСвойства.Удалить("РегистрацияВНалоговомОргане");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ЗаполнениеЕГР

// В форме организации заполняет реквизиты объекта и формы данными, полученными из госреестров.
//
// Параметры:
//  ОтветДанныхЕГР	- Структура - см. НовыйОтветДанныхЕГР(), результат запроса к сервису, предоставляющему данные госреестров
//  Форма  - УправляемаяФорма - форма элемента справочника Организации. 
//								Обязательные реквизиты формы:
//                              - Объект
//								- КодПоОКТМО
//								- КонтактнаяИнформацияОписаниеДополнительныхРеквизитов
//								- КонтактнаяИнформацияПолеТелефонОрганизации
//								- КонтактнаяИнформацияПолеЮрАдресОрганизации
//
Процедура ЗаполнитьДаннымиЕГР(ОтветДанныхЕГР, Форма) Экспорт
	
	Форма.Модифицированность = Истина;
	
	ДанныеЕГР = ПолучитьИзВременногоХранилища(ОтветДанныхЕГР.АдресДанных);
	УдалитьИзВременногоХранилища(ОтветДанныхЕГР.АдресДанных); // Самостоятельно очистим временное хранилище.
	
	Объект = Форма.Объект;
	
	// Общие реквизиты ИП и юр.лица.
	ЗаполнитьЗначенияСвойств(Объект, ДанныеЕГР, "ИНН, ДатаРегистрации");
	// Для ИП наименования нельзя брать из данных реестра, так как там они не соответствуют правилам,
	// принятым в программе.
	
	Объект.ОГРН = ДанныеЕГР.РегистрационныйНомер;

	Если ДанныеЕГР.ЭтоОКВЭД2 Тогда
		Объект.КодОКВЭД2          = ДанныеЕГР.КодОКВЭД;
		Объект.НаименованиеОКВЭД2 = ОрганизацииФормыКлиентСервер.ПрочитатьОКВЭД2(Объект.КодОКВЭД2);
	КонецЕсли;
	// ОКВЭД1 не используется с 2017 года
	
	Если ОтветДанныхЕГР.КраткиеДанные.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		
		// Реквизиты юридического лица
		ЗаполнитьЗначенияСвойств(Объект, ДанныеЕГР, "Наименование, НаименованиеПолное, НаименованиеСокращенное");
		
		Объект.КПП = ДанныеЕГР.КПП;
		ЗаполнитьКодыПравовойФормы(Объект, ДанныеЕГР.ПравоваяФорма);
		
		ЗаполнитьЭлементКонтактнойИнформации(Форма, "КонтактнаяИнформацияПолеТелефонОрганизации", ДанныеЕГР.Телефон);
		
		Если ЗначениеЗаполнено(ДанныеЕГР.ЮридическийАдрес) Тогда
			
			ЗаполнитьЭлементКонтактнойИнформации(Форма, "КонтактнаяИнформацияПолеЮрАдресОрганизации", ДанныеЕГР.ЮридическийАдрес);
			
			ЗаполнитьПоАдресу(Объект, Форма.КодПоОКТМО, ДанныеЕГР.ЮридическийАдрес.КонтактнаяИнформация);
			
		КонецЕсли;
		
		СведенияОРуководителе = Новый Структура;
		СведенияОРуководителе.Вставить("РуководительФамилия", "");
		СведенияОРуководителе.Вставить("РуководительИмя", "");
		СведенияОРуководителе.Вставить("РуководительОтчество", "");
		СведенияОРуководителе.Вставить("РуководительИНН", "");
		СведенияОРуководителе.Вставить("РуководительДолжность", Справочники.Должности.ПустаяСсылка());
		Если ЗначениеЗаполнено(ДанныеЕГР.Руководитель) Тогда
			СведенияОРуководителе.РуководительФамилия   = ДанныеЕГР.Руководитель.Фамилия;
			СведенияОРуководителе.РуководительИмя       = ДанныеЕГР.Руководитель.Имя;
			СведенияОРуководителе.РуководительОтчество  = ДанныеЕГР.Руководитель.Отчество;
			СведенияОРуководителе.РуководительИНН       = ДанныеЕГР.Руководитель.ИНН;
			СведенияОРуководителе.РуководительДолжность = ОтветственныеЛицаБП.ПолучитьСоздатьДолжность(ДанныеЕГР.Руководитель.Должность);
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Форма, СведенияОРуководителе);
	
	ИначеЕсли ОтветДанныхЕГР.КраткиеДанные.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		
		// Реквизиты индивидуального предпринимателя.
		
		Объект.ФамилияИП  = ДанныеЕГР.Фамилия;
		Объект.ИмяИП      = ДанныеЕГР.Имя;
		Объект.ОтчествоИП = ДанныеЕГР.Отчество;
		
		Если ЗначениеЗаполнено(ДанныеЕГР.СвидетельствоОРегистрации) Тогда
			
			Объект.СвидетельствоСерияНомер = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 №%2'"),
									ДанныеЕГР.СвидетельствоОРегистрации.Серия,
									ДанныеЕГР.СвидетельствоОРегистрации.Номер);
			Объект.СвидетельствоДатаВыдачи = ДанныеЕГР.СвидетельствоОРегистрации.Дата;
			
		КонецЕсли;
		
		// Данные о номере телефона и адресе физического лица ЕГРИП не предоставляет
		
	КонецЕсли;
	
	// Эти реквизиты могут быть определены по адресу - в ЗаполнитьПоАдресу().
	// А могут быть и не определены (например, для ИП или очищены при изменении адреса).
	// В последнем случае заполним их данными ЕГР
	Если ЗначениеЗаполнено(ДанныеЕГР.РегистрацияВНалоговомОргане) Тогда
		
		Если Не ЗначениеЗаполнено(Объект.КодНалоговогоОргана) Тогда
			Объект.КодНалоговогоОргана          = ДанныеЕГР.РегистрацияВНалоговомОргане.Код;
			Объект.НаименованиеНалоговогоОргана = ДанныеЕГР.РегистрацияВНалоговомОргане.Наименование;
			// сразу закешируем в информационной базе и платежные реквизиты
			ОрганизацииФормыДляОтчетностиВызовСервера.РеквизитыГосударственногоОрганаПоКоду(
				Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган,
				Объект.КодНалоговогоОргана);
		КонецЕсли;

		Если Не ЗначениеЗаполнено(Форма.КодПоОКТМО) Тогда
			Форма.КодПоОКТМО = ДанныеЕГР.РегистрацияВНалоговомОргане.ОКТМО;
		КонецЕсли;
		
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЕГР.РегистрацияВПенсионномФонде) Тогда
		
		Если ОтветДанныхЕГР.КраткиеДанные.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
			Объект.ИПРегистрационныйНомерПФР = ДанныеЕГР.РегистрацияВПенсионномФонде.РегистрационныйНомерПФР;	
		Иначе
			Объект.РегистрационныйНомерПФР   = ДанныеЕГР.РегистрацияВПенсионномФонде.РегистрационныйНомерПФР;
		КонецЕсли;
		
		Объект.КодОрганаПФР                          = ДанныеЕГР.РегистрацияВПенсионномФонде.КодОрганаПФР;
		Объект.НаименованиеТерриториальногоОрганаПФР = ДанныеЕГР.РегистрацияВПенсионномФонде.НаименованиеОрганаПФР;
		
		// сразу закешируем в информационной базе и платежные реквизиты
		ОрганизацииФормыДляОтчетностиВызовСервера.РеквизитыГосударственногоОрганаПоКоду(
			Перечисления.ВидыГосударственныхОрганов.ОрганПФР,
			Объект.КодОрганаПФР);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЕГР.РегистрацияВФСС) Тогда
		
		Объект.РегистрационныйНомерФСС = ДанныеЕГР.РегистрацияВФСС.РегистрационныйНомерФСС;
		
		Если ЗначениеЗаполнено(ДанныеЕГР.РегистрацияВФСС.КодПодчиненности) Тогда
			Объект.КодПодчиненностиФСС                   = ДанныеЕГР.РегистрацияВФСС.КодПодчиненности;
			Объект.НаименованиеТерриториальногоОрганаФСС = ДанныеЕГР.РегистрацияВФСС.НаименованиеОрганаФСС;
		Иначе
			// Нередко сведения отсутствуют в ЕГР
			Объект.КодПодчиненностиФСС = ОрганизацииФормыДляОтчетностиКлиентСервер.КодПодчиненностиФСС(
				Объект.РегистрационныйНомерФСС,
				Объект.ЮридическоеФизическоеЛицо,
				Объект.ОбособленноеПодразделение);
			Объект.НаименованиеТерриториальногоОрганаФСС = ОрганизацииФормыДляОтчетностиВызовСервера.РеквизитыГосударственногоОрганаПоКоду(
				Перечисления.ВидыГосударственныхОрганов.ОрганФСС,
				Объект.КодПодчиненностиФСС);
		КонецЕсли;
			
		// сразу закешируем в информационной базе и платежные реквизиты
		ОрганизацииФормыДляОтчетностиВызовСервера.РеквизитыГосударственногоОрганаПоКоду(
			Перечисления.ВидыГосударственныхОрганов.ОрганФСС,
			Объект.КодПодчиненностиФСС);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКодыПравовойФормы(Объект, ПравоваяФорма)
	
	Если Не ЗначениеЗаполнено(ПравоваяФорма) Тогда
		
		Объект.КодОКОПФ          = "";
		Объект.НаименованиеОКОПФ = "";
		Объект.КодОКФС           = "";
		Объект.НаименованиеОКФС  = "";
		
		// Перед записью попробуем заполнить по наименованию - см., например, СформироватьОКОПФ()
		
	Иначе
		
		// Определяем по соответствию имен правовой формы и кодов по классификаторам,
		// поставляемом в составе конфигурации
		ТаблицаПодстановки = Справочники.Организации.ТаблицаВыбораРеквизитовДляПодстановки();
		СтрокаПодстановки = ТаблицаПодстановки.Найти(ВРЕГ(ПравоваяФорма), "ПравоваяФорма");
		
		Если СтрокаПодстановки <> Неопределено Тогда
			Объект.КодОКОПФ          = СтрокаПодстановки.КодОКОПФ;
			Объект.НаименованиеОКОПФ = ОрганизацииФормыДляОтчетностиВызовСервера.НаименованиеПоКлассификатору("ОКОПФ", Объект.КодОКОПФ);
			Объект.КодОКФС           = СтрокаПодстановки.КодОКФС;
			Объект.НаименованиеОКФС  = ОрганизацииФормыДляОтчетностиВызовСервера.НаименованиеПоКлассификатору("ОКФС", Объект.КодОКФС);
		Иначе
			// Ищем по наименованию в классификаторе ОКОПФ
			ПараметрыОКОПФ = Справочники.Организации.ПолучитьПараметрыФормыВыбораДляКода("ОКОПФ", ТекущаяДатаСеанса());
			ЗначениеОКОПФ  = ПараметрыОКОПФ.СписокКодов.Найти(ПравоваяФорма, "Наименование");
			Если ЗначениеОКОПФ <> Неопределено Тогда
				Объект.КодОКОПФ          = ЗначениеОКОПФ.Код;
				Объект.НаименованиеОКОПФ = ЗначениеОКОПФ.Наименование;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьЭлементКонтактнойИнформации(Форма, ИмяРеквизита, КонтактнаяИнформация)
	
	// КонтактнаяИнформация - см. ДанныеЕдиныхГосРеестров.НоваяКонтактнаяИнформация()
	
	Если Не ЗначениеЗаполнено(КонтактнаяИнформация) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор  = Новый Структура("ИмяРеквизита", ИмяРеквизита);
	Строки = Форма.КонтактнаяИнформацияОписаниеДополнительныхРеквизитов.НайтиСтроки(Отбор);
	ДанныеСтроки = ?(Строки.Количество() = 0, Неопределено, Строки[0]);
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки.Представление = КонтактнаяИнформация.Представление Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки.Представление = КонтактнаяИнформация.Представление;
	ДанныеСтроки.ЗначенияПолей = КонтактнаяИнформация.КонтактнаяИнформация;
	
	Форма[ИмяРеквизита] = ДанныеСтроки.Представление;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаЗаполненияВФорме

// Проверяет заполнение реквизитов организации.
// Выполняет проверки, дополнительные к реализованным в модуле объекта.
// Перечень таких проверок может быть передан в форму при ее открытии.
// Если есть незаполненные реквизиты, то выдает сообщения пользователю.
//
// Параметры:
//  Форма						 - УправляемаяФорма - форма элемента справочника Организации.
//                                 Для формы предполагается
//                                 1. данные объекта в ней размещены в реквизите с именем Объект
//                                 2. состав, размещение и взаимосвязи полей соответствуют описаниям,
//                                    приведенным в области ОписаниеФормы
//  ПроверяемыеРеквизиты		 - Массив - имена реквизитов, для которых должна быть вызвана платформенная проверка
//  ХранилищеПроверяемыеДанные	 - Соответствие - имена реквизитов, заполнение которых должно быть проверено этой процедурой
//                                 * Ключ - путь к данным
//                                 * Значение - любое, кроме Неопределено
//                                 см. также ПроверяемыеДанные()
//                               - Строка - адрес временного хранилища, где размещено это Соответствие
//  Отказ						 - Булево - Истина, если что-то не заполнено
//
Процедура ПроверитьЗаполнение(Форма, ПроверяемыеРеквизиты, ХранилищеПроверяемыеДанные, Отказ) Экспорт
	
	Если ТипЗнч(ХранилищеПроверяемыеДанные) <> Тип("Строка") Тогда
		ПроверяемыеДанные = ХранилищеПроверяемыеДанные;
	ИначеЕсли ЭтоАдресВременногоХранилища(ХранилищеПроверяемыеДанные) Тогда
		ПроверяемыеДанные = ПолучитьИзВременногоХранилища(ХранилищеПроверяемыеДанные);
	КонецЕсли;
	
	Если ТипЗнч(ПроверяемыеДанные) <> Тип("Соответствие") Или Не ЗначениеЗаполнено(ПроверяемыеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	ЗависимыеДанные = ЗависимыеДанные();
	
	// О незаполненных реквизитах объектов сообщим. Сообщения будем выдавать в порядке следования полей на форме
	РазмещениеРеквизитов          = РазмещениеДанныхФормыДляОтчетности();
	ОписанияНезаполненныхЗначений = ОписанияНезаполненныхЗначений();
	
	Для Каждого ОписаниеРеквизита Из ОписаниеРеквизитовФормы(Форма.Объект.ЮридическоеФизическоеЛицо) Цикл
		
		ПутьКДанным = ПутьКДанным(ОписаниеРеквизита.Имя, РазмещениеРеквизитов);
		
		Если ПроверяемыеДанные[ПутьКДанным] = Неопределено
			И Не ТребуетсяПроверитьЗависимыеДанные(ПутьКДанным, ЗависимыеДанные, ПроверяемыеДанные, Форма) Тогда
			Продолжить;
		КонецЕсли;
		
		// отметим, что проверили
		Индекс = ПроверяемыеРеквизиты.Найти(ПутьКДанным);
		Если Индекс <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(Индекс); // Сейчас проверим сами
		КонецЕсли;
		
		Значение = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКДанным);
		
		// Реквизиты, представленные на форме гиперссылками, следует обработать отдельно:
		// их незаполненное значение обозначено нестандартно - особым текстом
		ТекстНезаполненного = ОписанияНезаполненныхЗначений[ПутьКДанным];
		Если ЗначениеЗаполнено(Значение) И (ТекстНезаполненного = Неопределено Или Значение <> ТекстНезаполненного) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Поле",
			"Заполнение",
			ОписаниеРеквизита.Заголовок);
			
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			Неопределено,// КлючДанных
			,//Поле
			ПутьКДанным,// ПутьКДанным
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Помещает во временное хранилище перечень данных, заполнение которых контролируется в конкретном вызове формы.
// Перечень состоит из путей к данным реквизитов.
// Значение помещается в хранилище на время жизни формы.
//
// Параметры:
//  Организация                  - Справочник.Организации - организация, реквизиты которой редактируются
//  ПараметрПроверяемыеРеквизиты - Массив, Неопределено - перечень имен проверяемых реквизитов;
//                                 имена - в соответствии с ВсеРеквизитыДляОтчетности()
//  ПараметрКонтекст             - Структура, Неопределено - сведения, описывающие, для какой цели была вызвана форма.
//                                 Если эти сведения переданы, то перечень имен проверяемых реквизитов дополняется определенными исходя из контекста.
//                                 Ключи структуры:
//                                  * Период           - Дата   - период, к которому относится выполняемое действие, например, за который представляется декларация
//                                  * ИмяРеглОтчета    - Строка - (опционально) имя отчета, для работы с которым нужны реквизиты организации
//                                  * ПолноеИмяПравила - Строка - (опционально) имя правила, для выполнения которого нужны реквизиты организации
//                                  * Правило          - СправочникСсылка.ПравилаПредставленияОтчетовУплатыНалогов - 
//                                                                (опционально) правило, для выполнения которого нужны реквизиты организации
//                                 Структура должна содержать ровно два свойства: Период и одно из опциональных.
//  АдресПроверяемыеДанные		 - Строка - возвращаемый параметр: адрес в хранилище, по которому размещен перечень проверяемых данных
//  ИдентификаторФормы			 - УникальныйИдентификатор - идентификатор формы, в которой будет проверяться заполнение реквизитов
//
Процедура УстановитьПроверяемыеДанные(Организация, ПараметрПроверяемыеРеквизиты, ПараметрКонтекст, АдресПроверяемыеДанные, ИдентификаторФормы) Экспорт
	
	Если ЭтоАдресВременногоХранилища(АдресПроверяемыеДанные) Тогда
		// Проверяемые данные уже размещены в хранилище.
		// Предполагается, что проверяемые данные передаются в форму и не могут быть изменены в ходе работы формы,
		// поэтому если хранилище уже заполнено, то обновлять в нем данные незачем.
		Возврат;
	КонецЕсли;
	
	// Настроим проверку заполнения в соответствии с параметром формы
	ПроверяемыеРеквизиты = Новый Массив;
	Если ТипЗнч(ПараметрПроверяемыеРеквизиты) = Тип("Массив") Тогда
		// перечень реквизитов в готовом виде
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеРеквизиты, ПараметрПроверяемыеРеквизиты);
	КонецЕсли;
	
	Если ТипЗнч(ПараметрКонтекст) = Тип("Структура") Тогда
		// информация, которая позволяет определить перечень реквизитов
		
		Период = ПараметрКонтекст.Период;
		
		Если ПараметрКонтекст.Свойство("ИмяРеглОтчета") Тогда
			ИмяРеглОтчета = ПараметрКонтекст.ИмяРеглОтчета;
		Иначе
			Если ПараметрКонтекст.Свойство("ПолноеИмяПравила") Тогда
				ПолноеИмяПравила = ПараметрКонтекст.ПолноеИмяПравила;
			Иначе
				ПолноеИмяПравила = Справочники.ПравилаПредставленияОтчетовУплатыНалогов.ПолноеИмяПравила(ПараметрКонтекст.Правило);
			КонецЕсли;
			ИмяРеглОтчета = ИнтерфейсыВзаимодействияБРО.ИмяРеглОтчета(ПолноеИмяПравила);
		КонецЕсли;
			
		ПроверяемыеРеквизитыПоКонтексту = РегламентированнаяОтчетностьБП.РеквизитыОбязательныеДляОтчета(ИмяРеглОтчета, Организация, Период);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПроверяемыеРеквизиты, ПроверяемыеРеквизитыПоКонтексту);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПроверяемыеРеквизиты) Тогда
		Возврат;
	КонецЕсли;
		
	ПроверяемыеДанные = ПроверяемыеДанные(ПроверяемыеРеквизиты);
	АдресПроверяемыеДанные = ПоместитьВоВременноеХранилище(ПроверяемыеДанные, ИдентификаторФормы);
	
КонецПроцедуры

// Настраивает элементы формы и условное оформление формы для отображения пометки незаполненного у тех реквизитов,
// проверка которых должна быть выполнена дополнительно к проверкам, описанным для объекта метаданных.
//
// Параметры:
//  Форма					 - УправляемаяФорма - 
//  АдресПроверяемыеДанные	 - Строка - адрес временного хранилища, где размещен перечень данных, подлежащих контролю.
//                             См. УстановитьПроверяемыеДанные()
//
Процедура НастроитьПроверяемыеРеквизиты(Форма, АдресПроверяемыеДанные) Экспорт
	
	Если НЕ ЭтоАдресВременногоХранилища(АдресПроверяемыеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверяемыеДанные = ПолучитьИзВременногоХранилища(АдресПроверяемыеДанные);
	Если ТипЗнч(ПроверяемыеДанные) <> Тип("Соответствие") 
		Или Не ЗначениеЗаполнено(ПроверяемыеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы                      = Форма.Элементы;
	ЗависимыеДанные               = ЗависимыеДанные();
	ПоляЗависимыхДанных           = Новый Соответствие; // Ключ - путь к данным; Значение - имя поля
	ОписанияНезаполненныхЗначений = ОписанияНезаполненныхЗначений();
	
	// Если ИНН заполнен, форма сокращается до обязательных незаполненных реквизитов
	СокращеннаяФорма = ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, "Объект.ИНН"));
	
	// В сокращенной форме кнопка "Записать и закрыть" внизу не отображается
	Элементы.ЗаписатьИЗакрытьНиз.Видимость = НЕ СокращеннаяФорма;
	
	Для Каждого Элемент Из Форма.Элементы Цикл
		
		Если ТипЗнч(Элемент) <> Тип("ПолеФормы") Тогда
			Продолжить;
		КонецЕсли;
		
		ПутиКЗависимымДанным = ПутиКЗависимымДанным(Элемент.ПутьКДанным, ЗависимыеДанные);
		Для каждого ПутьКДанным Из ПутиКЗависимымДанным Цикл
			ПоляЗависимыхДанных.Вставить(Элемент.ПутьКДанным, Элемент.Имя);
		КонецЦикла;
		
		// Включим отметку незаполненного у полей, отображающих проверяемые данные и зависимые от них данные
		Если ПроверяемыеДанные[Элемент.ПутьКДанным] = Неопределено
			И Не ТребуетсяПроверитьЗависимыеДанные(Элемент.ПутьКДанным, ЗависимыеДанные, ПроверяемыеДанные) Тогда
			
			Если СокращеннаяФорма Тогда
				Элемент.Видимость = Ложь; // Ненужные поля не отображаются
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		Если Элемент.Вид = ВидПоляФормы.ПолеВвода
			Или Элемент.Вид = ВидПоляФормы.ПолеПереключателя
			Или Элемент.Вид = ВидПоляФормы.ПолеТекстовогоДокумента
			Или Элемент.Вид = ВидПоляФормы.ПолеНадписи Тогда
			
			// Нужные, но заполненные поля не отображаются, если зависимые от них реквизиты тоже заполнены
			Если СокращеннаяФорма Тогда
				ВидимостьЭлемента = НЕ РеквизитЗаполнен(Форма, Элемент.ПутьКДанным);
				Для каждого ПутьКДанным Из ПутиКЗависимымДанным Цикл
					Если НЕ РеквизитЗаполнен(Форма, ПутьКДанным) Тогда
						ВидимостьЭлемента = Истина;
					КонецЕсли;
				КонецЦикла;
				Элемент.Видимость = ВидимостьЭлемента;
			КонецЕсли;
			
			Если Элемент.Видимость Тогда
				
				// Для гиперссылок используем условное оформление
				Если Элемент.Вид = ВидПоляФормы.ПолеНадписи Тогда
					
					ТекстНезаполненного = ОписанияНезаполненныхЗначений[Элемент.ПутьКДанным];
					
					Если ТекстНезаполненного <> Неопределено Тогда
						
						ЭлементУО = Форма.УсловноеОформление.Элементы.Добавить();
						
						КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, Элемент.Имя);
							
						ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
							ЭлементУО.Отбор,
							Элемент.ПутьКДанным,
							ВидСравненияКомпоновкиДанных.Равно,
							ТекстНезаполненного);
							
						ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
						
					КонецЕсли;
					
				Иначе
					
					Элемент.АвтоОтметкаНезаполненного = Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Управление видимостью связанных реквизитов
	Элементы.ЗаполнитьРеквизитыПоИНН.Видимость         = Элементы.ИНН.Видимость;
	Элементы.ПояснениеНекорректныйИНН.Видимость        = Элементы.ИНН.Видимость;
	Элементы.ПояснениеНекорректныйКодПоОКТМО.Видимость = Элементы.КодПоОКТМО.Видимость;
	Элементы.ПодсказкаОКВЭД2.Видимость                 = Элементы.КодОКВЭД2.Видимость;
	
	// Установим условное оформление, которое будет требовать заполнения полей с зависимыми данными,
	// если заполнены поля с ключевыми для них данными
	Для Каждого ОписаниеЗависимыхДанных Из ЗависимыеДанные Цикл
		
		Зависимое = ОписаниеЗависимыхДанных.Ключ;
		МассивКлючевых = СтрРазделить(ОписаниеЗависимыхДанных.Значение, ",");
		
		ИмяОформляемогоПоля = ПоляЗависимыхДанных[Зависимое];
		Если ИмяОформляемогоПоля = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементУО = Форма.УсловноеОформление.Элементы.Добавить();
		
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, ИмяОформляемогоПоля);
		
		Для каждого Ключевое Из МассивКлючевых Цикл
		
			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
				ЭлементУО.Отбор,
				Ключевое,
				ВидСравненияКомпоновкиДанных.Заполнено);
				
		КонецЦикла;
			
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			Зависимое,
			ВидСравненияКомпоновкиДанных.НеЗаполнено);
			
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	КонецЦикла;
	
КонецПроцедуры

// Конструирует коллекцию, содержащую имена и заголовки реквизитов формы.
// См. также ЗаголовкиРеквизитовФормыИсключения()
//
// Параметры:
//  ЮридическоеФизическоеЛицо	 - ПеречислениеСсылка.ЮридическоеФизическоеЛицо - тип организации, данные которой представлены в форме
// 
// Возвращаемое значение:
//  ТаблицаЗначений - описание реквизитов. 
//    Колонки:
//    * Имя - Строка - имя реквизита в соответствии со "словарем" ВсеРеквизитыДляОтчетности()
//    * Заголовок - Строка - заголовок реквизита
//
Функция ОписаниеРеквизитовФормы(ЮридическоеФизическоеЛицо) Экспорт
	
	ОписаниеРеквизитов = Новый ТаблицаЗначений;
	ОписаниеРеквизитов.Колонки.Добавить("Имя",       Новый ОписаниеТипов("Строка"));
	ОписаниеРеквизитов.Колонки.Добавить("Заголовок", Новый ОписаниеТипов("Строка"));
	
	ВсеРеквизиты        = ВсеРеквизитыДляОтчетности(ЮридическоеФизическоеЛицо);
	ЗаголовкиИсключения = ЗаголовкиРеквизитовФормыИсключения(ЮридическоеФизическоеЛицо);
	
	МетаданныеОрганизации = Метаданные.Справочники.Организации;
	
	Для каждого ИмяРеквизита Из ВсеРеквизиты Цикл
		
		// Сначала ищем среди явно заданных заголовков
		ЗаголовокРеквизита = ЗаголовкиИсключения[ИмяРеквизита];
		
		Если ЗаголовокРеквизита = Неопределено Тогда
			// Не нашли в особенностях, ищем в метаданных справочника
			МетаданныеРеквизита = МетаданныеОрганизации.Реквизиты.Найти(ИмяРеквизита);
			Если МетаданныеРеквизита = Неопределено Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ЗаголовокРеквизита = МетаданныеРеквизита.Синоним;
			
		КонецЕсли;
		
		Запись = ОписаниеРеквизитов.Добавить();
		Запись.Имя       = ИмяРеквизита;
		Запись.Заголовок = ЗаголовокРеквизита;
		
	КонецЦикла;
	
	Возврат ОписаниеРеквизитов;
	
КонецФункции

#Область ОписаниеФормы

// Описывает "словарь" реквизитов, представленных в формах для отчетности.
// Это могут быть как реквизиты организации (основного объекта формы),
// так и реквизиты формы.
// Отличить одни от других можно с помощью функции РазмещениеДанныхФормыДляОтчетности.
//
Функция ВсеРеквизитыДляОтчетности(ЮридическоеФизическоеЛицо)
	
	ЭтоЮрлицо = ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	
	Реквизиты = Новый Массив;
	
	Если ЭтоЮрлицо Тогда
		Реквизиты.Добавить("НаименованиеСокращенное");   // Только для юрлиц
		Реквизиты.Добавить("НаименованиеПолное");        // Только для юрлиц
	Иначе
		Реквизиты.Добавить("ФамилияИП");                 // Только для ИП
		Реквизиты.Добавить("ИмяИП");                     // Только для ИП
		Реквизиты.Добавить("ОтчествоИП");                // Только для ИП
	КонецЕсли;
	
	Реквизиты.Добавить("ИНН");
	
	Если ЭтоЮрлицо Тогда
		Реквизиты.Добавить("КПП");                       // Только для юрлиц
	КонецЕсли;
	
	Реквизиты.Добавить("ДатаРегистрации");
	
	Если НЕ ЭтоЮрлицо Тогда
		Реквизиты.Добавить("ИПРегистрационныйНомерПФР"); // Только для ИП
	КонецЕсли;
	
	Реквизиты.Добавить("РегистрационныйНомерПФР");
	Реквизиты.Добавить("РегистрационныйНомерФСС");
	Реквизиты.Добавить("КодПоОКПО");
	Реквизиты.Добавить("Телефон");
	Реквизиты.Добавить("Адрес");
	Реквизиты.Добавить("КодПоОКТМО");
	Реквизиты.Добавить("КодНалоговогоОргана");
	Реквизиты.Добавить("НаименованиеНалоговогоОргана");
	Реквизиты.Добавить("КодОрганаПФР");
	Реквизиты.Добавить("НаименованиеТерриториальногоОрганаПФР");
	Реквизиты.Добавить("КодПодчиненностиФСС");
	Реквизиты.Добавить("НаименованиеТерриториальногоОрганаФСС");
	Реквизиты.Добавить("КодОКВЭД2");
	Реквизиты.Добавить("НаименованиеОКВЭД2");
	
	Если ЭтоЮрлицо Тогда
		Реквизиты.Добавить("РуководительФамилия");       // Только для юрлиц
		Реквизиты.Добавить("РуководительИмя");           // Только для юрлиц
		Реквизиты.Добавить("РуководительОтчество");      // Только для юрлиц
		Реквизиты.Добавить("РуководительДолжность");     // Только для юрлиц
	КонецЕсли;
	
	Возврат Реквизиты;
	
КонецФункции

// Описывает размещение проверяемых реквизитов в форме
// 
// Возвращаемое значение:
//  Структура - перечень реквизитов, размещенных на форме отдельно от объекта организации
//   * Ключ - имя реквизита из словаря, приведенного в ВсеРеквизитыДляОтчетности()
//   * Значение - путь в форме к данным этого реквизита
//
Функция РазмещениеДанныхФормыДляОтчетности()
	
	Пути = Новый Структура;
	
	Пути.Вставить("КодПоОКТМО",            "КодПоОКТМО");
	
	Пути.Вставить("Адрес",                 "КонтактнаяИнформацияПолеЮрАдресОрганизации");
	Пути.Вставить("Телефон",               "КонтактнаяИнформацияПолеТелефонОрганизации");
	
	Пути.Вставить("РуководительФамилия",   "РуководительФамилия");
	Пути.Вставить("РуководительИмя",       "РуководительИмя");
	Пути.Вставить("РуководительОтчество",  "РуководительОтчество");
	Пути.Вставить("РуководительДолжность", "РуководительДолжность");
	
	// Остальные реквизиты предполагаются в Объект (Объект.Наименование и т.п.)
	
	Возврат Пути;
	
КонецФункции

Функция ЗависимыеДанные()
	
	ЗависимыеДанные = Новый Соответствие;
	
	// 1. Наименования обязательны, если указаны коды, и наоборот
	ЗависимыеДанные.Вставить("Объект.КодОрганаПФР",                          "Объект.НаименованиеТерриториальногоОрганаПФР");
	ЗависимыеДанные.Вставить("Объект.НаименованиеТерриториальногоОрганаПФР", "Объект.КодОрганаПФР");
	
	ЗависимыеДанные.Вставить("Объект.НаименованиеТерриториальногоОрганаФСС", "Объект.КодПодчиненностиФСС");
	ЗависимыеДанные.Вставить("Объект.КодПодчиненностиФСС",                   "Объект.НаименованиеТерриториальногоОрганаФСС");
	
	ЗависимыеДанные.Вставить("Объект.НаименованиеОКВЭД2",                    "Объект.КодОКВЭД2");
	ЗависимыеДанные.Вставить("Объект.КодОКВЭД2",                             "Объект.НаименованиеОКВЭД2");
	
	// 2. Законодательство РФ предполагает, что у человека обязательно есть имя и фамилия
	ЗависимыеДанные.Вставить("РуководительФамилия",                          "РуководительИмя");
	ЗависимыеДанные.Вставить("РуководительИмя",                              "РуководительФамилия");
	
	// 3. В программе код ОКТМО хранится в составе данных о налоговой инспекции и зависит от адреса
	ЗависимыеДанные.Вставить("Объект.КодНалоговогоОргана",                   "КонтактнаяИнформацияПолеЮрАдресОрганизации,КодПоОКТМО,Объект.НаименованиеНалоговогоОргана");
	ЗависимыеДанные.Вставить("Объект.НаименованиеНалоговогоОргана",          "КонтактнаяИнформацияПолеЮрАдресОрганизации,КодПоОКТМО,Объект.КодНалоговогоОргана");
	ЗависимыеДанные.Вставить("КодПоОКТМО",                                   "КонтактнаяИнформацияПолеЮрАдресОрганизации,Объект.КодНалоговогоОргана,Объект.НаименованиеНалоговогоОргана");
	ЗависимыеДанные.Вставить("КонтактнаяИнформацияПолеЮрАдресОрганизации",   "КодПоОКТМО,Объект.КодНалоговогоОргана,Объект.НаименованиеНалоговогоОргана");
	
	Возврат ЗависимыеДанные;
	
КонецФункции

Функция ПутиКЗависимымДанным(ПутьКДанным, ЗависимыеДанные)
	
	Результат = Новый Массив;
	
	ОписаниеЗависимыхДанных = ЗависимыеДанные[ПутьКДанным];
	Если ОписаниеЗависимыхДанных <> Неопределено Тогда
		ПутиКЗависимымДанным = СтрРазделить(ОписаниеЗависимыхДанных, ",");
		Для каждого ПутьКЗависимымДанным Из ПутиКЗависимымДанным Цикл
			Результат.Добавить(ПутьКЗависимымДанным);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РеквизитЗаполнен(Форма, ПутьКДанным)
	
	ОписанияНезаполненныхЗначений = ОписанияНезаполненныхЗначений();
	ТекстНезаполненного = ОписанияНезаполненныхЗначений[ПутьКДанным];
	
	ЗначениеРеквизита = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКДанным);
	
	Если ТекстНезаполненного <> Неопределено Тогда
		Возврат ЗначениеРеквизита <> ТекстНезаполненного;
	Иначе
		Возврат ЗначениеЗаполнено(ЗначениеРеквизита);
	КонецЕсли;
	
КонецФункции

Функция ЗаголовкиРеквизитовФормыИсключения(ЮридическоеФизическоеЛицо)
	
	// Обычно заголовки реквизитов заданы в метаданных справочника.
	// Исключения описаны в функции
	
	ЭтоЮрлицо = (ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	
	ОсобенныеРеквизиты = Новый Соответствие;
	
	ОсобенныеРеквизиты.Вставить("КодПоОКТМО", НСтр("ru = 'Код по ОКТМО'"));
	
	ОсобенныеРеквизиты.Вставить("Телефон", НСтр("ru = 'Телефон'"));
	Если ЭтоЮрлицо Тогда
		ОсобенныеРеквизиты.Вставить("Адрес", НСтр("ru = 'Юридический адрес'"));
	Иначе
		ОсобенныеРеквизиты.Вставить("Адрес", НСтр("ru = 'Адрес места жительства'"));
	КонецЕсли;
	
	ОсобенныеРеквизиты.Вставить("КодОКВЭД2",          НСтр("ru = 'Код основного вида деятельности'"));
	ОсобенныеРеквизиты.Вставить("НаименованиеОКВЭД2", НСтр("ru = 'Наименование основного вида деятельности'"));
	
	Если ЭтоЮрлицо Тогда
		ОсобенныеРеквизиты.Вставить("РуководительФамилия",   НСтр("ru = 'Фамилия руководителя'"));
		ОсобенныеРеквизиты.Вставить("РуководительИмя",       НСтр("ru = 'Имя руководителя'"));
		ОсобенныеРеквизиты.Вставить("РуководительОтчество",  НСтр("ru = 'Отчество руководителя'"));
		ОсобенныеРеквизиты.Вставить("РуководительДолжность", НСтр("ru = 'Должность руководителя'"));
	КонецЕсли;
	
	Возврат ОсобенныеРеквизиты;
	
КонецФункции

Функция ОписанияНезаполненныхЗначений()
	
	Описания = Новый Соответствие;
	
	Описания.Вставить(
		"КонтактнаяИнформацияПолеЮрАдресОрганизации",
		УправлениеКонтактнойИнформациейКлиентСервер.ТекстПустогоАдресаВВидеГиперссылки());
		
	Возврат Описания;
		
КонецФункции

#КонецОбласти

Функция ПутьКДанным(ИмяРеквизита, РазмещениеРеквизитов)
	
	Путь = "";
	Если Не РазмещениеРеквизитов.Свойство(ИмяРеквизита, Путь) Тогда
		Путь = "Объект." + ИмяРеквизита;
	КонецЕсли;
	
	Возврат Путь;
	
КонецФункции

Функция ПроверяемыеДанные(ПроверяемыеРеквизиты)
	
	ПроверяемыеДанные = Новый Соответствие; // Ключ - путь к данным, Значение - Истина
	
	РазмещениеРеквизитов = РазмещениеДанныхФормыДляОтчетности();
	
	Для Каждого ИмяРеквизита Из ПроверяемыеРеквизиты Цикл
		
		ПутьКДанным = ПутьКДанным(ИмяРеквизита, РазмещениеРеквизитов);
		
		ПроверяемыеДанные.Вставить(ПутьКДанным, Истина);
		
	КонецЦикла;
	
	Возврат ПроверяемыеДанные;
	
КонецФункции

Функция ТребуетсяПроверитьЗависимыеДанные(ПутьКДанным, ЗависимыеДанные, ПроверяемыеДанные, Форма = Неопределено)
	
	ОписаниеПутейКДаннымКлючевыхРеквизитов = ЗависимыеДанные[ПутьКДанным];
	
	Если ОписаниеПутейКДаннымКлючевыхРеквизитов = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПутиКДаннымКлючевыхРеквизитов = СтрРазделить(ОписаниеПутейКДаннымКлючевыхРеквизитов, ",");
	Для каждого ПутьКДаннымКлючевогоРеквизита Из ПутиКДаннымКлючевыхРеквизитов Цикл
		
		// Зависимые данные нужно проверять в двух случаях:
		// 1. Требуется проверить ключевое значение (а значит - и зависимое от него)
		// 2. Заполнено ключевое значение (а значит, должно быть заполнено и зависимое от него)
		
		// Первый случай проверяется в этой функции всегда, а второй - если передан параметр Форма.
		// Это различие в поведении связано с тем, что при работе с формой второй случай обрабатывается с помощью условного оформления.
		
		// первый случай:
		Если ПроверяемыеДанные[ПутьКДаннымКлючевогоРеквизита] <> Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
		
		Если Форма = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// второй случай:
		ЗначениеКлючевогоРеквизита = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Форма, ПутьКДаннымКлючевогоРеквизита);
		
		Если ЗначениеЗаполнено(ЗначениеКлючевогоРеквизита) Тогда
			Возврат Истина;
		Иначе
			Продолжить;
		КонецЕсли;
	
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область ПроверкаЗаполненияВДанных

Функция РеквизитыЗаполнены(Организация, ПроверяемыеРеквизиты, НезаполненныеРеквизиты) Экспорт
	
	// Реквизиты организации указываются в отчетности на дату подписи.
	// Дата подписи устанавливается в момент создания отчета по текущей дате,
	// поэтому срезы периодических данных получаем по состоянию на текущую дату.
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ТребуемыеВидыКИ = Новый Массив;
	Для каждого КлючИЗначение Из ВсеВидыКонтактнойИнформацииДляПроверки() Цикл
		Если ПроверяемыеРеквизиты.Найти(КлючИЗначение.Ключ) <> Неопределено Тогда
			ТребуемыеВидыКИ.Добавить(КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("ВидыКИ", ТребуемыеВидыКИ);
	
	Запрос.Текст = ТекстЗапросаПроверкаРеквизитов(ПроверяемыеРеквизиты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Результат = Истина;
	
	НезаполненныеРеквизиты = Новый Массив;
	
	Для Каждого ИмяРеквизита Из ПроверяемыеРеквизиты Цикл
		Если НЕ ЗначениеЗаполнено(Выборка[ИмяРеквизита]) Тогда
			Результат = Ложь;
			НезаполненныеРеквизиты.Добавить(ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ВсеВидыКонтактнойИнформацииДляПроверки()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("Телефон", Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	Результат.Вставить("Адрес",   Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаПроверкаРеквизитов(ПроверяемыеРеквизиты)
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапросаШаблонВыборкаРеквизитов());
	
	// Удалим невостребованные поля
	
	КоличествоЗапросов = СхемаЗапроса.ПакетЗапросов.Количество();
	
	ОператорЗапроса    = СхемаЗапроса.ПакетЗапросов[КоличествоЗапросов - 1].Операторы[0];
	КолонкиЗапроса     = СхемаЗапроса.ПакетЗапросов[КоличествоЗапросов - 1].Колонки;
	
	ИндексыУдаляемых = Новый Массив;
	Для каждого КолонкаЗапроса Из КолонкиЗапроса Цикл
		Если ПроверяемыеРеквизиты.Найти(КолонкаЗапроса.Псевдоним) = Неопределено Тогда
			ИндексыУдаляемых.Добавить(КолонкиЗапроса.Индекс(КолонкаЗапроса));
		КонецЕсли;
	КонецЦикла;
	
	ВГраница = ИндексыУдаляемых.ВГраница();
	Для Индекс = 0 По ВГраница Цикл
		КолонкиЗапроса.Удалить(ИндексыУдаляемых[ВГраница - Индекс]);
	КонецЦикла;
	
	// Определим используемые источники данных
	
	ВсеИсточники = Новый Массив;
	Для каждого ОписаниеИсточника Из ОператорЗапроса.Источники Цикл
		ВсеИсточники.Добавить(ОписаниеИсточника.Источник.Псевдоним);
	КонецЦикла;
	
	ПоляЗапроса = ОператорЗапроса.ВыбираемыеПоля;
	
	ИспользуемыеИсточники = Новый Массив;
	Для Каждого Поле Из ПоляЗапроса Цикл
		ТекстПоля = Строка(Поле);
		Для Каждого ПсевдонимИсточника Из ВсеИсточники Цикл
			Если ИспользуемыеИсточники.Найти(ПсевдонимИсточника) = Неопределено
					И СтрНайти(ТекстПоля, ПсевдонимИсточника) > 0 Тогда
				ИспользуемыеИсточники.Добавить(ПсевдонимИсточника);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Удалим не используемые в выборке источники
	
	НеиспользуемыеИсточники = ОбщегоНазначенияКлиентСервер.СократитьМассив(ВсеИсточники, ИспользуемыеИсточники);
	
	КорневыеИсточники = ОператорЗапроса.Источники.ПолучитьКорневыеИсточники();
	
	Для Каждого ПсевдонимИсточника Из НеиспользуемыеИсточники Цикл
		// Основной источник удалять нельзя
		Если КорневыеИсточники.НайтиПоПсевдониму(ПсевдонимИсточника) = Неопределено Тогда
			ОператорЗапроса.Источники.Удалить(ПсевдонимИсточника);
		КонецЕсли;
	КонецЦикла;
	
	// Соберем окончательный текст
	
	ТекстЗапроса = ТекстЗапросаКонтактнаяИнформация(ИспользуемыеИсточники)
		+ ТекстЗапросаОтветственныеЛица(ИспользуемыеИсточники)
		+ СхемаЗапроса.ПолучитьТекстЗапроса();
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаШаблонВыборкаРеквизитов()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Организации.НаименованиеСокращенное,
	|	Организации.НаименованиеПолное,
	|	Организации.ФамилияИП,
	|	Организации.ИмяИП,
	|	Организации.ОтчествоИП,
	|	Организации.ИНН,
	|	Организации.КПП,
	|	Организации.ДатаРегистрации,
	|	Организации.ИПРегистрационныйНомерПФР,
	|	Организации.РегистрационныйНомерПФР,
	|	Организации.РегистрационныйНомерФСС,
	|	Организации.КодПоОКПО,
	|	ЕСТЬNULL(ДанныеКонтактнойИнформации.Телефон, НЕОПРЕДЕЛЕНО) КАК Телефон,
	|	ЕСТЬNULL(ДанныеКонтактнойИнформации.Адрес, НЕОПРЕДЕЛЕНО) КАК Адрес,
	|	ЕСТЬNULL(Организации.РегистрацияВНалоговомОргане.КодПоОКТМО, НЕОПРЕДЕЛЕНО) КАК КодПоОКТМО,
	|	Организации.КодНалоговогоОргана,
	|	Организации.НаименованиеНалоговогоОргана КАК НаименованиеНалоговогоОргана,
	|	Организации.КодОрганаПФР,
	|	Организации.НаименованиеТерриториальногоОрганаПФР,
	|	Организации.КодПодчиненностиФСС,
	|	Организации.НаименованиеТерриториальногоОрганаФСС,
	|	Организации.КодОКВЭД2,
	|	Организации.НаименованиеОКВЭД2,
	|	ЕСТЬNULL(ДанныеОтветственныхЛиц.РуководительФамилия, НЕОПРЕДЕЛЕНО) КАК РуководительФамилия,
	|	ЕСТЬNULL(ДанныеОтветственныхЛиц.РуководительИмя, НЕОПРЕДЕЛЕНО) КАК РуководительИмя,
	|	ЕСТЬNULL(ДанныеОтветственныхЛиц.РуководительОтчество, НЕОПРЕДЕЛЕНО) КАК РуководительОтчество,
	|	ЕСТЬNULL(ДанныеОтветственныхЛиц.РуководительДолжность, НЕОПРЕДЕЛЕНО) КАК РуководительДолжность
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеКонтактнойИнформации КАК ДанныеКонтактнойИнформации
	|		ПО Организации.Ссылка = ДанныеКонтактнойИнформации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеОтветственныхЛиц КАК ДанныеОтветственныхЛиц
	|		ПО Организации.Ссылка = ДанныеОтветственныхЛиц.Организация
	|ГДЕ
	|	Организации.Ссылка = &Организация"
	;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаКонтактнаяИнформация(ИспользуемыеИсточники)
	
	Если ИспользуемыеИсточники.Найти("ДанныеКонтактнойИнформации") = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КонтактнаяИнформация.Ссылка КАК Организация,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации)
	|				ТОГДА КонтактнаяИнформация.Представление
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК Телефон,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации)
	|				ТОГДА КонтактнаяИнформация.Представление
	|			ИНАЧЕ NULL
	|		КОНЕЦ) КАК Адрес
	|ПОМЕСТИТЬ ДанныеКонтактнойИнформации
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Тип <> ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.ПустаяСсылка)
	|	И КонтактнаяИнформация.Ссылка = &Организация
	|	И КонтактнаяИнформация.Вид В(&ВидыКИ)
	|
	|СГРУППИРОВАТЬ ПО
	|	КонтактнаяИнформация.Ссылка"
	;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаОтветственныеЛица(ИспользуемыеИсточники)
	
	Если ИспользуемыеИсточники.Найти("ДанныеОтветственныхЛиц") = Неопределено Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница КАК Организация,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность КАК РуководительДолжность,
	|	ЕСТЬNULL(ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Фамилия, НЕОПРЕДЕЛЕНО) КАК РуководительФамилия,
	|	ЕСТЬNULL(ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Имя, НЕОПРЕДЕЛЕНО) КАК РуководительИмя,
	|	ЕСТЬNULL(ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Отчество, НЕОПРЕДЕЛЕНО) КАК РуководительОтчество
	|ПОМЕСТИТЬ ДанныеОтветственныхЛиц
	|ИЗ
	|	РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(
	|			,
	|			ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)
	|				И СтруктурнаяЕдиница = &Организация) КАК ОтветственныеЛицаОрганизацийСрезПоследних"
	;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти
