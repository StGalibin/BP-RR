
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиентские методы формы записи регистра сведений СтавкиНалогаНаИмущество
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСобытийЭлементовШапкиФормы

Процедура ОсвобождениеОтНалогаНаИмуществоПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.СтавкиНалогаНаИмущество;
	
	Если Запись.ОсвобождениеОтНалогообложения Тогда
		Запись.СнижениеНалоговойСтавки = Ложь;
		Запись.СниженнаяНалоговаяСтавка = 0;
		Запись.УменьшениеСуммыНалогаВПроцентах = Ложь;
		Запись.ПроцентУменьшения = 0;
	Иначе
		Запись.КодНалоговойЛьготыОсвобождениеОтНалогообложения = "";
	КонецЕсли;
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура КодЛьготыОсвобождениеОтНалогаНаИмуществоНачалоВыбора(Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Запись = Форма.СтавкиНалогаНаИмущество;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипОбъекта", "РегистрСведений");
	ПараметрыФормы.Вставить("НазваниеОбъекта", "СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	ПараметрыФормы.Вставить("НазваниеМакета", "ЛьготыПоНалогуНаИмущество");
	ПараметрыФормы.Вставить("ТекущийПериод", Запись.Период);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("КодЛьготыОсвобождениеОтНалогаНаИмуществоНачалоВыбораЗавершение", ЭтотОбъект, Форма);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораКода", ПараметрыФормы, Форма, , , , ОповещениеОЗакрытии);
	
КонецПроцедуры

Процедура СнижениеСтавкиНалогаНаИмуществоПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.СтавкиНалогаНаИмущество;
	
	Если Не Запись.СнижениеНалоговойСтавки Тогда
		Запись.СниженнаяНалоговаяСтавка = 0;
	КонецЕсли;
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура СниженнаяСтавкаНалогаНаИмуществоПриИзменении(Форма, Элемент) Экспорт
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура УменьшениеСуммыНалогаНаИмуществоВПроцентахПриИзменении(Форма, Элемент) Экспорт
	
	Запись = Форма.СтавкиНалогаНаИмущество;
	
	Если Не Запись.УменьшениеСуммыНалогаВПроцентах Тогда
		Запись.ПроцентУменьшения = 0;
	КонецЕсли;
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

Процедура ПроцентУменьшенияНалогаНаИмуществоПриИзменении(Форма, Элемент) Экспорт
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

#КонецОбласти

Процедура КодЛьготыОсвобождениеОтНалогаНаИмуществоНачалоВыбораЗавершение(РезультатЗакрытия, Форма) Экспорт
	
	КодЛьготы = РезультатЗакрытия;
	Если КодЛьготы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись = Форма.СтавкиНалогаНаИмущество;
	Запись.КодНалоговойЛьготыОсвобождениеОтНалогообложения = КодЛьготы;
	
	СтавкиНалогаНаИмуществоФормыКлиентСервер.УправлениеФормой(Форма);
	
КонецПроцедуры

#КонецОбласти
