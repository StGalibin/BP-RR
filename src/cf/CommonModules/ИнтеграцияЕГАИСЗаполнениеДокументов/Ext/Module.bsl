
Процедура ЗаполнениеДокументовЕГАИС(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(Источник) = Тип("ДокументОбъект.АктСписанияЕГАИС") Тогда
		
		Если ЗначениеЗаполнено(ДанныеЗаполнения) И ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СписаниеТоваров") Тогда
			
			ЗаполнитьОрганизациюЕГАИСВШапке(Источник, ДанныеЗаполнения);
				
			Источник.ПричинаСписания = Перечисления.ПричиныСписанийЕГАИС.Недостача;
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
		
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.АктПостановкиНаБалансЕГАИС") Тогда
		
		Если ЗначениеЗаполнено(ДанныеЗаполнения) И ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОприходованиеТоваров") Тогда
			
			ЗаполнитьОрганизациюЕГАИСВШапке(Источник, ДанныеЗаполнения);
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);
		
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ДокументОбъект.ТТНИсходящаяЕГАИС") Тогда
		
		Если ЗначениеЗаполнено(ДанныеЗаполнения) И ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ВозвратТоваровПоставщику") Тогда
			
			РеквизитыШапки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, Контрагент, Дата, ПодразделениеОрганизации");
			Если ЗначениеЗаполнено(РеквизитыШапки.ПодразделениеОрганизации) 
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.ПодразделениеОрганизации, "ОбособленноеПодразделение") Тогда
				Источник.Грузоотправитель = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.ПодразделениеОрганизации, РеквизитыШапки.Дата);				
			Иначе
				Источник.Грузоотправитель = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.Организация, РеквизитыШапки.Дата);				
			КонецЕсли;
			
			Источник.Грузополучатель 	= ПолучитьОрганизациюЕГАИС(РеквизитыШапки.Контрагент, 	РеквизитыШапки.Дата);
			Источник.ВидОперации 		= Перечисления.ВидыОперацийТТНИсходящейЕГАИС.ВозвратПоставщику;
			
			ЗаполнитьТоварыЕГАИСВозврат(Источник, ДанныеЗаполнения);		
		
		ИначеЕсли ЗначениеЗаполнено(ДанныеЗаполнения) И ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
			
			РеквизитыШапки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, Дата, ПодразделениеОтправитель, ПодразделениеПолучатель");
			Если ЗначениеЗаполнено(РеквизитыШапки.ПодразделениеОтправитель) 
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.ПодразделениеОтправитель, "ОбособленноеПодразделение") Тогда
				Источник.Грузоотправитель = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.ПодразделениеОтправитель, РеквизитыШапки.Дата);				
			Иначе
				Источник.Грузоотправитель = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.Организация, РеквизитыШапки.Дата);				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(РеквизитыШапки.ПодразделениеПолучатель) 
				И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.ПодразделениеПолучатель, "ОбособленноеПодразделение") Тогда
				Источник.Грузополучатель = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.ПодразделениеПолучатель, РеквизитыШапки.Дата);				
			Иначе
				Источник.Грузополучатель = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.Организация, РеквизитыШапки.Дата);				
			КонецЕсли;
			
			Источник.ВидОперации 		= Перечисления.ВидыОперацийТТНИсходящейЕГАИС.РасходнаяНакладная;
			
			ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения);		
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьОрганизациюЕГАИСВШапке(Источник, ДанныеЗаполнения)
	
	РеквизитыШапки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения, "Организация, Дата, ПодразделениеОрганизации");
	Если ЗначениеЗаполнено(РеквизитыШапки.ПодразделениеОрганизации) 
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыШапки.ПодразделениеОрганизации, "ОбособленноеПодразделение") Тогда
		Источник.ОрганизацияЕГАИС = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.ПодразделениеОрганизации, РеквизитыШапки.Дата);
	Иначе
		Источник.ОрганизацияЕГАИС = ПолучитьОрганизациюЕГАИС(РеквизитыШапки.Организация, РеквизитыШапки.Дата);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОрганизациюЕГАИС(Ссылка, ДатаСведений)
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.Контрагенты") Тогда
		КПП = Справочники.Контрагенты.КППНаДату(Ссылка, ДатаСведений);
		ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ИНН");
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.Организации") Тогда
		КПП = Справочники.Организации.КППНаДату(Ссылка, ДатаСведений);
		ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ИНН");
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
		КПП = Справочники.ПодразделенияОрганизаций.КППНаДату(Ссылка, ДатаСведений);
		ИНН = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка.Владелец, "ИНН");
	Иначе
		Возврат Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИНН) Тогда
		Возврат Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка();
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Результат = Новый Соответствие;
	
	Запрос.УстановитьПараметр("ИНН", ИНН);
	Запрос.УстановитьПараметр("КПП", КПП);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КлассификаторОрганизацийЕГАИС.Ссылка КАК ОрганизацияЕГАИС
	|ИЗ
	|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
	|ГДЕ
	|	КлассификаторОрганизацийЕГАИС.ИНН = &ИНН
	|	И КлассификаторОрганизацийЕГАИС.КПП = &КПП";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОрганизацияЕГАИС;
	КонецЕсли;
	
	Возврат Справочники.КлассификаторОрганизацийЕГАИС.ПустаяСсылка();
	
КонецФункции

Процедура ЗаполнитьТоварыЕГАИС(Источник, ДанныеЗаполнения)
	
	ИмяДокумента 		= ДанныеЗаполнения.Метаданные().Имя;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.КоэффициентПересчетаУпаковки, 1) КАК КоэффициентПересчетаУпаковки,
	|	Товары.Количество,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ." + ИмяДокумента + ".Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО Товары.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки = """",
	|	КоэффициентПересчетаУпаковки
	|ИТОГИ ПО
	|	НомерСтроки";
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		НоваяСтрока = Источник.Товары.Добавить();
		ДетальнаяВыборка = ВыборкаНоменклатура.Выбрать();
		
		Если ДетальнаяВыборка.Следующий() Тогда
			
			Коэффициент = ДетальнаяВыборка.КоэффициентПересчетаУпаковки;
			НоваяСтрока.АлкогольнаяПродукция 	= ДетальнаяВыборка.АлкогольнаяПродукция;
			НоваяСтрока.Количество 				= ?(Коэффициент = 0, ДетальнаяВыборка.Количество,  ДетальнаяВыборка.Количество/Коэффициент);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыЕГАИСВозврат(Источник, ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНоменклатурыЕГАИС.АлкогольнаяПродукция,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки КАК ИдентификаторУпаковки,
	|	ЕСТЬNULL(СоответствиеНоменклатурыЕГАИС.КоэффициентПересчетаУпаковки, 1) КАК КоэффициентПересчетаУпаковки,
	|	Товары.Количество,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Цена,
	|	Товары.Сумма,
	|	Товары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.ВозвратТоваровПоставщику.Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствиеНоменклатурыЕГАИС КАК СоответствиеНоменклатурыЕГАИС
	|		ПО Товары.Номенклатура = СоответствиеНоменклатурыЕГАИС.Номенклатура
	|ГДЕ
	|	Товары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Товары.НомерСтроки,
	|	СоответствиеНоменклатурыЕГАИС.ИдентификаторУпаковки = """",
	|	КоэффициентПересчетаУпаковки
	|ИТОГИ ПО
	|	НомерСтроки";
	
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		НоваяСтрока = Источник.Товары.Добавить();
		ДетальнаяВыборка = ВыборкаНоменклатура.Выбрать();
		
		Если ДетальнаяВыборка.Следующий() Тогда
			
			Коэффициент = ДетальнаяВыборка.КоэффициентПересчетаУпаковки;
			НоваяСтрока.АлкогольнаяПродукция 	= ДетальнаяВыборка.АлкогольнаяПродукция;
			НоваяСтрока.Количество 				= ?(Коэффициент = 0, ДетальнаяВыборка.Количество,  ДетальнаяВыборка.Количество/Коэффициент);
			НоваяСтрока.Цена              		= ДетальнаяВыборка.Цена;
			НоваяСтрока.Сумма              		= ДетальнаяВыборка.Сумма;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
