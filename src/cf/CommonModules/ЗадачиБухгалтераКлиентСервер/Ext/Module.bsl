Функция Идентификатор(Имя) Экспорт
	
	Цифры = "1234567890";
	НомерСтрокой = "";
	ДлинаИмени = СтрДлина(Имя);
	Для Индекс = 0 По ДлинаИмени - 1 Цикл
		Позиция = ДлинаИмени - Индекс;
		Символ = Сред(Имя,Позиция,1);
		Если СтрНайти(Цифры, Символ) = 0 Тогда
			Прервать;
		КонецЕсли;
		НомерСтрокой = Символ + НомерСтрокой;
	КонецЦикла;
	
	Если ПустаяСтрока(НомерСтрокой) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		Возврат Число(НомерСтрокой) - 1;
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
КонецФункции

Функция НомерКонтейнера(Индекс) Экспорт
	
	Возврат Формат(Индекс + 1, "ЧГ=0");
	
КонецФункции

Функция СсылкаНаИТС(Срок = Неопределено) Экспорт
	
	Если Срок = Неопределено Тогда
		Параметры = "";
	Иначе
		Параметры = "/?date=" + Формат(Срок, "ДФ=yyyyMMdd");
	КонецЕсли;
	
	Возврат "http://its.1c.ru/calendar" + Параметры;
	
КонецФункции

Функция ОписаниеДействия(ПараметрыКоманды) Экспорт
	
	Если ТипЗнч(ПараметрыКоманды.Правило) = Тип("ПеречислениеСсылка.ЗадачиНачалаРаботы") Тогда
		
		Если ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.СписокНалоговОтчетов") Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			Если ОбщегоНазначенияБПВызовСервера.ЭтоМедленныйРежимРаботы() ИЛИ ОбщегоНазначенияКлиентСервер.ЭтоВебКлиент() Тогда
				ИдентификаторФормы = "ДлительнаяОперацияНалогиИОтчеты";
				ИмяФормы = "ОбщаяФорма.ДлительнаяОперацияНалогиИОтчеты";
				ПараметрыФормы.Вставить("РежимНастройки", Истина);
				ПараметрыФормы.Вставить("ИдентификаторФормы", ИдентификаторФормы);
			Иначе
				ИмяФормы = "ОбщаяФорма.НалогиИОтчеты";
			КонецЕсли;
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       ИмяФормы);
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.Подключение1СОтчетности") Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПредложениеОформитьЗаявлениеНаПодключение");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.Подключение1СЭДО") Тогда
			
			СпособыОЭД = Новый Массив;
			СпособыОЭД.Добавить(ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО"));
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("СпособыОбменаЭД", СпособыОЭД);
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "Справочник.ПрофилиНастроекЭДО.Форма.ПомощникПодключенияЭДО");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		ИначеЕсли ПараметрыКоманды.Правило = ПредопределенноеЗначение("Перечисление.ЗадачиНачалаРаботы.НастройкаЗаполненияФормСтатистики") Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
			
			ОписаниеДействия = Новый Структура;
			ОписаниеДействия.Вставить("ИмяФормы",       "РегистрСведений.НастройкаЗаполненияФормСтатистики.Форма.НастройкаЗаполненияФормСтатистики");
			ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
			
		Иначе
			
			Возврат Неопределено;
			
		КонецЕсли
		
	ИначеЕсли ТипЗнч(ПараметрыКоманды.Правило) = Тип("СправочникСсылка.ПроверкиКонтролирующимиОрганами") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ",        ПараметрыКоманды.Правило);
		ПараметрыФормы.Вставить("Организация", ПараметрыКоманды.Организация);
		
		ОписаниеДействия = Новый Структура;
		ОписаниеДействия.Вставить("ИмяФормы",       "Справочник.ПроверкиКонтролирующимиОрганами.Форма.ФормаПросмотра");
		ОписаниеДействия.Вставить("ПараметрыФормы", ПараметрыФормы);
		
	Иначе
		
		ОписаниеДействия = ВыполнениеЗадачБухгалтераВызовСервера.ОписаниеДействия(ПараметрыКоманды);
		
	КонецЕсли;
	
	Возврат ОписаниеДействия;
	
КонецФункции
