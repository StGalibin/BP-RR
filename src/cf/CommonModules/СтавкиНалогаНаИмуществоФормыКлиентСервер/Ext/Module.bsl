
////////////////////////////////////////////////////////////////////////////////
// Универсальные методы для формы записи регистра и формы настройки налогов
//
// Клиент-серверные методы формы записи регистра сведений СтавкиНалогаНаИмущество
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура УправлениеФормой(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	Запись   = Форма.СтавкиНалогаНаИмущество;
	
	Элементы.ЛьготыНалогаНаИмущество.ЗаголовокСвернутогоОтображения = ЗаголовокГруппыЛьготыНалогаНаИмущество(Форма);
	
	Элементы.КодЛьготыОсвобождениеОтНалогаНаИмущество.Доступность = Запись.ОсвобождениеОтНалогообложения;
	
	Элементы.ГруппаСнижениеСтавкиНалогаНаИмущество.Доступность = Не Запись.ОсвобождениеОтНалогообложения;
	Элементы.ГруппаСниженнаяСтавкаНалогаНаИмущество.Доступность = Запись.СнижениеНалоговойСтавки;
	
	Элементы.ГруппаУменьшениеСуммыНалогаНаИмущество.Доступность = Не Запись.ОсвобождениеОтНалогообложения;
	Элементы.ГруппаПроцентУменьшенияНалогаНаИмущество.Доступность = Запись.УменьшениеСуммыНалогаВПроцентах;
	
КонецПроцедуры

Функция ЗаголовокГруппыЛьготыНалогаНаИмущество(Форма)
	
	Запись = Форма.СтавкиНалогаНаИмущество;
	
	ЭлементыЗаголовка = Новый Массив;
	
	Если Запись.ОсвобождениеОтНалогообложения Тогда
		ЭлементыЗаголовка.Добавить(СтрШаблон(НСтр("ru = 'освобождение от налога (%1)'"),
			Запись.КодНалоговойЛьготыОсвобождениеОтНалогообложения));
	КонецЕсли;
	
	Если Запись.СнижениеНалоговойСтавки Тогда
		ЭлементыЗаголовка.Добавить(СтрШаблон(НСтр("ru = 'сниженная ставка налога %1%%'"),
			Запись.СниженнаяНалоговаяСтавка));
	КонецЕсли;
	
	Если Запись.УменьшениеСуммыНалогаВПроцентах Тогда
		ЭлементыЗаголовка.Добавить(СтрШаблон(НСтр("ru = 'налог уменьшен на %1%%'"),
			Запись.ПроцентУменьшения));
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Налоговые льготы'");
	Если ЭлементыЗаголовка.Количество() > 0 Тогда
		Заголовок = Заголовок + ": " + СтрСоединить(ЭлементыЗаголовка, ", ");
	КонецЕсли;
	
	Возврат Заголовок;
	
КонецФункции

#КонецОбласти
