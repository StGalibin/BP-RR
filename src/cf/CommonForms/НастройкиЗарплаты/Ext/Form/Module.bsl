&НаКлиенте
Перем ОбновитьИнтерфейс;

#Область ОбработчикиСобытийФормы

Процедура ПриСозданииНаСервере()
	
	Элементы.УчетЗарплатыИКадровВоВнешнейПрограмме.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.УчетЗарплатыИКадровВоВнешнейПрограмме);
		
	Элементы.РасчетЗарплатыДляНебольшихОрганизаций.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.РасчетЗарплатыДляНебольшихОрганизаций);
		
	Элементы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций);
	Элементы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций.Видимость = 
		НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям");
		
	Элементы.РассчитыватьДокументыПриРедактировании.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.РассчитыватьДокументыПриРедактировании);
		
	Элементы.КадровыйУчетУпрощенный.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьКадровыйУчет);
		
	Элементы.КадровыйУчетПолный.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьКадровыйУчет);
		
	Элементы.СформироватьКадровыеДокументы.Доступность = 
		ПравоДоступа("Изменение", Метаданные.Константы.ИспользоватьКадровыйУчет);
		
	УчетЗарплатыИКадровВоВнешнейПрограмме = БухгалтерскийУчетПереопределяемый.УчетЗарплатыИКадровВоВнешнейПрограмме();
	КадровыйУчет = БухгалтерскийУчетПереопределяемый.ВедетсяКадровыйУчет();
	РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций = Константы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций.Получить();

	НесколькоОрганизаций = НесколькоОрганизаций();
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Организации" Тогда
		НесколькоОрганизаций = НесколькоОрганизаций();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетЗарплатыИКадровВоВнешнейПрограммеПриИзменении(Элемент)
	
	НаборКонстант.УчетЗарплатыИКадровВоВнешнейПрограмме = 
		?(УчетЗарплатыИКадровВоВнешнейПрограмме = 1, Истина, Ложь);
	
	ИзмененныеКонстанты = УчетЗарплатыИКадровВоВнешнейПрограммеПриИзмененииНаСервере();
	
	УправлениеФормой(ЭтаФорма);
	
	ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 1, Истина);
	ОбновитьИнтерфейс = Истина;
	
	Для Каждого КонстантаИмя ИЗ ИзмененныеКонстанты Цикл
		Если КонстантаИмя <> "" Тогда
			Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядокУчетаЗарплатыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НЕ НесколькоОрганизаций Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ключ", ПолучитьКлючНастроекЗарплаты()); 
		ОткрытьФорму("РегистрСведений.НастройкиУчетаЗарплаты.ФормаЗаписи", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетЗарплатыДляНебольшихОрганизацийПриИзменении(Элемент)
	
	ПриИзмененииРеквизитаНаКлиенте(Элемент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетЗарплатыПоПодразделениямДляНебольшихОрганизацийПриИзменении(Элемент)
	
	ПриИзмененииРеквизитаНаКлиенте(Элемент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитыватьДокументыПриРедактированииПриИзменении(Элемент)
	
	ПриИзмененииРеквизитаНаКлиенте(Элемент, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КадровыйУчетУпрощенныйПриИзменении(Элемент)
	
	ПриИзмененииНастройкиКадровыйУчет();
	
КонецПроцедуры

&НаКлиенте
Процедура КадровыйУчетПолныйПриИзменении(Элемент)
	
	ПриИзмененииНастройкиКадровыйУчет();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьКадровыеДокументы(Команда)
	
	СформироватьКадровыеПриказы();			
	ПоказатьОповещениеПользователя(НСтр("ru='Формирование кадровых документов завершено.'"), "e1cib/list/ЖурналДокументов.КадровыеДокументы", "Журнал ""Приемы, увольнения, переводы""");
	
	УправлениеФормой(ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьСтандартныеНастройки(Команда)
	
	ЗарплатаКадрыКлиент.ВосстановитьНачальныеЗначения(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	ВедетсяУчетЗарплатыВЭтойПрограмме = Форма.УчетЗарплатыИКадровВоВнешнейПрограмме = 0;
	
	Элементы.ПорядокУчетаЗарплаты.Видимость                   = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.ПорядокУчетаЗарплатыВоВнешнейПрограмме.Видимость = НЕ ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.НастройкиДокументовИПечатныхФорм.Видимость       = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.РасчетЗарплатыДляНебольшихОрганизаций.Видимость  = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.РассчитыватьДокументыПриРедактировании.Видимость = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.Начисления.Видимость                             = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.Удержания.Видимость                              = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.КадровыйУчетУпрощенный.Видимость                 = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.КадровыйУчетПолный.Видимость                     = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.СтраховыеВзносы.Видимость                        = ВедетсяУчетЗарплатыВЭтойПрограмме;
	Элементы.НДФЛ.Видимость                                   = ВедетсяУчетЗарплатыВЭтойПрограмме;
	
	Элементы.СформироватьКадровыеДокументы.Видимость = Форма.КадровыйУчет = 1 И НеобходимоСформироватьКадровыеПриказы();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВосстановитьСтандартныеНастройки", "Видимость", ВедетсяУчетЗарплатыВЭтойПрограмме);		
	
КонецПроцедуры

&НаСервере
Функция УчетЗарплатыИКадровВоВнешнейПрограммеПриИзмененииНаСервере()
	
	ИзмененныеКонстанты = Новый Массив;
	
	КонстантаИмя = СохранитьЗначениеРеквизита("НаборКонстант.УчетЗарплатыИКадровВоВнешнейПрограмме");
	ИзмененныеКонстанты.Добавить(КонстантаИмя);
	
	НаборКонстант.РасчетЗарплатыДляНебольшихОрганизаций = Ложь;
	КонстантаИмя = СохранитьЗначениеРеквизита("НаборКонстант.РасчетЗарплатыДляНебольшихОрганизаций");
	ИзмененныеКонстанты.Добавить(КонстантаИмя);
	
	КадровыйУчет = 0;
	НаборКонстант.ИспользоватьКадровыйУчет = Ложь;
	КонстантаИмя = СохранитьЗначениеРеквизита("НаборКонстант.ИспользоватьКадровыйУчет");
	ИзмененныеКонстанты.Добавить(КонстантаИмя);
	
	УстановитьНастройкиУчетаЗарплаты(НаборКонстант.УчетЗарплатыИКадровВоВнешнейПрограмме);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат ИзмененныеКонстанты;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииРеквизитаНаКлиенте(Элемент, ОбновлятьИнтерфейс = Истина)
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным)
	
	// Сохранение значений реквизитов, не связанных с константами напрямую (в отношении один-к-одному).
	Если РеквизитПутьКДанным = "" Тогда
		Возврат "";
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций" Тогда
		НовоеЗначение = РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций;
		Константы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций.Установить(НовоеЗначение);
		Если Константы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций.Получить() = НовоеЗначение Тогда
			ТекстОшибки = "";
			НачатьТранзакцию();
			Если ИзменитьФлагЕстьОбособленныеПодразделенияОрганизаций(ТекстОшибки) Тогда
				ЗафиксироватьТранзакцию();
			Иначе
				Константы.РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций.Установить(НЕ НовоеЗначение);
				РасчетЗарплатыПоПодразделениямДляНебольшихОрганизаций = НЕ НовоеЗначение;
				ВызватьИсключение ТекстОшибки;
			КонецЕсли;
		КонецЕсли;
		Возврат "";
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 14)) = НРег("НаборКонстант.") Тогда
		// Если путь к данным реквизита указан через "НаборКонстант".
		КонстантаИмя = Сред(РеквизитПутьКДанным, 15);
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		КонстантаЗначение = НаборКонстант[КонстантаИмя];
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
	КонецЕсли;
	
	Если КонстантаИмя = "ИспользоватьКадровыйУчет" Тогда
		Если КонстантаЗначение Тогда
			Если НЕ Константы.ПоддержкаСовместительства.Получить() Тогда
				Константы.ПоддержкаСовместительства.Установить(Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииНастройкиКадровыйУчет()
	
	НаборКонстант.ИспользоватьКадровыйУчет = 
		?(КадровыйУчет = 1, Истина, Ложь);
	
	КонстантаИмя = ПриИзмененииНастройкиКадровыйУчетНаСервере();
	
	УправлениеФормой(ЭтотОбъект);
	
	ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 1, Истина);
	ОбновитьИнтерфейс = Истина;
	
	Если КонстантаИмя <> "" Тогда
		Оповестить("Запись_НаборКонстант", Новый Структура, КонстантаИмя);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииНастройкиКадровыйУчетНаСервере()

	РеквизитПутьКДанным = "НаборКонстант.ИспользоватьКадровыйУчет";
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;

КонецФункции

&НаСервереБезКонтекста
Функция НеобходимоСформироватьКадровыеПриказы()
	
	Возврат УчетЗарплаты.НеобходимоСформироватьКадровыеПриказы()
	
КонецФункции

&НаСервереБезКонтекста
Процедура СформироватьКадровыеПриказы()
	
	УчетЗарплаты.СформироватьКадровыеПриказы();
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКлючНастроекЗарплаты()
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Справочники.Организации.ОрганизацияПоУмолчанию());
	Возврат РегистрыСведений.НастройкиУчетаЗарплаты.СоздатьКлючЗаписи(Отбор);
	
КонецФункции

&НаСервереБезКонтекста
Функция НесколькоОрганизаций()
	
	Возврат Справочники.Организации.ИспользуетсяНесколькоОрганизаций();	
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьНастройкиУчетаЗарплаты(ВестиУчетЗарплатыИКадровВоВнешнейПрограмме)
	
	УчетЗарплаты.УстановитьНастройкиУчетаЗарплаты(ВестиУчетЗарплатыИКадровВоВнешнейПрограмме);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОжиданиеВыполненияДлительнойОперации()
	
	Если ЗарплатаКадрыКлиент.ВосстановлениеНачальныхЗначенийВыполнено(ЭтаФорма) Тогда
		ТекстСообщения = НСтр("ru = 'Стандартные настройки восстановлены.'");
		ПоказатьОповещениеПользователя(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИзменитьФлагЕстьОбособленныеПодразделенияОрганизаций(ТекстИсключения)
	
	Если ПолучитьФункциональнуюОпцию("ВестиУчетПоПодразделениям") Тогда
		Возврат Истина;
	КонецЕсли;
	
	// Для правильной установки флага ЕстьОбособленныеПодразделения перезаписываем все организации.
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Справочники.Организации.Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			ОрганизацияОбъект = Выборка.ПолучитьОбъект();
			ОрганизацияОбъект.Записать();
		Исключение
			ТекстИсключения = ТекстОшибкиИзмененияФлагаЕстьОбособленныеПодразделения();
			Возврат Ложь;
		КонецПопытки;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстОшибкиИзмененияФлагаЕстьОбособленныеПодразделения()
	
	Возврат НСтр("ru='Не удалось изменить признак наличия обособленных подразделений организаций.'");
	
КонецФункции


#КонецОбласти